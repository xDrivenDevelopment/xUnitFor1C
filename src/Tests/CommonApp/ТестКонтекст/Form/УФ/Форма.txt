&НаКлиенте
Перем ЮТест;  // для тестирования

//{ основная процедура для юнит-тестирования 1CUnit
&НаКлиенте
Функция ПолучитьСписокТестов(ЮнитТестирование) Экспорт
	
	ЮТест = ЮнитТестирование;
	
	ВсеТесты = Новый Массив;

	ВсеТесты.Добавить("ТестДолжен_УстановитьКонтекст");
	ВсеТесты.Добавить("ТестДолжен_ПрочитатьКонтекст");
	ВсеТесты.Добавить("ТестДолжен_ОчиститьКонтекст");
	ВсеТесты.Добавить("ТестДолжен_УстановитьКонтекст_БезИдентификатора");
	ВсеТесты.Добавить("ТестДолжен_ПрочитатьКонтекст_БезИдентификатора");
	ВсеТесты.Добавить("ТестДолжен_ОчиститьКонтекст_БезИдентификатора");

	Возврат ВсеТесты;
	
КонецФункции

&НаКлиенте
Функция РазрешенСлучайныйПорядокВыполненияТестов() Экспорт
	Возврат Ложь;
КонецФункции
//}

//{ блок юнит-тестов - сами тесты

&НаКлиенте
Процедура ТестДолжен_УстановитьКонтекст() Экспорт
	Контекст = Новый Структура("Ключ", 10);
	ЮТест.УстановитьКонтекст(Контекст, Идентификатор());
КонецПроцедуры

&НаКлиенте
Процедура ТестДолжен_ПрочитатьКонтекст() Экспорт
	Контекст = ЮТест.ПолучитьКонтекст(Идентификатор());
	ЮТест.ПроверитьРавенство(10, Контекст.Ключ, "передача контекста между тестами не работает");
КонецПроцедуры

&НаКлиенте
Процедура ТестДолжен_ОчиститьКонтекст() Экспорт
	Контекст = Новый Структура("Ключ", 11);
	ЮТест.УстановитьКонтекст(Контекст, Идентификатор());
	ЮТест.УстановитьКонтекст(Неопределено, Идентификатор());
	Контекст = ЮТест.ПолучитьКонтекст(Идентификатор());
	ЮТест.ПроверитьРавенство(Неопределено, Контекст, "Очистка контекста (установка пустого контекста) не работает");
КонецПроцедуры

&НаКлиенте
Процедура ТестДолжен_УстановитьКонтекст_БезИдентификатора() Экспорт
	Контекст = Новый Структура("Ключ", 12);
	ЮТест.УстановитьКонтекст(Контекст);
КонецПроцедуры

&НаКлиенте
Процедура ТестДолжен_ПрочитатьКонтекст_БезИдентификатора() Экспорт
	Контекст = ЮТест.ПолучитьКонтекст();
	ЮТест.ПроверитьРавенство(12, Контекст.Ключ, "передача контекста между тестами не работает");
КонецПроцедуры

&НаКлиенте
Процедура ТестДолжен_ОчиститьКонтекст_БезИдентификатора() Экспорт
	Контекст = Новый Структура("Ключ", 13);
	ЮТест.УстановитьКонтекст(Контекст);
	ЮТест.УстановитьКонтекст(Неопределено);
	Контекст = ЮТест.ПолучитьКонтекст();
	ЮТест.ПроверитьРавенство(Неопределено, Контекст, "Очистка контекста (установка пустого контекста) не работает");
КонецПроцедуры

&НаКлиенте
Функция Идентификатор()
	Возврат "МойКонтекст_9347";
КонецФункции

//} конец блока юнит-тестов
