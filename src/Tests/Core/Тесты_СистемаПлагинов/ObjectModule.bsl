Перем КонтекстЯдра;
Перем Утверждения;

Процедура Инициализация(КонтекстЯдраПараметр) Экспорт
	КонтекстЯдра = КонтекстЯдраПараметр;
	Утверждения = КонтекстЯдра.Плагин("БазовыеУтверждения");
КонецПроцедуры

Функция ПолучитьСписокТестов() Экспорт
	ВсеТесты = Новый Массив;
	// Core
	ВсеТесты.Добавить("ТестДолжен_ПроверитьПолучениеПлагинаПоИдентификатору");
	ВсеТесты.Добавить("ТестДолжен_ПолучитьСписокЗагруженныхПлагиновПоТипу");
	// Plugin interface
	ВсеТесты.Добавить("ТестДолжен_ПроверитьОписаниеПлагина");
	
	Возврат ВсеТесты;
КонецФункции

// { Core
Процедура ТестДолжен_ПроверитьПолучениеПлагинаПоИдентификатору() Экспорт
	ЗагрузчикТестов = КонтекстЯдра.Плагин("ЗагрузчикФайла");
	ОписаниеПлагина = ЗагрузчикТестов.ОписаниеПлагина(КонтекстЯдра.ТипыПлагинов);
	
	// Убираем из идентификатора префикс подсистемы
	ИдентификаторПлагинаБезПрефикса = КонтекстЯдра.ПолучитьИдентификаторБезПрефиксаПодсистемы(ОписаниеПлагина.Идентификатор);
	
	Утверждения.ПроверитьРавенство(ОписаниеПлагина.Тип, КонтекстЯдра.ТипыПлагинов.Загрузчик, "ОписаниеПлагина.Тип, КонтекстЯдра.ТипыПлагинов.Загрузчик");
	Утверждения.ПроверитьРавенство(ИдентификаторПлагинаБезПрефикса, "ЗагрузчикФайла", "ОписаниеПлагина.Идентификатор");
	Утверждения.ПроверитьРавенство(ОписаниеПлагина.Представление, "Загрузить тесты из файлов", "ОписаниеПлагина.Представление");
КонецПроцедуры

Процедура ТестДолжен_ПолучитьСписокЗагруженныхПлагиновПоТипу() Экспорт
	ОписанияПлагинов = КонтекстЯдра.ПолучитьОписанияПлагиновПоТипу(КонтекстЯдра.ТипыПлагинов.Загрузчик);
	
	Утверждения.ПроверитьТип(ОписанияПлагинов, "Массив", "Загрузчики");
	Утверждения.ПроверитьБольше(ОписанияПлагинов.Количество(), 0, "Загрузчики.Количество()");
	Для каждого ОписаниеПлагина Из ОписанияПлагинов Цикл
		Утверждения.ПроверитьРавенство(ОписаниеПлагина.Тип, КонтекстЯдра.ТипыПлагинов.Загрузчик, "ОписаниеПлагина.Тип, КонтекстЯдра.ТипыПлагинов.Загрузчик");
		Утверждения.ПроверитьЗаполненность(ОписаниеПлагина.Идентификатор, "ОписаниеПлагина.Идентификатор");
		Утверждения.ПроверитьЗаполненность(ОписаниеПлагина.Представление, "ОписаниеПлагина.Представление");
	КонецЦикла;
КонецПроцедуры
// } Core

// { Plugin interface
Процедура ТестДолжен_ПроверитьОписаниеПлагина() Экспорт
	ЗагрузчикТестов = КонтекстЯдра.ЗагрузчикПоУмолчанию();
	ОписаниеПлагина = ЗагрузчикТестов.ОписаниеПлагина(КонтекстЯдра.ТипыПлагинов);
	Утверждения.ПроверитьРавенство(ОписаниеПлагина.Тип, КонтекстЯдра.ТипыПлагинов.Загрузчик, "ОписаниеПлагина.Тип, КонтекстЯдра.ТипыПлагинов.Загрузчик");
	Утверждения.ПроверитьРавенство(ОписаниеПлагина.Идентификатор, "ЗагрузчикКаталога", "ОписаниеПлагина.Идентификатор");
	Утверждения.ПроверитьРавенство(ОписаниеПлагина.Представление, "Загрузить тесты из каталога файловой системы", "ОписаниеПлагина.Представление");
КонецПроцедуры
// } Plugin interface