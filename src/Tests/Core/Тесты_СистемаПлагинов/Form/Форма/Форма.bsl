&НаКлиенте
Перем КонтекстЯдра;
&НаКлиенте
Перем Утверждения;

// Префикс для объектов, встроенных в состав конфигурации
// Если браузер тестов, плагины и утилиты встроены в конфигурцаию,
// то они должны иметь один префикс
Перем ИспользуемыйПрефиксПодсистемы Экспорт;

&НаКлиенте
Процедура Инициализация(КонтекстЯдраПараметр) Экспорт
	КонтекстЯдра = КонтекстЯдраПараметр;
	Утверждения = КонтекстЯдра.Плагин("БазовыеУтверждения");
КонецПроцедуры

&НаКлиенте
Функция ПолучитьСписокТестов() Экспорт
	ВсеТесты = Новый Массив;
	
	// Plugin interface
	ВсеТесты.Добавить("ТестДолжен_ПроверитьОписаниеПлагина");
	
	// Core
	ВсеТесты.Добавить("ТестДолжен_ПроверитьПолучениеПлагинаПоИдентификатору");
	ВсеТесты.Добавить("ТестДолжен_ПолучитьСписокЗагруженныхПлагиновПоТипу");
	
	Возврат ВсеТесты;
КонецФункции

// { Plugin interface
&НаКлиенте
Процедура ТестДолжен_ПроверитьОписаниеПлагина() Экспорт
	ЗагрузчикТестов = КонтекстЯдра.ЗагрузчикПоУмолчанию();
	ВозможныеТипыПлагинов = КонтекстЯдра.Объект.ТипыПлагинов;
	ОписаниеПлагина = ЗагрузчикТестов.ОписаниеПлагина(ВозможныеТипыПлагинов);
	Утверждения.ПроверитьРавенство(ОписаниеПлагина.Тип, ВозможныеТипыПлагинов.Загрузчик, "ОписаниеПлагина.Тип, юТест.ТипыПлагинов().Загрузчик");
	Утверждения.ПроверитьРавенство(ОписаниеПлагина.Идентификатор, "ЗагрузчикКаталога", "ОписаниеПлагина.Идентификатор");
	Утверждения.ПроверитьРавенство(ОписаниеПлагина.Представление, "Загрузить тесты из каталога файловой системы", "ОписаниеПлагина.Представление");
КонецПроцедуры
// } Plugin interface

// { Core
&НаКлиенте
Процедура ТестДолжен_ПроверитьПолучениеПлагинаПоИдентификатору() Экспорт
	ЗагрузчикТестов = КонтекстЯдра.Плагин("ЗагрузчикФайла");
	ВозможныеТипыПлагинов = КонтекстЯдра.Объект.ТипыПлагинов;
	ОписаниеПлагина = ЗагрузчикТестов.ОписаниеПлагина(ВозможныеТипыПлагинов);
	Утверждения.ПроверитьРавенство(ОписаниеПлагина.Тип, ВозможныеТипыПлагинов.Загрузчик, "ОписаниеПлагина.Тип, юТест.ТипыПлагинов().Загрузчик");
	Утверждения.ПроверитьРавенство(ОписаниеПлагина.Идентификатор, "ЗагрузчикФайла", "ОписаниеПлагина.Идентификатор");
	Утверждения.ПроверитьРавенство(ОписаниеПлагина.Представление, "Загрузить тесты из файлов", "ОписаниеПлагина.Представление");
КонецПроцедуры

&НаКлиенте
Процедура ТестДолжен_ПолучитьСписокЗагруженныхПлагиновПоТипу() Экспорт
	ВозможныеТипыПлагинов = КонтекстЯдра.Объект.ТипыПлагинов;
	ОписанияЗагрузчиков = КонтекстЯдра.ПолучитьОписанияПлагиновПоТипу(ВозможныеТипыПлагинов.Загрузчик);
	
	Утверждения.ПроверитьТип(ОписанияЗагрузчиков, "Массив", "Загрузчики");
	Утверждения.ПроверитьБольше(ОписанияЗагрузчиков.Количество(), 0, "Загрузчики.Количество()");
	Для каждого ОписаниеПлагина Из ОписанияЗагрузчиков Цикл
		Утверждения.ПроверитьРавенство(ОписаниеПлагина.Тип, ВозможныеТипыПлагинов.Загрузчик, "ОписаниеПлагина.Тип, юТест.ТипыПлагинов().Загрузчик");
		Утверждения.ПроверитьЗаполненность(ОписаниеПлагина.Идентификатор, "ОписаниеПлагина.Идентификатор");
		Утверждения.ПроверитьЗаполненность(ОписаниеПлагина.Представление, "ОписаниеПлагина.Представление");
	КонецЦикла;
КонецПроцедуры
// } Core
