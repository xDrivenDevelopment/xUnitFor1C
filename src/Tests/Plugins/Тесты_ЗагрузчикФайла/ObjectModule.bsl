Перем КонтекстЯдра;
Перем Утверждения;
Перем Ожидаем;
Перем ЗагрузчикФайла;
Перем ВременныеФайлы;

Процедура Инициализация(КонтекстЯдраПараметр) Экспорт
	КонтекстЯдра = КонтекстЯдраПараметр;
	Ожидаем = КонтекстЯдра.Плагин("УтвержденияBDD");
КонецПроцедуры

Функция ПолучитьСписокТестов() Экспорт
	ВсеТесты = Новый Массив;
	// Loader interface
	ВсеТесты.Добавить("ТестДолжен_Загрузить_ОдинФайл");
	ВсеТесты.Добавить("ТестДолжен_Загрузить_ДваФайла");
	
	// Проверка работы старого интерфейса объявления параметризованных тестов
	ВсеТесты.Добавить(Новый Структура("ИмяТеста", "ТестДолжен_ПроверитьОписаниеТестаЧерезСтруктуру_ЗаполненоТолькоИмяТеста"));
	ВсеТесты.Добавить(Новый Структура("ПредставлениеТеста, ИмяТеста", "", "ТестДолжен_ПроверитьОписаниеТестаЧерезСтруктуру_ЗаполненоИмяИПредставлениеТеста"));
	ВсеТесты.Добавить(Новый Структура("ПредставлениеТеста, ИмяТеста, Параметр", "Тест с параметрами должен сложить 7 и 5", "ТестСПараметрамиДолжен_ВыполнитьСложениеПереданныхЗначенийИСверитьСРезультатом", Новый Структура("Знач1, Знач2, Результат", 7, 5, 12)));
	ВсеТесты.Добавить(Новый Структура("ПредставлениеТеста, ИмяТеста, Параметр", "Тест с параметрами должен сложить ""А"" и ""Б""", "ТестСПараметрамиДолжен_ВыполнитьСложениеПереданныхЗначенийИСверитьСРезультатом", Новый Структура("Знач1, Знач2, Результат", "А", "Б", "АБ")));
	
	// Проверка режимов случайного или строгого порядка выполнения тестов
	ВсеТесты.Добавить("ТестДолжен_ПроверитьРежим_СлучайногоПорядкаВыполнения");
	ВсеТесты.Добавить("ТестДолжен_ПроверитьРежим_СтрогогоПорядкаВыполнения");
	
	// Проверка загрузки тестовых обработок с новым API
	ВсеТесты.Добавить("ТестДолжен_Загрузить_ОбработкуСНовымAPI");
	ВсеТесты.Добавить("ТестДолжен_Сформировать_ПараметрыТеста_Простые");
	ВсеТесты.Добавить("ТестДолжен_Сформировать_ПараметрыТеста_СНеопределеноВСередине");
	ВсеТесты.Добавить("ТестДолжен_Сформировать_ПараметрыТеста_СНесколькимиНеопределеноВНачале");
	ВсеТесты.Добавить("ТестДолжен_Сформировать_ПараметрыТеста_СЕдинственнымПараметромНеопределено");
	
	Возврат ВсеТесты;
КонецФункции

Процедура ПередЗапускомТеста() Экспорт
	ВременныеФайлы = Новый Массив;
	ЗагрузчикФайла = КонтекстЯдра.Плагин("ЗагрузчикФайла");
КонецПроцедуры

Процедура ПослеЗапускаТеста() Экспорт
	ЗагрузчикФайла = Неопределено;
	УдалитьВременныеФайлы();
КонецПроцедуры

// { Helpers
Функция НовыйВременныйФайл(Расширение) Экспорт
	ВременныйФайл = Новый Файл(ПолучитьИмяВременногоФайла(Расширение));
	ВременныеФайлы.Добавить(ВременныйФайл);
	
	Возврат ВременныйФайл;
КонецФункции 

Процедура УдалитьВременныеФайлы()
	Для каждого ВременныйФайл Из ВременныеФайлы Цикл
		Попытка
			УдалитьФайлы(ВременныйФайл.ПолноеИмя);
		Исключение
			Сообщить("Не удален временный файл: " + ВременныйФайл.ПолноеИмя + "
			|-" + ОписаниеОшибки());
		КонецПопытки;
	КонецЦикла;
	ВременныеФайлы.Очистить();
КонецПроцедуры
// } Helpers

// { Loader interface
Процедура ТестДолжен_Загрузить_ОдинФайл() Экспорт
	ФайлСТестами = НовыйВременныйФайл(".epf");
	ЭтотОбъект.ПолучитьМакет("ТестовыйНаборДляЗагрузчикаФС").Записать(ФайлСТестами.ПолноеИмя);
	
	ДеревоТестов = ЗагрузчикФайла.Загрузить(КонтекстЯдра, ФайлСТестами.ПолноеИмя);
	
	Ожидаем.Что(ДеревоТестов, "ДеревоТестов").ИмеетТип("Структура");
	Ожидаем.Что(ДеревоТестов.Строки, "Узлы верхнего уровня").ИмеетДлину(1);
	
	Контейнер = ДеревоТестов.Строки[0];
	ПроверитьКонтейнер(Контейнер, ФайлСТестами);
КонецПроцедуры

Процедура ПроверитьКонтейнер(Контейнер, ФайлСТестами)
	ТипыУзловДереваТестов = КонтекстЯдра.Плагин("ПостроительДереваТестов").ТипыУзловДереваТестов;
	
	Ожидаем.Что(Контейнер.Тип, "Контейнер.Тип").Равно(ТипыУзловДереваТестов.Контейнер);
	Ожидаем.Что(Контейнер.Имя, "Контейнер.Имя").Равно(ФайлСТестами.ИмяБезРасширения);
	Ожидаем.Что(Контейнер.Строки, "Контейнер.Строки").ИмеетТип("Массив").ИмеетДлину(3);
	
	Элемент1 = Контейнер.Строки[0];
	Ожидаем.Что(Элемент1.Тип, "Элемент1.Тип").Равно(ТипыУзловДереваТестов.Элемент);
	Ожидаем.Что(Элемент1.Путь, "Элемент1.Путь").Равно(ФайлСТестами.ПолноеИмя);
	Ожидаем.Что(Элемент1.ИмяМетода, "Элемент1.ИмяМетода").Равно("УспешныйТест");
	
	Элемент2 = Контейнер.Строки[1];
	Ожидаем.Что(Элемент2.Тип, "Элемент2.Тип").Равно(ТипыУзловДереваТестов.Элемент);
	Ожидаем.Что(Элемент2.Путь, "Элемент2.Путь").Равно(ФайлСТестами.ПолноеИмя);
	Ожидаем.Что(Элемент2.ИмяМетода, "Элемент2.ИмяМетода").Равно("ПроваленныйТест");
	
	Элемент3 = Контейнер.Строки[2];
	Ожидаем.Что(Элемент3.Тип, "Элемент3.Тип").Равно(ТипыУзловДереваТестов.Элемент);
	Ожидаем.Что(Элемент3.Путь, "Элемент3.Путь").Равно(ФайлСТестами.ПолноеИмя);
	Ожидаем.Что(Элемент3.ИмяМетода, "Элемент3.ИмяМетода").Равно("НесуществующийТест");
КонецПроцедуры

Процедура ТестДолжен_Загрузить_ДваФайла() Экспорт
	ФайлСТестами1 = НовыйВременныйФайл(".epf");
	ЭтотОбъект.ПолучитьМакет("ТестовыйНаборДляЗагрузчикаФС").Записать(ФайлСТестами1.ПолноеИмя);
	
	ФайлСТестами2 = НовыйВременныйФайл(".epf");
	ЭтотОбъект.ПолучитьМакет("ТестовыйНаборДляЗагрузчикаФС").Записать(ФайлСТестами2.ПолноеИмя);
	
	ПутьКНесколькимФайлам = ФайлСТестами1.ПолноеИмя + Символы.ПС + ФайлСТестами2.ПолноеИмя;
	
	ДеревоТестов = ЗагрузчикФайла.Загрузить(КонтекстЯдра, ПутьКНесколькимФайлам);
	
	Ожидаем.Что(ДеревоТестов, "ДеревоТестов").ИмеетТип("Структура");
	Ожидаем.Что(ДеревоТестов.Строки, "Количество узлов верхнего уровня").ИмеетДлину(2);
	
	Контейнер = ДеревоТестов.Строки[0];
	ПроверитьКонтейнер(Контейнер, ФайлСТестами1);
	
	Контейнер = ДеревоТестов.Строки[1];
	ПроверитьКонтейнер(Контейнер, ФайлСТестами2);
КонецПроцедуры
// } Loader interface

// { Проверка работы старого интерфейса объявления параметризованных тестов
Процедура ТестДолжен_ПроверитьОписаниеТестаЧерезСтруктуру_ЗаполненоТолькоИмяТеста() Экспорт
	// Визуальный контроль в дереве тестов
КонецПроцедуры

Процедура ТестДолжен_ПроверитьОписаниеТестаЧерезСтруктуру_ЗаполненоИмяИПредставлениеТеста() Экспорт
	// Визуальный контроль в дереве тестов
КонецПроцедуры

Процедура ТестСПараметрамиДолжен_ВыполнитьСложениеПереданныхЗначенийИСверитьСРезультатом(Параметр) Экспорт
	Ожидаем.Что(Параметр).ИмеетТип("Структура");
	Ожидаем.Что(Параметр.Знач1 + Параметр.Знач2).Равно(Параметр.Результат);
КонецПроцедуры
// } Проверка работы старого интерфейса объявления параметризованных тестов

// { Проверка режимов случайного или строгого порядка выполнения тестов
Процедура ТестДолжен_ПроверитьРежим_СлучайногоПорядкаВыполнения() Экспорт
	ФайлСТестами = НовыйВременныйФайл(".epf");
	ЭтотОбъект.ПолучитьМакет("ТестовыйНаборДляЗагрузчикаФС").Записать(ФайлСТестами.ПолноеИмя);
	ДеревоТестов = ЗагрузчикФайла.Загрузить(КонтекстЯдра, ФайлСТестами.ПолноеИмя);
	Контейнер = ДеревоТестов.Строки[0];
	
	Ожидаем.Что(ДеревоТестов.СлучайныйПорядокВыполнения, "ДеревоТестов").ЭтоИстина();
	Ожидаем.Что(Контейнер.СлучайныйПорядокВыполнения, "Контейнер").ЭтоИстина();
КонецПроцедуры

Процедура ТестДолжен_ПроверитьРежим_СтрогогоПорядкаВыполнения() Экспорт
	ФайлСТестами = НовыйВременныйФайл(".epf");
	ЭтотОбъект.ПолучитьМакет("ТестовыйНаборСоСтрогимПорядкомВыполнения").Записать(ФайлСТестами.ПолноеИмя);
	ДеревоТестов = ЗагрузчикФайла.Загрузить(КонтекстЯдра, ФайлСТестами.ПолноеИмя);
	Контейнер = ДеревоТестов.Строки[0];
	
	Ожидаем.Что(ДеревоТестов.СлучайныйПорядокВыполнения, "ДеревоТестов").ЭтоИстина();
	Ожидаем.Что(Контейнер.СлучайныйПорядокВыполнения, "Контейнер").ЭтоЛожь();
КонецПроцедуры
// } Проверка режимов случайного или строгого порядка выполнения тестов

// { Проверка загрузки тестовых обработок с новым API
Процедура ТестДолжен_Загрузить_ОбработкуСНовымAPI() Экспорт
	ФайлКонтекстаЯдра = Новый Файл(КонтекстЯдра.ИспользуемоеИмяФайла);
	// сначала проверяем полный путь (для обычного приложения или толстого клиента), затем имя файла (тонкий клиент,клиент-сервер)
	Попытка
		ФайлОбработкиСНовымAPI = Новый Файл(ФайлКонтекстаЯдра.Путь + "\Tests\Plugins\Тесты_ЗагрузчикФайла_НовыйAPIОбъявленияТестов.epf");
		ДеревоТестов = ЗагрузчикФайла.Загрузить(КонтекстЯдра, ФайлОбработкиСНовымAPI.ПолноеИмя);
	Исключение
		ДеревоТестов = ЗагрузчикФайла.Загрузить(КонтекстЯдра, "Тесты_ЗагрузчикФайла_НовыйAPIОбъявленияТестов");
	КонецПопытки;
	
	ТестыОбработкиСНовымAPI = ДеревоТестов.Строки[0];
	
	Ожидаем.Что(ТестыОбработкиСНовымAPI.СлучайныйПорядокВыполнения, "ТестыОбработкиСНовымAPI порядок выполнения").ЭтоИстина();
	Ожидаем.Что(ТестыОбработкиСНовымAPI.Строки, "ТестыОбработкиСНовымAPI количество дочерних узлов").ИмеетДлину(4);
	
	Элемент1 = ТестыОбработкиСНовымAPI.Строки[0];
	Ожидаем.Что(Элемент1.Тип, "Элемент1.Тип").Равно(КонтекстЯдра.ТипыУзловДереваТестов.Элемент);
	Ожидаем.Что(Элемент1.Путь, "Элемент1.Путь").Равно(ФайлОбработкиСНовымAPI.ПолноеИмя);
	Ожидаем.Что(Элемент1.ИмяМетода, "Элемент1.ИмяМетода").Равно("ТестДолжен_ПроверитьВыполнение_ПростогоТеста");
	
	ДочернийКонтейнер1 = ТестыОбработкиСНовымAPI.Строки[1];
	Ожидаем.Что(ДочернийКонтейнер1.Тип, "ДочернийКонтейнер1.Тип").Равно(КонтекстЯдра.ТипыУзловДереваТестов.Контейнер);
	Ожидаем.Что(ДочернийКонтейнер1.СлучайныйПорядокВыполнения, "ДочернийКонтейнер1 порядок выполнения").ЭтоИстина();
	Ожидаем.Что(ДочернийКонтейнер1.Имя, "ДочернийКонтейнер1.Имя").Равно("Группа со случайным порядком выполнения");
	Ожидаем.Что(ДочернийКонтейнер1.Строки, "ДочернийКонтейнер1.Строки").ИмеетТип("Массив").ИмеетДлину(2);
	
	ДочернийКонтейнер2 = ТестыОбработкиСНовымAPI.Строки[2];
	Ожидаем.Что(ДочернийКонтейнер2.Тип, "ДочернийКонтейнер2.Тип").Равно(КонтекстЯдра.ТипыУзловДереваТестов.Контейнер);
	Ожидаем.Что(ДочернийКонтейнер2.СлучайныйПорядокВыполнения, "ДочернийКонтейнер2 порядок выполнения").ЭтоЛожь();
	Ожидаем.Что(ДочернийКонтейнер2.Имя, "ДочернийКонтейнер2.Имя").Равно("Группа со строгим порядком выполнения");
	Ожидаем.Что(ДочернийКонтейнер2.Строки, "ДочернийКонтейнер2.Строки").ИмеетТип("Массив").ИмеетДлину(2);
	
	ДочернийКонтейнер3 = ТестыОбработкиСНовымAPI.Строки[3];
	Ожидаем.Что(ДочернийКонтейнер3.Тип, "ДочернийКонтейнер3.Тип").Равно(КонтекстЯдра.ТипыУзловДереваТестов.Контейнер);
	Ожидаем.Что(ДочернийКонтейнер3.СлучайныйПорядокВыполнения, "ДочернийКонтейнер3 порядок выполнения").ЭтоЛожь();
	Ожидаем.Что(ДочернийКонтейнер3.Имя, "ДочернийКонтейнер3.Имя").Равно("Группа параметризированных тестов со строгим порядком выполнения");
	Ожидаем.Что(ДочернийКонтейнер3.Строки, "ДочернийКонтейнер3.Строки").ИмеетТип("Массив").ИмеетДлину(2);
КонецПроцедуры

Процедура ТестДолжен_Сформировать_ПараметрыТеста_Простые() Экспорт
	Параметры = ЗагрузчикФайла.ПараметрыТеста(1, 2, 3, 4, 5, 6, 7, 8, 9);
	Ожидаем.Что(Параметры, "Параметры").ИмеетТип("Массив").ИмеетДлину(9);
	Ожидаем.Что(Параметры[0], "Параметры[0]").Равно(1);
	Ожидаем.Что(Параметры[4], "Параметры[0]").Равно(5);
	Ожидаем.Что(Параметры[8], "Параметры[0]").Равно(9);
КонецПроцедуры

Процедура ТестДолжен_Сформировать_ПараметрыТеста_СНеопределеноВСередине() Экспорт
	Параметры = ЗагрузчикФайла.ПараметрыТеста(Истина, , Ложь);
	Ожидаем.Что(Параметры, "Параметры").ИмеетДлину(3);
	Ожидаем.Что(Параметры[0], "Параметры[0]").ЭтоИстина();
	Ожидаем.Что(Параметры[1], "Параметры[1]").ЭтоНеопределено();
	Ожидаем.Что(Параметры[2], "Параметры[2]").ЭтоЛожь();
КонецПроцедуры

Процедура ТестДолжен_Сформировать_ПараметрыТеста_СНесколькимиНеопределеноВНачале() Экспорт
	Параметры = ЗагрузчикФайла.ПараметрыТеста(, , Истина, Ложь);
	Ожидаем.Что(Параметры, "Параметры").ИмеетДлину(4);
	Ожидаем.Что(Параметры[0], "Параметры[0]").ЭтоНеопределено();
	Ожидаем.Что(Параметры[1], "Параметры[1]").ЭтоНеопределено();
	Ожидаем.Что(Параметры[2], "Параметры[2]").ЭтоИстина();
	Ожидаем.Что(Параметры[3], "Параметры[3]").ЭтоЛожь();
КонецПроцедуры

Процедура ТестДолжен_Сформировать_ПараметрыТеста_СЕдинственнымПараметромНеопределено() Экспорт
	Параметры = ЗагрузчикФайла.ПараметрыТеста(Неопределено);
	Ожидаем.Что(Параметры, "Параметры").ИмеетДлину(1);
	Ожидаем.Что(Параметры[0], "Параметры[0]").ЭтоНеопределено();
КонецПроцедуры
// } Проверка загрузки тестовых обработок с новым API
