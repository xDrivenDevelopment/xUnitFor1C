Перем КонтекстЯдра;
Перем Утверждения;
Перем ЗагрузчикКаталога;
Перем ВременныеФайлы;

Процедура Инициализация(КонтекстЯдраПараметр) Экспорт
	КонтекстЯдра = КонтекстЯдраПараметр;
	Утверждения = КонтекстЯдра.Плагин("БазовыеУтверждения");
КонецПроцедуры

Функция ПолучитьСписокТестов() Экспорт
	ВсеТесты = Новый Массив;
	ВсеТесты.Добавить("ТестДолжен_ЗагрузитьКаталогСОднимФайлом");
	ВсеТесты.Добавить("ТестДолжен_ЗагрузитьКаталогСДвумяФайлами");
	ВсеТесты.Добавить("ТестДолжен_ЗагрузитьКаталогСИерархией");
	
	Возврат ВсеТесты;
КонецФункции

Процедура ПередЗапускомТеста() Экспорт
	ВременныеФайлы = Новый Массив;
	ЗагрузчикКаталога = КонтекстЯдра.Плагин("ЗагрузчикКаталога");
КонецПроцедуры

Процедура ПослеЗапускаТеста() Экспорт
	ЗагрузчикФайла = Неопределено;
	УдалитьВременныеФайлы();
КонецПроцедуры

// { Helpers
Функция НовыйВременныйФайл(Расширение) Экспорт
	ВременныйФайл = Новый Файл(ПолучитьИмяВременногоФайла(Расширение));
	ВременныеФайлы.Добавить(ВременныйФайл);
	
	Возврат ВременныйФайл;
КонецФункции 

Процедура УдалитьВременныеФайлы()
	Для каждого ВременныйФайл Из ВременныеФайлы Цикл
		Попытка
			УдалитьФайлы(ВременныйФайл.ПолноеИмя);
		Исключение
			Сообщить("Не удален временный файл: " + ВременныйФайл.ПолноеИмя + "
			|-" + ОписаниеОшибки());
		КонецПопытки;
	КонецЦикла;
	ВременныеФайлы.Очистить();
КонецПроцедуры
// } Helpers

Процедура ТестДолжен_ЗагрузитьКаталогСОднимФайлом() Экспорт
	ВременныйКаталог = НовыйВременныйФайл("");
	СоздатьКаталог(ВременныйКаталог.ПолноеИмя);
	
	ФайлСТестами = Новый Файл(ВременныйКаталог.ПолноеИмя + "\ФайлСТестами.epf");
	ЭтотОбъект.ПолучитьМакет("ТестовыйНаборДляЗагрузчикаФС").Записать(ФайлСТестами.ПолноеИмя);
	
	ДеревоТестов = ЗагрузчикКаталога.Загрузить(КонтекстЯдра, ВременныйКаталог.ПолноеИмя);
	
	Утверждения.ПроверитьТип(ДеревоТестов, "Структура", "ДеревоТестов");
	Утверждения.ПроверитьРавенство(ДеревоТестов.Имя, ВременныйКаталог.ПолноеИмя, "ДеревоТестов.Имя");
	Утверждения.ПроверитьРавенство(ДеревоТестов.Строки.Количество(), 1, "Узлы верхнего уровня");
	
	Контейнер = ДеревоТестов.Строки[0];
	ПроверитьКонтейнер(Контейнер, ФайлСТестами);
КонецПроцедуры

Процедура ПроверитьКонтейнер(Контейнер, ФайлСТестами)
	ТипыУзловДереваТестов = КонтекстЯдра.Плагин("ПостроительДереваТестов").ТипыУзловДереваТестов;
	
	Утверждения.ПроверитьРавенство(Контейнер.Тип, ТипыУзловДереваТестов.Контейнер, "Контейнер.Тип");
	Утверждения.ПроверитьРавенство(Контейнер.Имя, ФайлСТестами.ИмяБезРасширения, "Контейнер.Имя");
	Утверждения.ПроверитьТип(Контейнер.Строки, "Массив", "Контейнер.Строки");
	Утверждения.ПроверитьРавенство(Контейнер.Строки.Количество(), 3, "Контейнер.Строки.Количество()");
	
	Элемент1 = Контейнер.Строки[0];
	Утверждения.ПроверитьРавенство(Элемент1.Тип, ТипыУзловДереваТестов.Элемент, "Элемент1.Тип");
	Утверждения.ПроверитьРавенство(Элемент1.Путь, ФайлСТестами.ПолноеИмя, "Элемент1.Путь");
	Утверждения.ПроверитьРавенство(Элемент1.ИмяМетода, "УспешныйТест", "Элемент1.ИмяМетода");
	
	Элемент3 = Контейнер.Строки[2];
	Утверждения.ПроверитьРавенство(Элемент3.Тип, ТипыУзловДереваТестов.Элемент, "Элемент3.Тип");
	Утверждения.ПроверитьРавенство(Элемент3.Путь, ФайлСТестами.ПолноеИмя, "Элемент3.Путь");
	Утверждения.ПроверитьРавенство(Элемент3.ИмяМетода, "НесуществующийТест", "Элемент3.ИмяМетода");
КонецПроцедуры

Процедура ТестДолжен_ЗагрузитьКаталогСДвумяФайлами() Экспорт
	ВременныйКаталог = НовыйВременныйФайл("");
	СоздатьКаталог(ВременныйКаталог.ПолноеИмя);
	
	ФайлСТестами1 = Новый Файл(ВременныйКаталог.ПолноеИмя + "\ФайлСТестами1.epf");
	ЭтотОбъект.ПолучитьМакет("ТестовыйНаборДляЗагрузчикаФС").Записать(ФайлСТестами1.ПолноеИмя);
	
	ФайлСТестами2 = Новый Файл(ВременныйКаталог.ПолноеИмя + "\ФайлСТестами2.epf");
	ЭтотОбъект.ПолучитьМакет("ТестовыйНаборДляЗагрузчикаФС").Записать(ФайлСТестами2.ПолноеИмя);
	
	ДеревоТестов = ЗагрузчикКаталога.Загрузить(КонтекстЯдра, ВременныйКаталог.ПолноеИмя);
	
	Утверждения.ПроверитьТип(ДеревоТестов, "Структура", "ДеревоТестов");
	Утверждения.ПроверитьРавенство(ДеревоТестов.Имя, ВременныйКаталог.ПолноеИмя, "ДеревоТестов.Имя");
	Утверждения.ПроверитьРавенство(ДеревоТестов.Строки.Количество(), 2, "Узлы верхнего уровня");
	
	Контейнер1 = ДеревоТестов.Строки[0];
	ПроверитьКонтейнер(Контейнер1, ФайлСТестами1);
	
	Контейнер2 = ДеревоТестов.Строки[1];
	ПроверитьКонтейнер(Контейнер2, ФайлСТестами2);
КонецПроцедуры

Процедура ТестДолжен_ЗагрузитьКаталогСИерархией() Экспорт
	ВременныйКаталог = НовыйВременныйФайл("");
	СоздатьКаталог(ВременныйКаталог.ПолноеИмя);
	
	ФайлСТестами1 = Новый Файл(ВременныйКаталог.ПолноеИмя + "\ФайлСТестами1.epf");
	ЭтотОбъект.ПолучитьМакет("ТестовыйНаборДляЗагрузчикаФС").Записать(ФайлСТестами1.ПолноеИмя);
	
	ДочернийКаталог = Новый Файл(ВременныйКаталог.ПолноеИмя + "\ДочернийКаталог");
	СоздатьКаталог(ДочернийКаталог.ПолноеИмя);
	
	ФайлСТестами2 = Новый Файл(ДочернийКаталог.ПолноеИмя + "\ФайлСТестами2.epf");
	ЭтотОбъект.ПолучитьМакет("ТестовыйНаборДляЗагрузчикаФС").Записать(ФайлСТестами2.ПолноеИмя);
	
	ДеревоТестов = ЗагрузчикКаталога.Загрузить(КонтекстЯдра, ВременныйКаталог.ПолноеИмя);
	
	Утверждения.ПроверитьТип(ДеревоТестов, "Структура", "ДеревоТестов");
	Утверждения.ПроверитьРавенство(ДеревоТестов.Имя, ВременныйКаталог.ПолноеИмя, "ДеревоТестов.Имя");
	Утверждения.ПроверитьРавенство(ДеревоТестов.Строки.Количество(), 2, "Узлы верхнего уровня");
	
	Контейнер1 = ДеревоТестов.Строки[0];
	Утверждения.ПроверитьТип(Контейнер1, "Структура", "ДеревоТестов");
	Утверждения.ПроверитьРавенство(Контейнер1.Имя, ДочернийКаталог.Имя, "ДеревоТестов.Имя");
	Утверждения.ПроверитьРавенство(Контейнер1.Строки.Количество(), 1, "Контейнер1.Строки.Количество()");
	Контейнер1_1 = Контейнер1.Строки[0];
	ПроверитьКонтейнер(Контейнер1_1, ФайлСТестами2);
	
	Контейнер2 = ДеревоТестов.Строки[1];
	ПроверитьКонтейнер(Контейнер2, ФайлСТестами1);
КонецПроцедуры
