Перем КонтекстЯдра;
Перем Ожидаем;

Перем ЖурналВыполнения;

Процедура Инициализация(КонтекстЯдраПараметр) Экспорт
	КонтекстЯдра = КонтекстЯдраПараметр;
	Ожидаем = КонтекстЯдра.Плагин("УтвержденияBDD");
КонецПроцедуры

Процедура ЗаполнитьНаборТестов(НаборТестов) Экспорт
	// Устанавливает режим выполнения для контейнера обработки
	НаборТестов.СлучайныйПорядокВыполнения();
	//НаборТестов.СтрогийПорядокВыполнения();
	
	НаборТестов.Добавить("ТестДолжен_ПроверитьВыполнение_ПростогоТеста");
	
	НаборТестов.НачатьГруппу("Группа со случайным порядком выполнения");
	НаборТестов.Добавить("ТестДолжен_ПроверитьВыполнение_ПростогоТеста_СПроизвольнымПредставлением", , "Тест с произвольным представлением");
	НаборТестов.Добавить("ТестДолжен_ПроверитьВыполнение_ТестаСПараметром", НаборТестов.ПараметрыТеста(Истина));
	
	НаборТестов.НачатьГруппу("Группа со строгим порядком выполнения", Истина);
	НаборТестов.Добавить("ТестДолжен_СохранитьКонтекст");
	НаборТестов.Добавить("ТестДолжен_ПроверитьСохраненныйКонтекст");
	
	НаборТестов.НачатьГруппу("Группа параметризированных тестов со строгим порядком выполнения", Истина);
	НаборТестов.Добавить("ТестДолжен_СохранитьРезультатСложенияВКонтекст", НаборТестов.ПараметрыТеста(5, 7), "Тест должен сохранить результат сложения (5 + 7) в контекст");
	Тест = НаборТестов.Добавить("ТестДолжен_СравнитьКонтекстСоСвоимПараметром");
	Тест.Параметры.Добавить(12);
	
	НаборТестов.НачатьГруппу("Группа со строгим порядком выполнения и вызовом деструктора группы", Истина);
	НаборТестов.Добавить("КонструкторСценария", ,"Начало сценария");
	НаборТестов.Добавить("ТестДолжен_ПроверитьЖурналВыполнения");
	НаборТестов.ДобавитьДеструктор("ДеструкторСценария", "Обязательное завершение сценария");
КонецПроцедуры

Процедура КонструкторСценария() Экспорт
	ЖурналВыполнения = "КонструкторСценария;";
КонецПроцедуры

Процедура ТестДолжен_ПроверитьЖурналВыполнения() Экспорт
	Ожидаем.Что(ЖурналВыполнения, "ТестДолжен_ПроверитьЖурналВыполнения").Равно("КонструкторСценария;");
	ЖурналВыполнения = ЖурналВыполнения + "ПроверитьЖурналВыполнения;";
	//Сообщить("ТестДолжен_ПроверитьЖурналВыполнения -- ЖурналВыполнения " + ЖурналВыполнения);
КонецПроцедуры

Процедура ДеструкторСценария() Экспорт
	//Сообщить("ПослеЗапускаГруппыТестов -- ЖурналВыполнения " + ЖурналВыполнения);
	Ожидаем.Что(ЖурналВыполнения, "ДеструкторСценария").Равно("КонструкторСценария;ПроверитьЖурналВыполнения;");
	ЖурналВыполнения = ЖурналВыполнения + "ДеструкторСценария;";
КонецПроцедуры

Процедура ТестДолжен_ПроверитьВыполнение_ПростогоТеста() Экспорт
	Ожидаем.Что(Истина).ЭтоИстина();
КонецПроцедуры

Процедура ТестДолжен_ПроверитьВыполнение_ПростогоТеста_СПроизвольнымПредставлением() Экспорт
	Ожидаем.Что(Истина).ЭтоИстина();
КонецПроцедуры

Процедура ТестДолжен_ПроверитьВыполнение_ТестаСПараметром(Параметр) Экспорт
	Ожидаем.Что(Параметр).ЭтоИстина();
КонецПроцедуры

// { Группа со строгим режимом выполнения
Процедура ТестДолжен_СохранитьКонтекст() Экспорт
	КонтекстЯдра.СохранитьКонтекст(10);
КонецПроцедуры

Процедура ТестДолжен_ПроверитьСохраненныйКонтекст() Экспорт
	Контекст = КонтекстЯдра.ПолучитьКонтекст();
	Ожидаем.Что(Контекст).Равно(10);
КонецПроцедуры
// } Группа со строгим режимом выполнения

// { Группа параметризированных тестов со строгим режимом выполнения
Процедура ТестДолжен_СохранитьРезультатСложенияВКонтекст(Знач1, Знач2) Экспорт
	КонтекстЯдра.СохранитьКонтекст(Знач1 + Знач2);
КонецПроцедуры

Процедура ТестДолжен_СравнитьКонтекстСоСвоимПараметром(Результат) Экспорт
	Контекст = КонтекстЯдра.ПолучитьКонтекст();
	Ожидаем.Что(Контекст).Равно(Результат);
КонецПроцедуры
// } Группа параметризированных тестов со строгим режимом выполнения
