Перем КонтекстЯдра;
Перем Ожидаем;

Перем ПарсерКоманднойСтроки;

Процедура Инициализация(КонтекстЯдраПараметр) Экспорт
	КонтекстЯдра = КонтекстЯдраПараметр;
	Ожидаем = КонтекстЯдра.Плагин("УтвержденияBDD");
	ПарсерКоманднойСтроки = КонтекстЯдра.Плагин("ПарсерКоманднойСтроки");
КонецПроцедуры

Функция ПолучитьСписокТестов() Экспорт
	ВсеТесты = Новый Массив;
	ВсеТесты.Добавить("ТестДолжен_Разобрать_ТиповыеПараметрыЗапуска");
	ВсеТесты.Добавить("ТестДолжен_Разобрать_ПутьСодержитПробелыИОбрамленКовычками");
	ВсеТесты.Добавить("ТестДолжен_Разобрать_ПутьНеСодержитПробелыНоОбрамленКавычками");
	
	Возврат ВсеТесты;
КонецФункции

Процедура ТестДолжен_Разобрать_ТиповыеПараметрыЗапуска() Экспорт
	СтрокаПараметров = "xddRun ЗагрузчикКаталога %ПутьКТестам%; xddShutdown; xddReport ГенераторОтчетаJUnitXML %ПутьОтчета%";
	
	ПараметрыЗапуска = ПарсерКоманднойСтроки.Разобрать(СтрокаПараметров);
	
	Ожидаем.Что(ПараметрыЗапуска).ИмеетТип("ФиксированнаяСтруктура").ИмеетДлину(3);
	
	Параметры_xddRun = Неопределено;
	Ожидаем.Что(ПараметрыЗапуска.Свойство("xddRun", Параметры_xddRun)).ЭтоИстина();
	Ожидаем.Что(Параметры_xddRun).ИмеетТип("ФиксированныйМассив").ИмеетДлину(2);
	ИдентификаторЗагрузчика = Параметры_xddRun[0];
	Ожидаем.Что(НРег(ИдентификаторЗагрузчика)).Равно(НРег("ЗагрузчикКаталога"));
	Плагин_Загрузчик = КонтекстЯдра.Плагин(ИдентификаторЗагрузчика);
	Ожидаем.Что(Плагин_Загрузчик, "Плагин_Загрузчик").Существует();
	Путь = Параметры_xddRun[1];
	Ожидаем.Что(НРег(Путь)).Равно(НРег("%ПутьКТестам%"));
	
	Параметры_xddShutdown = Неопределено;
	Ожидаем.Что(ПараметрыЗапуска.Свойство("xddShutdown", Параметры_xddShutdown)).ЭтоИстина();
	Ожидаем.Что(Параметры_xddShutdown, "Параметров у xddShutdown нет").ИмеетТип("ФиксированныйМассив").ИмеетДлину(0);
	
	Параметры_xddReport = Неопределено;
	Ожидаем.Что(ПараметрыЗапуска.Свойство("xddReport", Параметры_xddReport)).ЭтоИстина();
	Ожидаем.Что(Параметры_xddReport).ИмеетТип("ФиксированныйМассив").ИмеетДлину(2);
	ИдентификаторГенератораОтчета = Параметры_xddReport[0];
	Ожидаем.Что(НРег(ИдентификаторГенератораОтчета)).Равно(НРег("ГенераторОтчетаJUnitXML"));
	Плагин_ГенераторОтчета = КонтекстЯдра.Плагин(ИдентификаторГенератораОтчета);
	Ожидаем.Что(Плагин_ГенераторОтчета, "Плагин генератор отчета получен").Существует();
	Путь = Параметры_xddReport[1];
	Ожидаем.Что(НРег(Путь)).Равно(НРег("%ПутьОтчета%"));
КонецПроцедуры

Процедура ТестДолжен_Разобрать_ПутьСодержитПробелыИОбрамленКовычками() Экспорт
	СтрокаПараметров = "xddRun ЗагрузчикКаталога ""C:\Program Files""";
	
	ПараметрыЗапуска = ПарсерКоманднойСтроки.Разобрать(СтрокаПараметров);
	
	Ожидаем.Что(ПараметрыЗапуска).ИмеетТип("ФиксированнаяСтруктура").ИмеетДлину(1);
	
	Параметры_xddRun = Неопределено;
	Ожидаем.Что(ПараметрыЗапуска.Свойство("xddRun", Параметры_xddRun)).ЭтоИстина();
	Ожидаем.Что(Параметры_xddRun).ИмеетТип("ФиксированныйМассив").ИмеетДлину(2);
	ИдентификаторЗагрузчика = Параметры_xddRun[0];
	Ожидаем.Что(НРег(ИдентификаторЗагрузчика)).Равно(НРег("ЗагрузчикКаталога"));
	Путь = Параметры_xddRun[1];
	Ожидаем.Что(НРег(Путь)).Равно(НРег("C:\Program Files"));
КонецПроцедуры

Процедура ТестДолжен_Разобрать_ПутьНеСодержитПробелыНоОбрамленКавычками() Экспорт
	СтрокаПараметров = "xddRun ЗагрузчикКаталога ""C:\ProgramFiles""";
	
	ПараметрыЗапуска = ПарсерКоманднойСтроки.Разобрать(СтрокаПараметров);
	
	Ожидаем.Что(ПараметрыЗапуска).ИмеетТип("ФиксированнаяСтруктура").ИмеетДлину(1);
	
	Параметры_xddRun = Неопределено;
	Ожидаем.Что(ПараметрыЗапуска.Свойство("xddRun", Параметры_xddRun)).ЭтоИстина();
	Ожидаем.Что(Параметры_xddRun).ИмеетТип("ФиксированныйМассив").ИмеетДлину(2);
	ИдентификаторЗагрузчика = Параметры_xddRun[0];
	Ожидаем.Что(НРег(ИдентификаторЗагрузчика)).Равно(НРег("ЗагрузчикКаталога"));
	Путь = Параметры_xddRun[1];
	Ожидаем.Что(НРег(Путь)).Равно(НРег("C:\ProgramFiles"));
КонецПроцедуры
