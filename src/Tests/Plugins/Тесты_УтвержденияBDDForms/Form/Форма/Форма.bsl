#Если Не ВебКлиент Тогда

#Область ИНИЦИАЛИЗАЦИЯ
	
&НаКлиенте
Перем КонтекстЯдра;

&НаКлиенте
Перем Ожидаем;

&НаКлиенте
Перем ОжидаемФормы;

&НаКлиенте
Перем Утверждения;

&НаКлиенте
Перем ГенераторТестовыхДанных;

&НаКлиенте
Перем массивТестовыхПриложений;

&НаКлиенте
Перем ЮнитТестированиеИмя;

&НаКлиенте
Процедура Инициализация(КонтекстЯдраПараметр) Экспорт
	КонтекстЯдра = КонтекстЯдраПараметр;
	ПолноеИмяБраузераТестов = КонтекстЯдра.Объект.ПолноеИмяБраузераТестов;
	Ожидаем = КонтекстЯдра.Плагин("УтвержденияBDD");
	Утверждения = КонтекстЯдра.Плагин("БазовыеУтверждения");
	ГенераторТестовыхДанных = КонтекстЯдра.Плагин("СериализаторMXL");
КонецПроцедуры

&НаКлиенте
Процедура ПередЗапускомТеста() Экспорт
	
	//ИмяПеременнойПользователь = "Пользователь12";
	//ПользовательИмя = "ТестИмя12";
	//
	//ОбновитьПовторноИспользуемыеЗначения();
	//
	//КонтекстЯдра.ВывестиСообщение("Перед запуском теста");
	//
	//массивТестовыхПриложений = Новый Массив;
	//
	//СоздатьТестовыеДанныеСервер(ЮнитТестированиеИмя);
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеЗапускаТеста() Экспорт
	
	//КонтекстЯдра.ВывестиСообщение("После запуска теста");
	//УдалитьТестовыеДанныеСервер(ЮнитТестированиеИмя);
	//ЗавершитьВсеСеансыРаботыСТестовымиПриложениями();
	
КонецПроцедуры

&НаСервере
Процедура СоздатьТестовыеДанныеСервер(Знач ЮТестПуть)
	
	ГенераторТестовыхДанных = ГенераторТестовыхДанных();
	
	ИмяМакета = "СозданиеОдногоПользователяИБ";
	//ИмяМакета = "СозданиеОдногоПользователяИБ_УФ";
	
	ОсновнойОбъект = Объект();
	МассивИменПользователей = МассивИменПользователейПоУмолчанию();
	Для каждого ИмяПользователя Из МассивИменПользователей Цикл
		УдалитьПользователяИБ_ЕслиОнСуществует(ИмяПользователя);
	КонецЦикла;
	
	КоллекцияПараметров = Новый Структура("ИмяПеременнойПользователь, ПользовательИмя", ИмяПеременнойПользователь, ПользовательИмя);
	НовыйТабДок = ПолучитьМакетДанныхДоступа(ИмяМакета, КоллекцияПараметров);
	
	МассивМакетов = Новый Массив;
	МассивМакетов.Добавить(НовыйТабДок);
	
	ТестовыеДанные = ГенераторТестовыхДанных.СоздатьДанныеПоТабличномуДокументу(МассивМакетов);
	
	ТестовыеДанныеУФ.Очистить();

	ТестовыеДанныеДляУФ = Новый ТаблицаЗначений;
	Колонка = ТестовыеДанныеДляУФ.Колонки.Добавить("Ключ");
	Колонка = ТестовыеДанныеДляУФ.Колонки.Добавить("Значение");
	
	Для каждого ключЗначение Из ТестовыеДанные Цикл
		СписокВрем = Новый СписокЗначений;
		Строка = ТестовыеДанныеДляУФ.Добавить();
		Если ТипЗнч(ключЗначение.Значение) = Тип("ПользовательИнформационнойБазы") Тогда
			СписокВрем.Добавить(ключЗначение.Значение.Имя, ключЗначение.Ключ);
			Строка.Ключ = "ПользовательИнформационнойБазы";
		Иначе
			Строка.Ключ = ключЗначение.Ключ;
			СписокВрем.Добавить(ключЗначение.Значение);
		КонецЕсли;
		Строка.Значение = СписокВрем;
	КонецЦикла;
	
	ЗначениеВДанныеФормы(ТестовыеДанныеДляУФ, ТестовыеДанныеУФ);
	
КонецПроцедуры

&НаСервере
Процедура УдалитьТестовыеДанныеСервер(Знач ЮТестПуть)
	
	Если ТестовыеДанныеУФ.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	ГенераторТестовыхДанных = ГенераторТестовыхДанных();
	
	//лПользователь = ПользователиИнформационнойБазы.НайтиПоИмени(ПользовательИмя);
	//ЮТест.ПроверитьНеравенство(лПользователь, Неопределено, "лПользователь должен существовать");
	
	ТестовыеДанные = Новый Структура;
	Для каждого Строка Из ТестовыеДанныеУФ Цикл
		Если Строка.Ключ = "ПользовательИнформационнойБазы" Тогда
			ЭлементСпискаЗначений = Строка.Значение[0];
			лПользователь = ПользователиИнформационнойБазы.НайтиПоИмени(СокрЛП(ЭлементСпискаЗначений.Значение));
			ТестовыеДанные.Вставить(ЭлементСпискаЗначений.Представление, лПользователь);
		Иначе
			ТестовыеДанные.Вставить(Строка.Ключ, Строка.Значение[0].Значение);
		КонецЕсли;
	КонецЦикла;
	
	ГенераторТестовыхДанных.УдалитьСозданныеДанные(ТестовыеДанные);
	
	//лПользователь = ПользователиИнформационнойБазы.НайтиПоИмени(ПользовательИмя);
	//ЮТест.ПроверитьРавенство(лПользователь, Неопределено, "лПользователь должен быть удален");
	
	ТестовыеДанныеУФ.Очистить();
	
КонецПроцедуры

&НаСервере
Функция ГенераторТестовыхДанных()
	
	КонтекстЯдра = ПолучитьКонтекстЯдраНаСервере();
	
	СериализаторMXL = КонтекстЯдра.СоздатьОбъектПлагина("СериализаторMXL");	
	
	Возврат СериализаторMXL;
	
КонецФункции

Функция МассивИменПользователейПоУмолчанию()
	Рез = Новый Массив();
	Рез.Добавить(ПользовательИмя); //"ТестИмя");
	//Рез.Добавить("ТестИмя2");// должно совпадать с макетом;
	Возврат Рез;
КонецФункции

&НаСервере
Функция Объект()
	Возврат РеквизитФормыВЗначение("Объект");
КонецФункции

Функция ПолучитьМакетДанныхДоступа(ИмяМакета, КоллекцияПараметров) Экспорт

	Макет = Объект().ПолучитьМакет(ИмяМакета);
	ЗаполнитьЗначенияСвойств(Макет.Параметры, КоллекцияПараметров);
		//Если Не КоллекцияПараметров.Свойство("ИмяПользователя") Тогда
		//	Макет.Параметры.ИмяПользователя = ИмяНовогоПользователя;
		//КонецЕсли;
	
	НовыйТабДок = Новый ТабличныйДокумент;
	НовыйТабДок.Вывести(Макет);

	Возврат НовыйТабДок;
КонецФункции

Процедура УдалитьПользователяИБ_ЕслиОнСуществует(лИмяПользователя) Экспорт

	лПользователь = ПользователиИнформационнойБазы.НайтиПоИмени(лИмяПользователя);
	Если лПользователь <> Неопределено Тогда
		лПользователь.Удалить();
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Функция ЗапуститьТестовоеПриложение(ИмяПользователя, Пароль, Порт, ТаймаутОжиданияПриложенияВСекундах)
	Перем тестовоеПриложение;
	строкаЗапуска = ПолучитьСтрокуЗапуска(ИмяПользователя, Пароль, Порт);
	//Предупреждение("строкаЗапуска "+Символы.ПС+строкаЗапуска, 3);
	//Предупреждение("Порт "+Порт);
 	ЗапуститьПриложение(строкаЗапуска);
	тестовоеПриложение = Неопределено;
	Выполнить("тестовоеПриложение = Новый ТестируемоеПриложение(, XMLСтрока(Порт));");

	Подключен = Ложь;
	ВремяОкончанияОжидания = ТекущаяДата() + ТаймаутОжиданияПриложенияВСекундах; //60;
	ОписаниеОшибкиСоединения = "";
	Пока Не ТекущаяДата() >= ВремяОкончанияОжидания Цикл
		Попытка
			тестовоеПриложение.УстановитьСоединение();
			Подключен = Истина;
			Прервать;
		Исключение
			ОписаниеОшибкиСоединения = ОписаниеОшибки();
		КонецПопытки;
	КонецЦикла;

	сообщениеОшибки = "Не смогли установить соединение с тестовым приложением для пользователя "+ИмяПользователя+" !" + Символы.ПС + ОписаниеОшибкиСоединения; 
		//юТест.ПроверитьИстину(Подключен, сообщениеОшибки);
	Если Не Подключен Тогда 
    	тестовоеПриложение = Неопределено;
		ВызватьИсключение сообщениеОшибки;
	КонецЕсли;
	
	массивТестовыхПриложений.Добавить(тестовоеПриложение);

	Возврат тестовоеПриложение;
КонецФункции

&НаКлиенте
Процедура ЗавершитьВсеСеансыРаботыСТестовымиПриложениями()
	Если массивТестовыхПриложений.Количество() <> 0 Тогда
		Для Каждого Подопытный Из массивТестовыхПриложений Цикл
			Подопытный.РазорватьСоединение();
		КонецЦикла;	

		ЗавершитьВсеТестовыеПриложенияПринудительно();
	КонецЕсли;	
КонецПроцедуры

&НаКлиенте
Процедура ЗавершитьВсеТестовыеПриложенияПринудительно()
		Утверждения.ПроверитьЗаполненность(массивТестовыхПриложений.Количество(), "массивТестовыхПриложений.Количество()");

	Scr = Новый COMОбъект("MSScriptControl.ScriptControl");
	Scr.Language = "vbscript";
	Scr.AddCode("
		|Option Explicit
		|
		|Dim objWMIService, objProcess, colProcess
		|
		|Set objWMIService = GetObject(""winmgmts:{impersonationLevel=impersonate}!\\.\root\cimv2"") 
		|
		|Set colProcess = objWMIService.ExecQuery(""Select * from Win32_Process Where (CommandLine Like '%/TESTCLIENT%' And ExecutablePath Like '%1cv8c%')"")
		|
		|For Each objProcess in colProcess
		|	objProcess.Terminate()
		|Next
		|");
КонецПроцедуры

&НаКлиенте
Функция ПолучитьСтрокуЗапуска(ИмяПользователя = "", Пароль = "", Порт = "")
		//Если Не ЗначениеЗаполнено(ИмяПользователя) Тогда
		//	ИмяПользователя = "Администратор";
		//КонецЕсли;	
	
	Результат = КаталогПрограммы() + "1cv8c.exe ENTERPRISE " +
		"/IBConnectionString""" + СтрЗаменить(СтрокаСоединенияИнформационнойБазы(), """", """""") + """" +
		"/N""" + ИмяПользователя + """" + "/P""" + Пароль + """" +  
		" /TESTCLIENT -TPort" + XMLСтрока(Порт);

	Возврат Результат;
КонецФункции

#КонецОбласти  

#Область ВСПОМОГАТЕЛЬНЫЕ_МЕТОДЫ
	
// Добавляет значение с ключом в тестовые данные для УФ 
&НаСервере
Процедура ДобавитьТестовыеДанные(Ключ, ТестовыеДанные)
	
	СтрокаТестовыхДанных= ТестовыеДанныеУФ.НайтиСтроки(Новый Структура("Ключ", Ключ));
	
	Если СтрокаТестовыхДанных.Количество() Тогда
		Возврат;
	КонецЕсли;
	
	СтрокаТестовыйДокумент= ТестовыеДанныеУФ.Добавить();
	
	СтрокаТестовыйДокумент.Ключ= Ключ;
	
	Значение= Новый СписокЗначений;
	Значение.Добавить(ТестовыеДанные, Ключ);
	
	СтрокаТестовыйДокумент.Значение= Значение;
	
КонецПроцедуры

// Возвращает первое значение из списка по ключу из тестовых данные для УФ 
&НаКлиентеНаСервереБезКонтекста
Функция ПолучитьТестовыеДанные(ТестовыеДанныеУФ, Ключ)
	
	СписокТестовыхДанных= ПолучитьСписокТестовыхДанных(ТестовыеДанныеУФ, Ключ);
	
	Если СписокТестовыхДанных = Неопределено Тогда
		ТестовыеДанные= Неопределено;
	Иначе
		ТестовыеДанные= СписокТестовыхДанных[0].Значение;
	КонецЕсли;
	
	Возврат ТестовыеДанные;
	
КонецФункции

// Возвращает список значение по ключу из тестовых данные для УФ 
&НаКлиентеНаСервереБезКонтекста
Функция ПолучитьСписокТестовыхДанных(ТестовыеДанныеУФ, Ключ)
	
	СтрокиТестовыхДанных= ТестовыеДанныеУФ.НайтиСтроки(Новый Структура("Ключ", Ключ));
	
	Если Не СтрокиТестовыхДанных.Количество() Тогда
		СписокТестовыхДанных= Неопределено;
	Иначе
		СписокТестовыхДанных= СтрокиТестовыхДанных[0].Значение;
	КонецЕсли;
	
	Возврат СписокТестовыхДанных;
	
КонецФункции

// { Подсистема конфигурации xUnitFor1C

&НаСервере
Функция ПолучитьКонтекстЯдраНаСервере()
	
	// Получаем доступ к серверному контексту обработки с использованием 
	// полного имени метаданных браузера тестов. Иначе нет возможности получить
	// доступ к серверному контексту ядра, т.к. изначально вызов был выполнен на клиенте.
	// При передаче на сервер клиентский контекст теряется.
	КонтекстЯдра = Неопределено;
	МетаданныеЯдра = Метаданные.НайтиПоПолномуИмени(ПолноеИмяБраузераТестов);
	Если НЕ МетаданныеЯдра = Неопределено
		И Метаданные.Обработки.Содержит(МетаданныеЯдра) Тогда
		ИмяОбработкиКонекстаЯдра = СтрЗаменить(ПолноеИмяБраузераТестов, "Обработка", "Обработки");
		Выполнить("КонтекстЯдра = " + ИмяОбработкиКонекстаЯдра + ".Создать()");	
	Иначе
		ИмяОбработкиКонекстаЯдра = СтрЗаменить(ПолноеИмяБраузераТестов, "ВнешняяОбработка", "ВнешниеОбработки");
		ИмяОбработкиКонекстаЯдра = СтрЗаменить(ИмяОбработкиКонекстаЯдра, ".", Символы.ПС);
		МенеджерОбъектов = СтрПолучитьСтроку(ИмяОбработкиКонекстаЯдра, 1);
		ИмяОбъекта = СтрПолучитьСтроку(ИмяОбработкиКонекстаЯдра, 2);
		Выполнить("КонтекстЯдра = " + МенеджерОбъектов + ".Создать("""+ИмяОбъекта+""")");	
	КонецЕсли;
	
	Возврат КонтекстЯдра;
	
КонецФункции

// } Подсистема конфигурации xUnitFor1C

#КонецОбласти

#Область ЗАПОЛНЕНИЕ_НАБОРА_ТЕСТОВ
	
&НаКлиенте
Процедура ЗаполнитьНаборТестов(НаборТестов, КонтекстЯдра) Экспорт
	
	СисИнфо = Новый СистемнаяИнформация;
	Если СисИнфо.ВерсияПриложения < "8.3.0.0" Тогда
		Возврат;
	КонецЕсли;
	
	НаборТестов.НачатьГруппу("Проверка интерфейса автоматического тестирования", Истина);
	
	НаборТестов.Добавить("Тест_ПодключениеКлиента", , "Подключение клиента тестирования");
	
	НаборТестов.Добавить("Тест_ОткрытиеИПроведение", , "Открытие и проведение документа");
	НаборТестов.Добавить("Тест_ПроверкаДвижений", , "Проверка движений");
	
	НаборТестов.Добавить("Тест_УстаовкаОтбораВСпискеПоРегистратору", , "Установка отбора в списке по регистратору");
	
	НаборТестов.ДобавитьДеструктор("Тест_ОтключениеКлиента", "Оключение клиента тестирования и удаление тестовых данных");
	
КонецПроцедуры

#КонецОбласти

#Область ТЕСТЫ

&НаКлиенте
Процедура Тест_ПодключениеКлиента() Экспорт
	
	Ожидаем.ПодключениеКлиентаТестирования("Администратор", "", 1539);
	
КонецПроцедуры

&НаКлиенте
Процедура Тест_ОткрытиеИПроведение() Экспорт

	ТестовыеДанные= ПолучитьТестовыйДокумент();
	
	Ожидаем.Что(ТестовыеДанные, "Тестовые данные").Не_().ЭтоНеопределено();

	Ожидаем
		.ОткрытиеФормыСписка(ТестовыеДанные.ПолноеИмя, ТестовыеДанные.Имя)
		.ПоискВСписке(Новый Структура("Номер,Дата", ТестовыеДанные.Номер, Строка(ТестовыеДанные.Дата)))
		.ОткрытиеВСписке(ТестовыеДанные.Представление)
		.Проведение()
	;
	
КонецПроцедуры

&НаКлиенте
Процедура Тест_ПроверкаДвижений() Экспорт
	
	ТестовыеДанные= ПолучитьТестовыйДокумент();
	
	Ожидаем.Что(ТестовыеДанные, "Тестовые данные").Не_().ЭтоНеопределено();

	Ожидаем
		.ОткрытиеФормыСписка("РегистрСведений.РСПодчиненныйРегистратору", "РСПодчиненный регистратору")
		.УстановкаОтбораВСписке(Новый Структура("Регистратор", ТестовыеДанные.Представление))
		.КоличествоСтрокВСписке("Движения документа " + ТестовыеДанные.Представление).Равно(ТестовыеДанные.КоличествоСтрок);
	;
	
КонецПроцедуры

&НаКлиенте
Процедура Тест_УстаовкаОтбораВСпискеПоРегистратору() Экспорт
	
	// перезаполняем время так как оно изменено при оперативном проведении во время предыдущих тестов
	ТестовыеДанные= ПолучитьТестовыйДокумент();
	
	Ожидаем.Что(ТестовыеДанные, "Тестовые данные").Не_().ЭтоНеопределено();

	Ожидаем
		.ОткрытиеФормыСписка("РегистрСведений.РСПодчиненныйРегистратору", "РСПодчиненный регистратору")
		.УстановкаОтбораВСпискеПоРегистратору(ТестовыеДанные.ЗаголовокСписка, ТестовыеДанные.ПредставлениеТипа, СтрШаблон("Номер=%1;Дата=%2", ТестовыеДанные.Номер, ТестовыеДанные.Дата))
		.КоличествоСтрокВСписке("Движения документа " + ТестовыеДанные.Представление).Равно(ТестовыеДанные.КоличествоСтрок);
	;
	
КонецПроцедуры

&НаКлиенте
Процедура Тест_ОтключениеКлиента() Экспорт

	//УдалитьТестовыйДокумент();
	
	Ожидаем.ОтключениеКлиентаТестирования();
	
КонецПроцедуры

#КонецОбласти 

#Область ГЕНЕРАЦИЯ_ТЕСТОВЫХ_ДАННЫХ
	
&НаСервере
Функция СоздатьТестовыйДокументНаСервере(СтарыйТестовыйДокумент)
	
	Если СтарыйТестовыйДокумент = Неопределено Тогда
		
		ТестовыйДокумент= Документы.Документ2.СоздатьДокумент();
		
		ТестовыйДокумент.Дата= ТекущаяДата();
		ТестовыйДокумент.УстановитьНовыйНомер();
		ТестовыйДокумент.Состав.Добавить();
		ТестовыйДокумент.Записать(РежимЗаписиДокумента.Проведение, РежимПроведенияДокумента.Оперативный);
		
	Иначе
		ТестовыйДокумент= СтарыйТестовыйДокумент.Ссылка.ПолучитьОбъект();
	КонецЕсли;
	
	
	МетаданныеДокумента= ТестовыйДокумент.Метаданные();
	
	ТестовыеДанные= Новый Структура;
	ТестовыеДанные.Вставить("Ссылка", ТестовыйДокумент.Ссылка);
	ТестовыеДанные.Вставить("Номер", ТестовыйДокумент.Номер);
	ТестовыеДанные.Вставить("Дата", ТестовыйДокумент.Дата);
	ТестовыеДанные.Вставить("Имя", МетаданныеДокумента.Имя);
	ТестовыеДанные.Вставить("ПолноеИмя", МетаданныеДокумента.ПолноеИмя());
	ТестовыеДанные.Вставить("Представление", Строка(ТестовыйДокумент));
	ТестовыеДанные.Вставить("КоличествоСтрок", ТестовыйДокумент.Состав.Количество());
	//TODO: корректные описания из метаданных
	ТестовыеДанные.Вставить("ПредставлениеТипа", МетаданныеДокумента.Имя);
	ТестовыеДанные.Вставить("ЗаголовокСписка", МетаданныеДокумента.Имя);
	
	Возврат ТестовыеДанные;
	
КонецФункции

&НаКлиенте
Функция ПолучитьТестовыйДокумент()
	// Тестовый документ создаём только один раз. В следующий раз только перезаполняем его данные.
	ТестовыйДокумент= КонтекстЯдра.ПолучитьКонтекст();
	КонтекстЯдра.СохранитьКонтекст(СоздатьТестовыйДокументНаСервере(ТестовыйДокумент));
	Возврат КонтекстЯдра.ПолучитьКонтекст();
КонецФункции

&НаКлиенте
Процедура УдалитьТестовыйДокумент()
	УдалитьТестовыйДокументНаСервере(КонтекстЯдра.ПолучитьКонтекст());
КонецПроцедуры

&НаСервере
Процедура УдалитьТестовыйДокументНаСервере(ТестовыеДанные)
	Если ТестовыеДанные <> Неопределено Тогда
		ТестовыеДанные.Ссылка.ПолучитьОбъект().Удалить();
	КонецЕсли;
КонецПроцедуры

#КонецОбласти 

#КонецЕсли 


































