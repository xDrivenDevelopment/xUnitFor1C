Перем Я_Тест;

Функция ПолучитьСписокТестов(юТест) Экспорт
	
	Я_Тест = юТест;
	
	Результат = Я_Тест.Новый_НаборТестов();

	Я_Тест.НаборТестов_Добавить(Результат, "Тест_ПроверитьЗаполненность");
	Я_Тест.НаборТестов_Добавить(Результат, "Тест_ПроверитьЗаполненностьПадает");
	
	Я_Тест.НаборТестов_Добавить(Результат, "Тест_ПроверитьНеЗаполненность");
	Я_Тест.НаборТестов_Добавить(Результат, "Тест_ПроверитьНеЗаполненностьПадает");
	
	Я_Тест.НаборТестов_Добавить(Результат, "Тест_ПроверитьЗаполненность_и_НеЗаполненность_Булево");
	Я_Тест.НаборТестов_Добавить(Результат, "Тест_ПроверитьЗаполненность_и_НеЗаполненность_Массивы");
	// TODO Другие коллекции значений
	
	Возврат Результат;
	
КонецФункции

Процедура Тест_ПроверитьЗаполненность() Экспорт
	Я_Тест.ПроверитьЗаполненность(1);
	Я_Тест.ПроверитьЗаполненность(-1);
	Я_Тест.ПроверитьЗаполненность(0.00000000000000000000000001);
	Я_Тест.ПроверитьЗаполненность(-0.00000000000000000000000001);
	
	Я_Тест.ПроверитьЗаполненность('00010102');
	Я_Тест.ПроверитьЗаполненность(ТекущаяДата());
	
	Я_Тест.ПроверитьЗаполненность("а");
	
	Если Метаданные.Отчеты.Количество() > 0 Тогда
		Я_Тест.ПроверитьЗаполненность(Отчеты[Метаданные.Отчеты[0].Имя].Создать());
	КонецЕсли; 
	Если Метаданные.Обработки.Количество() > 0 Тогда
		Я_Тест.ПроверитьЗаполненность(Обработки[Метаданные.Обработки[0].Имя].Создать());
	КонецЕсли; 
КонецПроцедуры

Процедура Тест_ПроверитьЗаполненностьПадает() Экспорт
	Я_Тест.ПроверитьНеВыполнилось("ЭтотОбъект.ПроверитьЗаполненность(0)", "Значение (0) не заполнено, а ожидалась заполненность");
	Я_Тест.ПроверитьНеВыполнилось("ЭтотОбъект.ПроверитьЗаполненность('00010101')", "Значение (01.01.0001 0:00:00) не заполнено, а ожидалась заполненность");
	
	Я_Тест.ПроверитьНеВыполнилось("ЭтотОбъект.ПроверитьЗаполненность("""")", "Значение () не заполнено, а ожидалась заполненность");
	Я_Тест.ПроверитьНеВыполнилось("ЭтотОбъект.ПроверитьЗаполненность("" "")", "Значение ( ) не заполнено, а ожидалась заполненность"); // один пробел
	Я_Тест.ПроверитьНеВыполнилось("ЭтотОбъект.ПроверитьЗаполненность(""  "")", "Значение (  ) не заполнено, а ожидалась заполненность"); // два пробела
	Я_Тест.ПроверитьНеВыполнилось("ЭтотОбъект.ПроверитьЗаполненность(""	"")", "Значение (	) не заполнено, а ожидалась заполненность"); // табуляция
	Я_Тест.ПроверитьНеВыполнилось("ЭтотОбъект.ПроверитьЗаполненность(NULL, ""NULL"")", "Значение () не заполнено, а ожидалась заполненность"+Символы.ПС+"NULL");
	Я_Тест.ПроверитьНеВыполнилось("ЭтотОбъект.ПроверитьЗаполненность(Неопределено, ""Неопределено"")", "Значение () не заполнено, а ожидалась заполненность"+Символы.ПС+"Неопределено");
	
	Если Метаданные.Справочники.Количество() > 0 Тогда
		Я_Тест.ПроверитьНеВыполнилось("ЭтотОбъект.ПроверитьЗаполненность(Справочники[Метаданные.Справочники[0].Имя].ПустаяСсылка())", "Значение () не заполнено, а ожидалась заполненность");
	КонецЕсли; 
	Если Метаданные.Документы.Количество() > 0 Тогда
		Я_Тест.ПроверитьНеВыполнилось("ЭтотОбъект.ПроверитьЗаполненность(Документы[Метаданные.Документы[0].Имя].ПустаяСсылка())", "Значение () не заполнено, а ожидалась заполненность");
	КонецЕсли; 
КонецПроцедуры

Процедура Тест_ПроверитьНеЗаполненность() Экспорт
	Я_Тест.ПроверитьНеЗаполненность(0);
	Я_Тест.ПроверитьНеЗаполненность('00010101');
	
	Я_Тест.ПроверитьНеЗаполненность("");
	Я_Тест.ПроверитьНеЗаполненность(" ");
	Я_Тест.ПроверитьНеЗаполненность("  ");
	Я_Тест.ПроверитьНеЗаполненность("	"); // там табуляция
	Я_Тест.ПроверитьНеЗаполненность(NULL, "NULL");
	Я_Тест.ПроверитьНеЗаполненность(Неопределено, "Неопределено");
	
	Если Метаданные.Справочники.Количество() > 0 Тогда
		Я_Тест.ПроверитьНеЗаполненность(Справочники[Метаданные.Справочники[0].Имя].ПустаяСсылка());
	КонецЕсли; 
	Если Метаданные.Документы.Количество() > 0 Тогда
		Я_Тест.ПроверитьНеЗаполненность(Документы[Метаданные.Документы[0].Имя].ПустаяСсылка());
	КонецЕсли; 
КонецПроцедуры

Процедура Тест_ПроверитьНеЗаполненностьПадает() Экспорт
	Я_Тест.ПроверитьНеВыполнилось("ЭтотОбъект.ПроверитьНеЗаполненность(1)", "Значение (1) заполнено, а ожидалась незаполненность");
	Я_Тест.ПроверитьНеВыполнилось("ЭтотОбъект.ПроверитьНеЗаполненность(-1)", "Значение (-1) заполнено, а ожидалась незаполненность");
	Я_Тест.ПроверитьНеВыполнилось("ЭтотОбъект.ПроверитьНеЗаполненность(0.00000000000000000000000001)", "Значение (0,00000000000000000000000001) заполнено, а ожидалась незаполненность");
	Я_Тест.ПроверитьНеВыполнилось("ЭтотОбъект.ПроверитьНеЗаполненность(-0.00000000000000000000000001)", "Значение (-0,00000000000000000000000001) заполнено, а ожидалась незаполненность");
	
	Я_Тест.ПроверитьНеВыполнилось("ЭтотОбъект.ПроверитьНеЗаполненность('00010102')", "Значение (02.01.0001 0:00:00) заполнено, а ожидалась незаполненность");
	Я_Тест.ПроверитьНеВыполнилось("ЭтотОбъект.ПроверитьНеЗаполненность(ТекущаяДата())", "Значение ("+ТекущаяДата()+") заполнено, а ожидалась незаполненность");
	
	Я_Тест.ПроверитьНеВыполнилось("ЭтотОбъект.ПроверитьНеЗаполненность(""а"")", "Значение (а) заполнено, а ожидалась незаполненность");
	
	Если Метаданные.Отчеты.Количество() > 0 Тогда
		Я_Тест.ПроверитьНеВыполнилось("ЭтотОбъект.ПроверитьНеЗаполненность(Отчеты[Метаданные.Отчеты[0].Имя].Создать())", "Значение (ОтчетОбъект."+Метаданные.Отчеты[0].Имя+") заполнено, а ожидалась незаполненность");
	КонецЕсли; 
	Если Метаданные.Обработки.Количество() > 0 Тогда
		Я_Тест.ПроверитьНеВыполнилось("ЭтотОбъект.ПроверитьНеЗаполненность(Обработки[Метаданные.Обработки[0].Имя].Создать())", "Значение (ОбработкаОбъект."+Метаданные.Обработки[0].Имя+") заполнено, а ожидалась незаполненность");
	КонецЕсли; 
КонецПроцедуры

Процедура Тест_ПроверитьЗаполненность_и_НеЗаполненность_Булево() Экспорт
	Я_Тест.ПроверитьЗаполненность(Истина);
	Я_Тест.ПроверитьЗаполненность(ЛОЖЬ);
КонецПроцедуры

Процедура Тест_ПроверитьЗаполненность_и_НеЗаполненность_Массивы() Экспорт
	Массив = Новый Массив(1); 
	Я_Тест.ПроверитьЗаполненность(Массив);
	Я_Тест.ПроверитьЗаполненность(Новый ФиксированныйМассив(Массив));
	
	Массив = Новый Массив(2); 
	Я_Тест.ПроверитьЗаполненность(Массив);
	Я_Тест.ПроверитьЗаполненность(Новый ФиксированныйМассив(Массив));
	
	Я_Тест.ПроверитьНеЗаполненность(Новый Массив);
	Массив = Новый Массив(1); 
	Массив.Очистить();
	Я_Тест.ПроверитьНеЗаполненность(Массив);
	Я_Тест.ПроверитьНеЗаполненность(Новый ФиксированныйМассив(Массив));
КонецПроцедуры
