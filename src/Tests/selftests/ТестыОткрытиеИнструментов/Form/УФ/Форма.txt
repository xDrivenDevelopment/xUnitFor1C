&НаКлиенте
Перем ЮТест;  // для тестирования

&НаКлиенте
Перем ОткрытаяФорма;  // для тестирования

//{ основная процедура для юнит-тестирования 1CUnit
&НаКлиенте
Функция ПолучитьСписокТестов(ЮнитТестирование) Экспорт
	
	ЮТест = ЮнитТестирование;
	
	ВсеТесты = Новый Массив;

	КоллекцияИнструментов = Новый Соответствие;
	КоллекцияИнструментов.Вставить("Тест открытия инструмента ""Генератор данных""", "xddDataFixtureGen.epf");
	КоллекцияИнструментов.Вставить("Тест открытия инструмента ""Показать ГУИД""", "xddGuidShow.epf");
	
	Для каждого КлючЗначение Из КоллекцияИнструментов Цикл
		ПараметрыТеста = Новый Структура("ПредставлениеТеста,ИмяТеста,Параметр", КлючЗначение.Ключ + " - метод xddTestRunner.ОткрытьИнструмент", "ТестДолжен_ОткрытьЗакрытьИнструмент_ЧерезМетод_ЮТест_ОткрытьИнструмент", КлючЗначение.Значение);
		ВсеТесты.Добавить(ПараметрыТеста);

		ПараметрыТеста = Новый Структура("ПредставлениеТеста,ИмяТеста,Параметр", КлючЗначение.Ключ + " - перебор команд УФ xddTestRunner", "ТестДолжен_ОткрытьЗакрытьИнструмент_ЧерезПереборКомандЮТест", КлючЗначение.Значение);
		ВсеТесты.Добавить(ПараметрыТеста);
	КонецЦикла;

	Возврат ВсеТесты;
	
КонецФункции
//}

//{ блок юнит-тестов - сами тесты

&НаКлиенте
Процедура ПередЗапускомТеста() Экспорт	
	ОткрытаяФорма = Неопределено;
КонецПроцедуры

&НаКлиенте
Процедура ПослеЗапускаТеста() Экспорт	
	Если ТипЗнч(ОткрытаяФорма) = Тип("УправляемаяФорма") Тогда
		ОткрытаяФорма.Закрыть();
	КонецЕсли;
	ОткрытаяФорма = Неопределено;
КонецПроцедуры

&НаКлиенте
Процедура ТестДолжен_ОткрытьЗакрытьИнструмент_ЧерезМетод_ЮТест_ОткрытьИнструмент(ИмяИнструмента) Экспорт
	ОткрытаяФорма = ЮТест.ОткрытьИнструмент(ИмяИнструмента);
	ЮТест.ПроверитьТип(ОткрытаяФорма, "УправляемаяФорма", "Не удалось получить управляемую форму инструмента <"+ИмяИнструмента+">");
	ЮТест.Проверить(ОткрытаяФорма.Открыта(), "Форма инструмента должна быть открыта. Инструмент <"+ИмяИнструмента+">");
КонецПроцедуры

&НаКлиенте
Процедура ТестДолжен_ОткрытьЗакрытьИнструмент_ЧерезПереборКомандЮТест(ИмяИнструмента) Экспорт
	СоответствиеОкон = Новый Соответствие;
	Для каждого Окно Из ПолучитьОкна() Цикл
		СоответствиеОкон.Вставить(Окно, Окно);
	КонецЦикла;
	
	ЮТест.ОткрытьИнструмент(ИмяИнструмента);
	
	Окна = ПолучитьОкна();
	Для к=-Окна.Количество()+1 По 0 Цикл
		НовоеОкно = Окна.Получить(-к);
		Если СоответствиеОкон.Получить(НовоеОкно) = Неопределено Тогда
			ОткрытаяФорма = НовоеОкно.ПолучитьСодержимое();
			Прервать;
		КонецЕсли;
	КонецЦикла;
	
	//ОткрытаяФорма = ЮТест.ОткрытьИнструмент(ИмяИнструмента);
	ЮТест.ПроверитьТип(ОткрытаяФорма, "УправляемаяФорма", "Не удалось получить управляемую форму инструмента <"+ИмяИнструмента+">");
	ЮТест.Проверить(ОткрытаяФорма.Открыта(), "Форма инструмента должна быть открыта. Инструмент <"+ИмяИнструмента+">");
КонецПроцедуры

//} конец блока юнит-тестов
