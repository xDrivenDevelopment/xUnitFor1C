
&НаКлиенте
Перем MiddleMan;

&НаКлиенте
Перем СоответствиеСтатусовДляTeamCity;

////////////////////////////////////////////////////////////////////////////////
// ИНТЕРФЕЙС АВТОМАТИЧЕСКИХ ТЕСТОВ

//#Область ИнтерфейсАвтоматическихТестов

////////////////////////////////////////////////////////////////////////////////
// СЛУЖЕБНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ ДЛЯ ПРОВЕДЕНИЯ ТЕСТИРОВАНИЯ 

//#Область СлужебныеПроцедурыИФункцииДляПроведенияТестирования

// { функции утверждения- проверки значений. 

&НаКлиенте
Процедура Проверить(_Истина, ДопСообщениеОшибки = "") Экспорт
	Если Не _Истина Тогда
		СообщениеОшибки = "Переданный параметр ("+Формат(_Истина, "БЛ=ложь; БИ=истина")+") не является Истиной, а хотели, чтобы являлся." + ФорматДСО(ДопСообщениеОшибки);
		ВызватьИсключение(СообщениеОшибки);
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ПроверитьИстину(_Истина, ДопСообщениеОшибки = "") Экспорт
	Проверить(_Истина, ДопСообщениеОшибки);
КонецПроцедуры

&НаКлиенте
Процедура ПроверитьЛожь(_Ложь, ДопСообщениеОшибки = "") Экспорт
	Если _Ложь Тогда
		СообщениеОшибки = "Переданный параметр ("+Формат(_Ложь, "БЛ=ложь; БИ=истина")+") не является Ложью, а хотели, чтобы являлся." + ФорматДСО(ДопСообщениеОшибки);
		ВызватьИсключение(СообщениеОшибки);
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ПроверитьДату(_Дата, _Период, ДопСообщениеОшибки = "") Экспорт
	Если _Дата < _Период.ДатаНачала или _Дата > _Период.ДатаОкончания Тогда
		представление = ПредставлениеПериода(_Период.ДатаНачала, _Период.ДатаОкончания, "ФП = Истина");
		СообщениеОшибки = "Переданный параметр ("+Формат(_Дата, "ДФ='dd.MM.yyyy HH:mm:ss'")+") не входит в период "+представление+", а хотели, чтобы являлся." + ФорматДСО(ДопСообщениеОшибки);
		ВызватьИсключение(СообщениеОшибки);
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ПроверитьРавенствоДатСТочностью2Секунды(_Дата, _Дата2, ДопСообщениеОшибки = "") Экспорт
	Если _Дата < _Дата2-2 или _Дата > _Дата2+2 Тогда
		СообщениеОшибки = "Переданная дата ("+Формат(_Дата, "ДФ='dd.MM.yyyy HH:mm:ss'")+") не равна дате ("+Формат(_Дата2, "ДФ='dd.MM.yyyy HH:mm:ss'")+") с точностью до 2-х секунд, а хотели, чтобы они равнялись." + ФорматДСО(ДопСообщениеОшибки);
		ВызватьИсключение(СообщениеОшибки);
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ПроверитьРавенство(_1, _2, ДопСообщениеОшибки = "") Экспорт
	Если _1 <> _2 Тогда
		СообщениеОшибки = "Сравниваемые значения ("+_1+"; "+_2+") не равны, а хотели, чтобы были равны." + ФорматДСО(ДопСообщениеОшибки);
		ВызватьИсключение(СообщениеОшибки);
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ПроверитьНеРавенство(_1, _2, ДопСообщениеОшибки = "") Экспорт
	Если _1 = _2 Тогда
		СообщениеОшибки = "Сравниваемые значения ("+_1+"; "+_2+") равны, а хотели, чтобы были не равны." + ФорматДСО(ДопСообщениеОшибки);
		ВызватьИсключение(СообщениеОшибки);
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ПроверитьБольше(_Больше, _Меньше, ДопСообщениеОшибки = "") Экспорт
	Если _Больше <= _Меньше Тогда
		СообщениеОшибки = "Первый параметр ("+_Больше+") меньше или равен второму ("+_Меньше+") а хотели, чтобы был больше." + ФорматДСО(ДопСообщениеОшибки);
		ВызватьИсключение(СообщениеОшибки);
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ПроверитьБольшеИлиРавно(_Больше, _Меньше, ДопСообщениеОшибки = "") Экспорт
	Если _Больше < _Меньше Тогда
		СообщениеОшибки = "Первый параметр ("+_Больше+") меньше второго ("+_Меньше+") а хотели, чтобы был больше или равен." + ФорматДСО(ДопСообщениеОшибки);
		ВызватьИсключение(СообщениеОшибки);
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ПроверитьМеньше(проверяемоеЗначение1, проверяемоеЗначение2, СообщениеОбОшибке = "") Экспорт
	Если проверяемоеЗначение1 >= проверяемоеЗначение2 Тогда
		ВызватьИсключение "Значение <"+проверяемоеЗначение1+"> больше или равно, чем <"+проверяемоеЗначение2+">, а ожидалось меньше"+
				ФорматДСО(СообщениеОбОшибке);
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ПроверитьМеньшеИлиРавно(проверяемоеЗначение1, проверяемоеЗначение2, СообщениеОбОшибке = "") Экспорт
	Если проверяемоеЗначение1 > проверяемоеЗначение2 Тогда
		ВызватьИсключение "Значение <"+проверяемоеЗначение1+"> больше, чем <"+проверяемоеЗначение2+">, а ожидалось меньше или равно"+
				ФорматДСО(СообщениеОбОшибке);
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ПроверитьВыполнилось(СтрокаВыполнения, ДопСообщениеОшибки = "") Экспорт
	Попытка
		Выполнить(СтрокаВыполнения);
		Упало = Ложь;
	Исключение
		Упало = Истина;
		ПолученноеОписаниеОшибки = ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
	КонецПопытки;
	
	Если Упало Тогда
		СообщениеОшибки = "Хотели, чтобы алгоритм """+СтрокаВыполнения+""" выполнился, а он упал с ошибкой """+ПолученноеОписаниеОшибки+"""." + ФорматДСО(ДопСообщениеОшибки);
		ВызватьИсключение(СообщениеОшибки);
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ПроверитьМетодВыполнился(Объект, ИмяМетода, ДопСообщениеОшибки = "") Экспорт
	СтрокаВыполнения = "Объект."+ИмяМетода+"()";
	
	Попытка
		Выполнить(СтрокаВыполнения);
		Упало = Ложь;
	Исключение
		Упало = Истина;
		ПолученноеОписаниеОшибки = ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
	КонецПопытки;
	
	Если Упало Тогда
		СообщениеОшибки = "Хотели, чтобы алгоритм """+СтрокаВыполнения+""" выполнился, а он упал с ошибкой """+ПолученноеОписаниеОшибки+"""." + ФорматДСО(ДопСообщениеОшибки);
		ВызватьИсключение(СообщениеОшибки);
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ПроверитьНеВыполнилось(СтрокаВыполнения, ОжидаемоеОписаниеОшибки, ДопСообщениеОшибки = "") Экспорт
	Попытка
		Выполнить(СтрокаВыполнения);
		Успех = Истина;
	Исключение
		Успех = Ложь;
		ПолученноеОписаниеОшибки = ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
	КонецПопытки;
	
	Если Успех Тогда
		ВызватьИсключение("Хотели, чтобы алгоритм """+СтрокаВыполнения+""" упал, а он выполнился" + ФорматДСО(ДопСообщениеОшибки));
	КонецЕсли;
	Если Найти(ПолученноеОписаниеОшибки, ОжидаемоеОписаниеОшибки) = 0 Тогда
		ВызватьИсключение("Хотели, чтобы алгоритм """+СтрокаВыполнения+""" упал с сообщением об ошибке """+ОжидаемоеОписаниеОшибки+""", а он упал с сообщением """+ПолученноеОписаниеОшибки+"""." + ФорматДСО(ДопСообщениеОшибки));
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ПроверитьМетодНеВыполнился(Объект, ИмяМетода, ОжидаемоеОписаниеОшибки, ДопСообщениеОшибки = "") Экспорт
	СтрокаВыполнения = "Объект."+ИмяМетода+"()";
	
	Попытка
		Выполнить(СтрокаВыполнения);
		Успех = Истина;
	Исключение
		Успех = Ложь;
		ПолученноеОписаниеОшибки = ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
	КонецПопытки;
	
	Если Успех Тогда
		ВызватьИсключение("Хотели, чтобы алгоритм """+СтрокаВыполнения+""" упал, а он выполнился" + ФорматДСО(ДопСообщениеОшибки));
	КонецЕсли;
	Если Найти(ПолученноеОписаниеОшибки, ОжидаемоеОписаниеОшибки) = 0 Тогда
		ВызватьИсключение("Хотели, чтобы алгоритм """+СтрокаВыполнения+""" упал с сообщением об ошибке """+ОжидаемоеОписаниеОшибки+""", а он упал с сообщением """+ПолученноеОписаниеОшибки+"""." + ФорматДСО(ДопСообщениеОшибки));
	КонецЕсли;
КонецПроцедуры

// проверка идет через ЗначениеЗаполнено, но мутабельные значение всегда считаем заполненными
&НаКлиенте
Процедура ПроверитьЗаполненность(ПроверяемоеЗначение, ДопСообщениеОшибки = "") Экспорт
	Попытка
		фЗаполнено = ЗначениеЗаполнено(ПроверяемоеЗначение);
	Исключение
		Возврат;
	КонецПопытки;
	Если НЕ фЗаполнено Тогда
		ВызватьИсключение "Значение ("+ПроверяемоеЗначение+") не заполнено, а ожидалась заполненность" + ФорматДСО(ДопСообщениеОшибки);
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ПроверитьНеЗаполненность(ПроверяемоеЗначение, ДопСообщениеОшибки = "") Экспорт
	СообщениеОшибки = "Значение ("+ПроверяемоеЗначение+") заполнено, а ожидалась незаполненность" + ФорматДСО(ДопСообщениеОшибки);
	Попытка
		фЗаполнено = ЗначениеЗаполнено(ПроверяемоеЗначение);
	Исключение
		ВызватьИсключение СообщениеОшибки;
	КонецПопытки;
	Если фЗаполнено Тогда
		ВызватьИсключение СообщениеОшибки;
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ПроверитьТип(значение, ТипИлиИмяТипа, ДопСообщениеОшибки = "") Экспорт
	Если ТипЗнч(ТипИлиИмяТипа) = Тип("Строка") Тогда
		искомыйТип = Тип(ТипИлиИмяТипа);
	ИначеЕсли ТипЗнч(ТипИлиИмяТипа) = Тип("Тип") Тогда
		искомыйТип = ТипИлиИмяТипа;
	Иначе
		ВызватьИсключение "ПроверитьТип: Тип значения параметра ТипИлиИмяТипа должен быть <Тип> или <Строка>, а получили <"+ ТипЗнч(ТипИлиИмяТипа) + ">" + ФорматДСО(ДопСообщениеОшибки);
	КонецЕсли;
	Если ТипЗнч(значение) <> искомыйТип Тогда
		ВызватьИсключение "Типом значения <"+значение+"> является <"+ТипЗнч(значение)+">, а ожидался тип <"+ТипИлиИмяТипа+">."+ФорматДСО(ДопСообщениеОшибки);
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ПроверитьВхождение(строка, подстрокаПоиска, ДопСообщениеОшибки = "") Экспорт
	Если Найти(строка, подстрокаПоиска) = 0 Тогда
		СообщениеОшибки = "Искали в <"+строка+"> подстроку <"+подстрокаПоиска+">, но не нашли." + ФорматДСО(ДопСообщениеОшибки);
		ВызватьИсключение(СообщениеОшибки);
	КонецЕсли;
КонецПроцедуры

//Коллекция - Массив, Структура, Соответствие, ФиксированныйМассив, ФиксированнаяСтруктура, ФиксированноеСоответствие, СписокЗначений
&НаКлиенте
Процедура ПроверитьВхождениеВКоллекцию(Элемент, Коллекция, ДопСообщениеОшибки = "") Экспорт
	Нашли = Неопределено;
	ТипКоллекции = ТипЗнч(Коллекция);
	Если ТипКоллекции = Тип("Массив") или ТипКоллекции = Тип("ФиксированныйМассив") Тогда
		Нашли = Коллекция.Найти(Элемент) <> Неопределено;
	ИначеЕсли ТипКоллекции = Тип("Структура") или ТипКоллекции = Тип("Соответствие") 
			или ТипКоллекции = Тип("ФиксированнаяСтруктура") или ТипКоллекции = Тип("ФиксированноеСоответствие") Тогда
		Для каждого КлючЗначение Из Коллекция Цикл
			Нашли = КлючЗначение.Значение = Элемент;
			Если Нашли Тогда
				Прервать;
			КонецЕсли;
		КонецЦикла;
	ИначеЕсли ТипКоллекции = Тип("СписокЗначений") Тогда
		Нашли = Коллекция.НайтиПоЗначению(Элемент) <> Неопределено;
	КонецЕсли;
	Если Нашли = Неопределено Тогда
		СообщениеОшибки = "Утверждение ""ПроверитьВхождениеВКоллекцию"" не умеет работать с типом коллекции <"+ТипКоллекции+">." + ФорматДСО(ДопСообщениеОшибки);
		ВызватьИсключение(СообщениеОшибки);
	КонецЕсли;
	Если Не Нашли Тогда
		СообщениеОшибки = "Не нашли элемент <"+Элемент+"> в коллекции, а хотели, чтобы он был в коллекции." + ФорматДСО(ДопСообщениеОшибки);
		ВызватьИсключение(СообщениеОшибки);
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ПроверитьВалидностьЗапросаСКД(ТекстЗапроса, ДопСообщениеОшибки = "") Экспорт
	ПроверитьВалидностьЗапросаСКДНаСервере(ТекстЗапроса, ДопСообщениеОшибки);
КонецПроцедуры

// Только для внутреннего использования
&НаСервере
Процедура ПроверитьВалидностьЗапросаСКДНаСервере(ТекстЗапроса, ДопСообщениеОшибки = "")
	Объект().ПроверитьВалидностьЗапросаСКД(ТекстЗапроса, ДопСообщениеОшибки);
КонецПроцедуры

&НаКлиенте
Процедура ПроверитьРавенствоТабличныхДокументовТолькоПоЗначениям(ТабДок1, ТабДок2, ФлагУчитыватьТолькоВидимыеКолонкиИлиДопСообщениеОшибки = Истина, Знач ДопСообщениеОшибки = "") Экспорт
	ПроверитьРавенствоТабличныхДокументовТолькоПоЗначениямСервер(ТабДок1, ТабДок2, ФлагУчитыватьТолькоВидимыеКолонкиИлиДопСообщениеОшибки, ДопСообщениеОшибки);
КонецПроцедуры

&НаСервере
Процедура ПроверитьРавенствоТабличныхДокументовТолькоПоЗначениямСервер(ТабДок1, ТабДок2, ФлагУчитыватьТолькоВидимыеКолонкиИлиДопСообщениеОшибки = Истина, Знач ДопСообщениеОшибки = "") Экспорт
	Объект().ПроверитьРавенствоТабличныхДокументовТолькоПоЗначениям(ТабДок1, ТабДок2, ФлагУчитыватьТолькоВидимыеКолонкиИлиДопСообщениеОшибки, ДопСообщениеОшибки);
КонецПроцедуры

// }

//#КонецОбласти

//#КонецОбласти


////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ФОРМЫ

//#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	// Получаем версию платформы для определения вызова процедур\функций для 8.2 или 8.3
	СистемнаяИнформация = Новый СистемнаяИнформация;
	ВерсияПлатформы = Лев(СистемнаяИнформация.ВерсияПриложения, 3);
	ЕстьПоддержкаАсинхронныхВызовов = (СравнитьВерсии(СистемнаяИнформация.ВерсияПриложения, "8.3.5.1383") >= 0);
	
	ОбъектНаСервере = Объект();
	ЭтаФорма.Заголовок = ОбъектНаСервере.ЗаголовокФормы();
	
	ЭтоВстроеннаяОбработка = ОбъектНаСервере.ЭтоВстроеннаяОбработка;
	УстановитьПоУмолчаниюОбработчикКоманды_ЗагрузитьТесты();
	
	ЗапретИспользованияИнформатора = Истина;
	
	ВосстановитьНастройки();
	
	Если ЗначениеЗаполнено(Параметры.Пути) Тогда
		Пути.ЗагрузитьЗначения(Параметры.Пути.ВыгрузитьЗначения());
	КонецЕсли;
	
	ЗначенияСостоянияТестов = ОбъектНаСервере.мЗначенияСостоянияТестов;
	ЗначенияТиповСтроки = ОбъектНаСервере.мЗначенияТиповСтроки;
	ЗначениеМодульИлиФорма = ОбъектНаСервере.мЗначениеМодульИлиФорма;
	
КонецПроцедуры

&НаСервере
Процедура УстановитьПоУмолчаниюОбработчикКоманды_ЗагрузитьТесты()
	Перем Команда;
	
	Если ЭтоВстроеннаяОбработка Тогда
		Команда = Команды.КомандаЗагрузитьТестыИзКонфигурацииПоИмени;
	Иначе
		Команда = Команды.КомандаЗагрузитьТестовыйНабор;
	КонецЕсли;
	
	Элементы.ФормаЗагрузитьТестыПоУмолчанию.ИмяКоманды = Команда.Имя;
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	СоздатьСоответствиеСтатусовДляTeamCity();
	
	Если ОбработатьПараметрыЗапуска(ПараметрЗапуска) Тогда
		Возврат;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(Пути) Тогда
		
		ОбновитьНадписьПути();
		
		Попытка
			LoadAllTests();
		Исключение
			ТекстОшибки = ОписаниеОшибки();
			ТекстСообщения = НСтр("ru='ОШИБКА загрузки тестов: %ТекстОшибки%'; en='Error loading testcases: %ТекстОшибки%'");
			ТекстСообщения = СтрЗаменить(ТекстСообщения, "%ТекстОшибки%", ТекстОшибки); 
			ВывестиСообщение(ТекстСообщения);
		КонецПопытки;
		
	КонецЕсли;
	
	Элементы.КомандаВключитьРежимОтладки.Пометка = Объект.РежимОтладки;
	
КонецПроцедуры

&НаКлиенте
Процедура СоздатьСоответствиеСтатусовДляTeamCity()

	//{ Соответствие Статуса сообщения 1с Статусу в Buildlog TeamCity
	СоответствиеСтатусовДляTeamCity = Новый Соответствие;
	СоответствиеСтатусовДляTeamCity.Вставить(Неопределено,"NORMAL");
	СоответствиеСтатусовДляTeamCity.Вставить(СтатусСообщения.БезСтатуса,"NORMAL");
	СоответствиеСтатусовДляTeamCity.Вставить(СтатусСообщения.Обычное,"NORMAL");
	СоответствиеСтатусовДляTeamCity.Вставить(СтатусСообщения.Информация,"NORMAL");
	СоответствиеСтатусовДляTeamCity.Вставить(СтатусСообщения.Внимание,"WARNING");
	СоответствиеСтатусовДляTeamCity.Вставить(СтатусСообщения.Важное,"FAILURE");
	СоответствиеСтатусовДляTeamCity.Вставить(СтатусСообщения.ОченьВажное,"ERROR");
	СоответствиеСтатусовДляTeamCity = Новый ФиксированноеСоответствие(СоответствиеСтатусовДляTeamCity);
	//} Соответствие Статуса сообщения 1с Статусу в Buildlog TeamCity
КонецПроцедуры

// Обработка оповещений о загрузке или ходе выполнения тестов.
// 
&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	// Возможные события:
	// 		xUnitFor1C.TestLoaded
	// 		xUnitFor1C.TestPassed
	// 		xUnitFor1C.TestNotInplemented
	// 		xUnitFor1C.TestFailed
	
	Если ИмяСобытия = "xUnitFor1C.TestPassed" Тогда
		
		ИндикаторВыполнения = ИндикаторВыполнения + 1; //Элементы.ИндикаторВыполнения.Значение = Элементы.ИндикаторВыполнения.Значение + 1;
		
	ИначеЕсли ИмяСобытия = "xUnitFor1C.TestNotImplemented" Тогда
		
		ИндикаторВыполнения = ИндикаторВыполнения + 1; //Элементы.ИндикаторВыполнения.Значение = Элементы.ИндикаторВыполнения.Значение + 1;
		
		Если Элементы.ИндикаторВыполнения.ЦветРамки <> WebЦвета.Красный Тогда
			Элементы.ИндикаторВыполнения.ЦветРамки = WebЦвета.Золотой;
		КонецЕсли;
		
	ИначеЕсли ИмяСобытия = "xUnitFor1C.TestFailed" Тогда
		
		ИндикаторВыполнения = ИндикаторВыполнения + 1; //Элементы.ИндикаторВыполнения.Значение = Элементы.ИндикаторВыполнения.Значение + 1;
		Элементы.ИндикаторВыполнения.ЦветРамки = WebЦвета.Красный;
		
	ИначеЕсли ИмяСобытия = "xUnitFor1C.LoadSubSystem" Тогда
		
		Если ТипЗнч(Параметр) = Тип("Строка") И НЕ ПустаяСтрока(Параметр)  Тогда
			
			Пути.Очистить();
			Пути.Добавить(Параметр);
			ОбновитьНадписьПути();
			LoadAllTests();
			
		КонецЕсли;
		
	КонецЕсли;
	
	ОбновитьОтображениеДанных();
	
КонецПроцедуры

&НаКлиенте
Процедура ПриЗакрытии()
	
	Если Не ЭтоЗапускИзКоманднойСтроки Тогда
		СохранитьНастройки();
	КонецЕсли;
	
КонецПроцедуры

//#КонецОбласти

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ ТАБЛИЦЫ ФОРМЫ ДЕРЕВОТЕСТОВ

//#Область ОбработчикиТаблицыФормы_ДеревоТестов

&НаКлиенте
Процедура РазвернутьСтрокиДерева(ВключаяПодчиненные = Ложь)
	
	Для Каждого СтрокаДерева Из Объект.ДеревоТестов.ПолучитьЭлементы() Цикл
		Идентификатор = СтрокаДерева.ПолучитьИдентификатор();
		Элементы.ДеревоТестов.Развернуть(Идентификатор, ВключаяПодчиненные);
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура СвернутьУспешныеСтрокиДереваИРазвернутьНеуспешные(СтрокиДереваТестов, ОставитьРазвернутым)
	
	ЭлементДеревоТестов = Элементы.ДеревоТестов;
	Для Каждого СтрокаДерева из СтрокиДереваТестов Цикл
		Если Не ОставитьРазвернутым Тогда
			Идентификатор = СтрокаДерева.ПолучитьИдентификатор();
			Если СтрокаДерева.Состояние = ЗначенияСостоянияТестов.Сломался
			 Или СтрокаДерева.Состояние = ЗначенияСостоянияТестов.НеРеализован Тогда
				ЭлементДеревоТестов.Развернуть(Идентификатор, Ложь);
			ИначеЕсли СтрокаДерева.Состояние <> ЗначенияСостоянияТестов.НеВыполнялся Тогда
				ЭлементДеревоТестов.Свернуть(Идентификатор);
			КонецЕсли;
		КонецЕсли;
		СвернутьУспешныеСтрокиДереваИРазвернутьНеуспешные(СтрокаДерева.ПолучитьЭлементы(), Ложь);
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура ИзменитьСостояниеВыполненияРодителей(СтрокаДереваТестов) Экспорт
	
	СтрокаРодителя = СтрокаДереваТестов.ПолучитьРодителя();
	Если СтрокаРодителя = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ПредыдушееСостояние = СтрокаРодителя.Состояние;
	Состояние = ЗначенияСостоянияТестов.НеВыполнялся;
	Для Каждого Строка Из СтрокаРодителя.ПолучитьЭлементы() Цикл
		Если Строка.Состояние = ЗначенияСостоянияТестов.НеВыполнялся Тогда
			Состояние = ЗначенияСостоянияТестов.НеВыполнялся;
			Прервать;
		КонецЕсли;
		ЗапомнитьСамоеХудшееСостояние(Состояние, Строка.Состояние);
	КонецЦикла;
	Если ПредыдушееСостояние <> Состояние Тогда
		СтрокаРодителя.Состояние = Состояние;
		ИзменитьСостояниеВыполненияРодителей(СтрокаРодителя);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура УдалитьПустуюСтрокуДерева(СтрокаТеста, РодительскаяСтрока)
	Если СтрокаТеста <> Неопределено И СтрокаТеста.ПолучитьЭлементы().Количество() = 0 Тогда
		Если РодительскаяСтрока <> Неопределено Тогда
			РодительскаяСтрока.ПолучитьЭлементы().Удалить(СтрокаТеста);
		Иначе
			Объект.ДеревоТестов.ПолучитьЭлементы().Удалить(СтрокаТеста);
		КонецЕсли;
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура УдалитьПустыеСтрокиКаталогов(СтрокиДерева)
	
	Для Каждого СтрокаДерева Из СтрокиДерева Цикл
		Если СтрокаДерева.ТипСтроки = ЗначенияТиповСтроки.КаталогТестов Тогда
			УдалитьПустыеСтрокиКаталогов(СтрокаДерева.ПолучитьЭлементы());
			Если СтрокаДерева.ПолучитьЭлементы().Количество() = 0 Тогда
				СтрокиДерева.Удалить(СтрокаДерева);
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры


&НаКлиенте
Функция ПосчитатьКоличествоТестовыхСлучаевПоВсемуДеревуТестов(РодительскаяСтрока)
	коллекцияСтрок = РодительскаяСтрока.ПолучитьЭлементы();
	Если коллекцияСтрок.Количество() = 0 Тогда
		Возврат 1;
	КонецЕсли;
	количествоТестов = 0;
	Для каждого СтрокаДерева из коллекцияСтрок Цикл
		лКоличество = ПосчитатьКоличествоТестовыхСлучаевПоВсемуДеревуТестов(СтрокаДерева);
		СтрокаДерева.КоличествоТестов = лКоличество;
		количествоТестов = количествоТестов + лКоличество;
	КонецЦикла;
	Возврат количествоТестов;
КонецФункции

//#КонецОбласти

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ КОМАНД ФОРМЫ

//#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура КомандаВыполнитьВсеТесты(Команда)
	ОчиститьСообщения();
	RunAllTests();
КонецПроцедуры

&НаКлиенте
Процедура КомандаПерезагрузитьВсеТесты(Команда)
	LoadAllTests();
КонецПроцедуры

&НаКлиенте
Процедура КомандаЗагрузитьТестовыйНабор(Команда)
	
	Режим = РежимДиалогаВыбораФайла.ВыборКаталога;
	ДиалогОткрытияФайла = Новый ДиалогВыбораФайла(Режим);
	ДиалогОткрытияФайла.ПолноеИмяФайла = "";
	ДиалогОткрытияФайла.МножественныйВыбор = Ложь;
	ДиалогОткрытияФайла.Заголовок = НСтр("en='Choose a folder with testcases'; ru='Выберите каталог с тестовыми случаями'");
	Если ДиалогОткрытияФайла.Выбрать() Тогда
		Пути.Очистить();
		Пути.Добавить(ДиалогОткрытияФайла.Каталог);
	Иначе
		Возврат;
	КонецЕсли;
	
	ДобавитьВИсториюКонтекст(Элементы.ИсторияВыбораМест.Имя, Пути.ВыгрузитьЗначения());
	
	ОбновитьНадписьПути();
	LoadAllTests();
	
КонецПроцедуры

&НаКлиенте
Процедура КомандаЗагрузитьТестовыеСлучаи(Команда)
	
	Режим = РежимДиалогаВыбораФайла.Открытие;
	ДиалогОткрытияФайла = Новый ДиалогВыбораФайла(Режим);
	ДиалогОткрытияФайла.МножественныйВыбор = Истина;
	ДиалогОткрытияФайла.Фильтр = НСтр("en='External processing form files (*.epf, *.erf)|*.epf; *.erf'; ru='Файлы внешних обработок (*.epf, *.erf)|*.epf; *.erf'");
	ДиалогОткрытияФайла.Заголовок = НСтр("en='Choose an external processing form file'; ru='Выберите файл с внешней обработкой'");
	Если ДиалогОткрытияФайла.Выбрать() Тогда
		Пути.Очистить();
		Для каждого ПолноеИмяФайла Из ДиалогОткрытияФайла.ВыбранныеФайлы Цикл
			Пути.Добавить(ПолноеИмяФайла);
		КонецЦикла;
	Иначе
		Возврат;
	КонецЕсли;
	
	ДобавитьВИсториюКонтекст(Элементы.ИсторияВыбораФайлов.Имя, Пути.ВыгрузитьЗначения());
	
	ОбновитьНадписьПути();
	LoadAllTests();
	
	РазвернутьСтрокиДерева(Истина);
	
КонецПроцедуры

&НаКлиенте
Процедура КомандаЗагрузитьТолькоВыделенныеТесты(Команда)
	
	Пути.Очистить();
	МассивИдентификаторовСтрок = Элементы.ДеревоТестов.ВыделенныеСтроки;
	Для каждого Идентификатор Из МассивИдентификаторовСтрок Цикл
		
		СтрокаДерева = Объект.ДеревоТестов.НайтиПоИдентификатору(Идентификатор);
		Если СтрокаДерева.ТипСтроки = ЗначенияТиповСтроки.ТестовыйМетод Тогда
			СтрокаДерева = СтрокаДерева.ПолучитьРодителя();
		КонецЕсли;
		
		Если Пути.НайтиПоЗначению(СтрокаДерева.ПолныйПуть) = Неопределено Тогда
			Пути.Добавить(СтрокаДерева.ПолныйПуть);
		КонецЕсли;
		
	КонецЦикла;
	
	ОбновитьНадписьПути();
	LoadAllTests();
	
КонецПроцедуры

&НаКлиенте
Процедура КомандаВыполнитьВыделенныеТесты(Команда)
	ВыполнитьВыделенныеТесты();
КонецПроцедуры

&НаКлиенте
Процедура КомандаВыполнитьВыделенныеТестыСИсключением(Команда)
	Сообщить(НСтр("ru = 'Подключитесь на исключение ""ПередЗапускомТеста""'"));
	ВыполнитьВыделенныеТесты(Истина);
КонецПроцедуры


&НаКлиенте
Процедура ГенераторМакетовДанных(Команда)
	ИмяИнструмента = "xddDataFixtureGen";
	ОткрытьИнструмент(ИмяИнструмента);
КонецПроцедуры

&НаКлиенте
Процедура ПоказатьГУИД(Команда)
	ИмяИнструмента = "xddGuidShow";
	ОткрытьИнструмент(ИмяИнструмента);
КонецПроцедуры

&НаКлиенте
Процедура ОПроекте(Команда)
	ЗапуститьПриложение("https://github.com/xDrivenDevelopment/xUnitFor1C/wiki");
КонецПроцедуры

&НаКлиенте
Процедура КомандаЗагрузитьТестыИзКонфигурации(Команда)
	ЗагрузитьТестыИзКонфигурации("");
КонецПроцедуры

&НаКлиенте
Процедура КомандаЗагрузитьТестыИзКонфигурацииПоИмени(Команда)
	ЗагрузитьТестыИзКонфигурации("Тест_");
КонецПроцедуры

&НаКлиенте
Процедура КомандаРегистрацияПрохожденияТестовВЖР(Команда)
	
	ИзменитьЗначениеРеквизитаИУстановитьПометкуНаКнопке("РегистрацияПрохожденияТестовВЖР", "КомандаРегистрацияПрохожденияТестовВЖР");
	
КонецПроцедуры

&НаКлиенте
Процедура КомандаРегистрацияОшибокТестовВЖР(Команда)
	
	ИзменитьЗначениеРеквизитаИУстановитьПометкуНаКнопке("РегистрацияОшибокТестовВЖР", "КомандаРегистрацияОшибокТестовВЖР");
	
КонецПроцедуры

&НаКлиенте
Процедура КомандаВыполнятьТестыВСлучайномПорядке(Команда)
	ИзменитьЗначениеРеквизитаИУстановитьПометкуНаКнопке("СлучайныйПорядокВыполнения", "КомандаСлучайныйПорядокВыполнения");
КонецПроцедуры

&НаКлиенте
Процедура КомандаПерезагрузитьФорму(Команда)
	
	Закрыть();
	ОткрытьФормуЗапускаТестов();
	
КонецПроцедуры

&НаКлиенте
Процедура КомандаВключитьРежимОтладки(Команда)
	
	ИзменитьЗначениеРеквизитаИУстановитьПометкуНаКнопке("РежимОтладки", "КомандаВключитьРежимОтладки");
	Закрыть();
	
	ОткрытьФормуЗапускаТестов();
	
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьФормуЗапускаТестов(НастройкиФормы = Неопределено)
	
	путьФайла = ИмяФайлаВнешнейОбработки_ПерезагрузитьФорму();
	Если Не ЭтоВстроеннаяОбработка Тогда
		ИмяОбработки = ПодключитьНаСервереВнешнююОбработкуОтКлиента(путьФайла, "");
	КонецЕсли;
	
	ОткрытьФорму(ЭтаФорма.ИмяФормы, НастройкиФормы,, Истина);
	
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_КомандаЗагрузитьТестыИзИстории(Команда)
	
	ИмяИстории = Элементы[Команда.Имя].Родитель.Имя;
	ПунктИстории = Число(СтрЗаменить(Команда.Имя, ИмяИстории, ""));
	Пути.Очистить();
	Пути.Добавить(ЭтаФорма[ИмяИстории].Получить(ПунктИстории).Значение);
	
	ДобавитьВИсториюКонтекст(ИмяИстории, Пути.ВыгрузитьЗначения());
	
	ОбновитьНадписьПути();
	LoadAllTests();
	
КонецПроцедуры

//#КонецОбласти



////////////////////////////////////////////////////////////////////////////////
// СЛУЖЕБНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ

//#Область СлужебныеПроцедурыИФункции

////////////////////////////////////////////////////////////////////////////////
// СЛУЖЕБНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ ДЛЯ ЗАГРУЗКИ ВЫПОЛНЕНИЯ ТЕСТОВ 

//#Область СлужебныеПроцедурыИФункцииЗагрузкиВыполненияТестов

&НаСервере
Функция Объект() Экспорт
	Возврат РеквизитФормыВЗначение("Объект");
КонецФункции

&НаСервере
Функция ВерсияСервер()
	Возврат Объект().Версия();
КонецФункции

//#Область ВнешнееAPI

// { ВНЕШНЕЕ API

// Процедура загружает тесты из внешних файлов и из тестируемой конфигурации
//
// Параметры:
//	Нет
&НаКлиенте
Процедура LoadAllTests() Экспорт
	ИнициализироватьИндикаторВыполнения();
	
	Объект.ДеревоТестов.ПолучитьЭлементы().Очистить();
	
	Для каждого ЭлементСписка Из Пути Цикл
		
		ОбработкаПрерыванияПользователя();
		
		ПутьКТесту = ЭлементСписка.Значение;
		Если Найти(ВРег(ПутьКТесту), "МЕТАДАННЫЕ.") = 1 Тогда
			ЗагрузитьТестыКонфигурации(Объект.ДеревоТестов, ПутьКТесту);
		Иначе
			ОбъектФайл = Новый Файл(ПутьКТесту);
			Если ОбъектФайл.Существует() Тогда
				ЗагрузитьТесты(Объект.ДеревоТестов, ОбъектФайл);
			Иначе
				ТекстСообщения = НСтр("en='File or directory does not exist ""%ПутьКТесту%""'; ru='Файл или каталог не существует ""%ПутьКТесту%""'");
				ТекстСообщения = СтрЗаменить(ТекстСообщения, "%ПутьКТесту%", ПутьКТесту);
				ВывестиОшибку(ТекстСообщения);
			КонецЕсли;
		КонецЕсли;
		
	КонецЦикла;
	
	УдалитьПустыеСтрокиКаталогов(Объект.ДеревоТестов.ПолучитьЭлементы());
	ПосчитатьКоличествоТестовыхСлучаевПоВсемуДеревуТестов(Объект.ДеревоТестов);
	РазвернутьСтрокиДерева();
	
КонецПроцедуры

&НаКлиенте
Функция RunAllTests() Экспорт
	
	МассивИдентификаторовСтрок = Новый Массив();
	Для каждого ЭлементДерева Из Объект.ДеревоТестов.ПолучитьЭлементы() Цикл
		МассивИдентификаторовСтрок.Добавить(ЭлементДерева.ПолучитьИдентификатор());
	КонецЦикла;
	
	Возврат _ВыполнитьВыбранныеТесты(МассивИдентификаторовСтрок);
	
КонецФункции

&НаКлиенте
Функция OpenBrowserForm() Экспорт
	ЭтаФорма.Открыть();
КонецФункции

&НаКлиенте
Функция SetHandlerObject(obj) Экспорт
	MiddleMan = obj;
КонецФункции

// }

//#КонецОбласти

&НаКлиенте
Процедура ЗагрузитьТесты(РодительскаяСтрока, ОбъектФайл)
	
	Перем ЭтоФайл, ЭтоКаталог;
	
	ЭтоФайл = ТипЗнч(ОбъектФайл) = Тип("Файл");
	Если ЭтоФайл Тогда
		ЭтоКаталог = ОбъектФайл.ЭтоКаталог();
	ИначеЕсли ТипЗнч(ОбъектФайл) = Тип("Структура") Тогда
		ЭтоКаталог = ОбъектФайл.ТипСтроки = ЗначенияТиповСтроки.КаталогТестов;
		ОбъектФайл.Вставить("Расширение", ".epf");
	КонецЕсли;
	
	GUID = Строка(Новый УникальныйИдентификатор);
	Если ЭтоКаталог Тогда
		
		ПараметрыСтроки = ПолучитьПустуюСтруктуруПараметровСтроки();
		ПараметрыСтроки.Вставить("Имя",                 ОбъектФайл.Имя);
		ПараметрыСтроки.Вставить("ИмяТестовогоСлучая",  ОбъектФайл.Имя);
		ПараметрыСтроки.Вставить("_GUID",               GUID);
		ПараметрыСтроки.Вставить("ТипСтроки",           ЗначенияТиповСтроки.КаталогТестов);
		ПараметрыСтроки.Вставить("ПолныйПуть",          ОбъектФайл.ПолноеИмя);
		
		СтрокаКаталога = ДобавитьСтрокуДереваТестов(РодительскаяСтрока, ПараметрыСтроки); //TODO Развернуть, нужно ли тут оповещение ОповеститьОСобытии("TestLoaded", СтрокаТеста);?
		
		Если ЭтоФайл Тогда
			НайденныеФайлы = НайтиФайлы(ОбъектФайл.ПолноеИмя, "*", Ложь);
		Иначе
			НайденныеФайлы = ОбъектФайл.Строки;
		КонецЕсли;
		
		Для каждого Файл из НайденныеФайлы Цикл
			ОбработкаПрерыванияПользователя();
			
			ЗагрузитьТесты(СтрокаКаталога, Файл);
		КонецЦикла;
		
	ИначеЕсли ОбъектФайл.Расширение = ".epf" ИЛИ ОбъектФайл.Расширение = ".erf" Тогда
		
		ДополнительныеПараметры = Новый Структура;
		ДополнительныеПараметры.Вставить("GUID",              GUID);
		ДополнительныеПараметры.Вставить("Расширение",        ОбъектФайл.Расширение);
		ДополнительныеПараметры.Вставить("ВнешнийФайл",       ЭтоФайл);
		ДополнительныеПараметры.Вставить("ИмяБезРасширения",  ОбъектФайл.ИмяБезРасширения);
		ДополнительныеПараметры.Вставить("РежимОтладки",      Объект.РежимОтладки);
		
		Если ТипЗнч(РодительскаяСтрока) <> Тип("ДанныеФормыДерево") Тогда
			ДополнительныеПараметры.Вставить("Идентификатор", РодительскаяСтрока.ПолучитьИдентификатор());
		КонецЕсли;
		
		Если ЭтоФайл Тогда
			Если ЕстьПоддержкаАсинхронныхВызовов Тогда
				Оповещение = Вычислить("Новый ОписаниеОповещения(""ВыполнитьПодключениеОбъектаНаКлиенте"", ЭтаФорма, ДополнительныеПараметры)");
				Выполнить("НачатьПомещениеФайла(Оповещение,, ОбъектФайл.ПолноеИмя, Ложь, УникальныйИдентификатор);");
			Иначе
				АдресХранилища = "";
				ПоместитьФайл(АдресХранилища, ОбъектФайл.ПолноеИмя, , Ложь, УникальныйИдентификатор);
				ВыполнитьПодключениеОбъектаНаКлиенте(Неопределено, АдресХранилища, ОбъектФайл.ПолноеИмя, ДополнительныеПараметры);
			КонецЕсли;
		Иначе
			ВыполнитьПодключениеОбъектаНаКлиенте(Неопределено, ОбъектФайл.Имя, ОбъектФайл.ПолноеИмя, ДополнительныеПараметры);
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ЗагрузитьТестыИзКонфигурации(ОтборПоИмениТеста)
	
	ПараметрыОткрытия = Новый Структура("ОтборПоИмениТеста, РежимОткрытияОкна ", ОтборПоИмениТеста, РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	ИмяФормыВыбораПодсистемыУФ = СтрЗаменить(ЭтаФорма.ИмяФормы, "УправляемаяФорма", "ФормаВыбораПодсистемУФ");
	ОткрытьФорму(ИмяФормыВыбораПодсистемыУФ, ПараметрыОткрытия, ЭтаФорма);
	
КонецПроцедуры


&НаКлиенте
Функция ЗагрузитьТестыКонфигурации(РодительскаяСтрока, Путь) Экспорт
	
	КоллекцияВстроенныхТестов = Новый Массив;
	КоллекцияВстроенныхТестовСервер(КоллекцияВстроенныхТестов, Путь);
	
	Для каждого Элемент Из КоллекцияВстроенныхТестов Цикл
		ЗагрузитьТесты(РодительскаяСтрока, Элемент);
	КонецЦикла;
	
КонецФункции

&НаСервере
Функция КоллекцияВстроенныхТестовСервер(КоллекцияВстроенныхТестовСервер, Путь)
	
	ОсновнойОбъект = Объект();
	
	ДеревоВстроенныхТестов = Новый ДеревоЗначений;
	ДеревоВстроенныхТестов.Колонки.Добавить("Имя");
	ДеревоВстроенныхТестов.Колонки.Добавить("ИмяБезРасширения");
	ДеревоВстроенныхТестов.Колонки.Добавить("ПолноеИмя");
	ДеревоВстроенныхТестов.Колонки.Добавить("ТипСтроки");
	
	ОбъектМетаданных = ОсновнойОбъект.ПолучитьОбъектМетаданныхПоПути(Путь);
	Если ОбъектМетаданных<>Неопределено Тогда
		Если ВРег(Лев(ОбъектМетаданных.ПолноеИмя(),11)) = "ПОДСИСТЕМА." Тогда
			ДобавитьИменаТестовПодсистемы(ДеревоВстроенныхТестов, ОбъектМетаданных);
		Иначе
			ДобавитьИмяВстроенногоТестовогоСлучая(ДеревоВстроенныхТестов, ОбъектМетаданных);
		КонецЕсли;
		
		ВыгрузитьИменаТестовИзДереваВСписокЗначений(ДеревоВстроенныхТестов.Строки, КоллекцияВстроенныхТестовСервер);
	КонецЕсли;
	
КонецФункции

&НаСервере
Функция ДобавитьИменаТестовПодсистемы(СтрокаДерева, МетаПодсистема) Экспорт
	
	СтрокаТеста = СтрокаДерева.Строки.Добавить();
	СтрокаТеста.Имя = МетаПодсистема.Имя;
	СтрокаТеста.ПолноеИмя = "Метаданные." + СтрЗаменить(МетаПодсистема.ПолноеИмя(), "Подсистема.", "Подсистемы.");
	СтрокаТеста.ТипСтроки = ЗначенияТиповСтроки.КаталогТестов;
	
	Для каждого МетаПодчиненнаяПодсистема из МетаПодсистема.Подсистемы цикл
		ДобавитьИменаТестовПодсистемы(СтрокаТеста, МетаПодчиненнаяПодсистема);
	КонецЦикла;
	
	Для каждого МетаОбработка из МетаПодсистема.Состав цикл
		ДобавитьИмяВстроенногоТестовогоСлучая(СтрокаТеста, МетаОбработка);
	КонецЦикла;
	
КонецФункции

&НаСервере
Функция ДобавитьИмяВстроенногоТестовогоСлучая(СтрокаДерева, МетаОбработка)
	НоваяСтрока = СтрокаДерева.Строки.Добавить();
	НоваяСтрока.Имя = МетаОбработка.Имя;
	НоваяСтрока.ИмяБезРасширения = МетаОбработка.Имя;
	НоваяСтрока.ПолноеИмя = "Метаданные." + СтрЗаменить(МетаОбработка.ПолноеИмя(), "Обработка.", "Обработки.");
	НоваяСтрока.ТипСтроки = ЗначенияТиповСтроки.ТестовыйСлучай;
КонецФункции

&НаСервере
Процедура ВыгрузитьИменаТестовИзДереваВСписокЗначений(СтрокиДереваВстроенныхТестов, КоллекцияВстроенныхТестов)
	Для каждого Строка Из СтрокиДереваВстроенныхТестов Цикл
		НоваяКоллекция = Новый Массив;
		ЭлементКоллекции = Новый Структура("Имя,ИмяБезРасширения,ПолноеИмя,ТипСтроки,Строки");
		ЭлементКоллекции.Имя = Строка.Имя;
		ЭлементКоллекции.ИмяБезРасширения = Строка.ИмяБезРасширения;
		ЭлементКоллекции.ПолноеИмя = Строка.ПолноеИмя;
		ЭлементКоллекции.ТипСтроки = Строка.ТипСтроки;
		ЭлементКоллекции.Строки = НоваяКоллекция;
		КоллекцияВстроенныхТестов.Добавить(ЭлементКоллекции);
		Если Строка.Строки.Количество() > 0 Тогда
			ВыгрузитьИменаТестовИзДереваВСписокЗначений(Строка.Строки, НоваяКоллекция);
		КонецЕсли;
	КонецЦикла;
КонецПроцедуры


&НаКлиенте
Процедура ВыполнитьПодключениеОбъектаНаКлиенте(Результат, АдресХранилища, ВыбранноеИмяФайла, ДополнительныеПараметры) Экспорт
	
	Перем GUID;
	Если ТипЗнч(ДополнительныеПараметры) = Тип("Структура") Тогда
		ДополнительныеПараметры.Свойство("GUID", GUID);
	КонецЕсли;
	
	Идентификатор = ВыполнитьПодключениеОбъектаНаСервере(АдресХранилища, ВыбранноеИмяФайла, ДополнительныеПараметры);
	Для каждого Оповещение Из ОчередьОповещений Цикл
		СтрокаТеста = Объект.ДеревоТестов.НайтиПоИдентификатору(Оповещение.Значение);
		Если СтрокаТеста <> Неопределено Тогда
			ОповеститьОСобытии("TestLoaded", СтрокаТеста);
		КонецЕсли;
	КонецЦикла;
	ОчередьОповещений.Очистить();
	
	Если Идентификатор <> Неопределено Тогда
		
		СтрокаКаталога = Объект.ДеревоТестов.НайтиПоИдентификатору(Идентификатор);
		
		РезультатПоиска = ТаблицаИменаФорм.НайтиСтроки(Новый Структура("GUID, ОсновнаяФорма", GUID, Истина));
		Если РезультатПоиска.Количество() Тогда
			ЗагрузитьСписокТестовыхФормИзОсновнойФормыОбъекта(ЭтаФорма, СтрокаКаталога, РезультатПоиска[0], ДополнительныеПараметры);
		КонецЕсли;
		
		РезультатПоиска = ТаблицаИменаФорм.НайтиСтроки(Новый Структура("GUID", GUID));
		Для Каждого ЭлементПоиска Из РезультатПоиска Цикл
			ЗагрузитьТестыИзФормыОбъекта(ЭтаФорма, СтрокаКаталога, ЭлементПоиска, ДополнительныеПараметры);
		КонецЦикла;
		
		УдалитьПустуюСтрокуДерева(СтрокаКаталога, СтрокаКаталога.ПолучитьРодителя());
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Функция ВыполнитьПодключениеОбъектаНаСервере(АдресХранилища, ВыбранноеИмяФайла, ДополнительныеПараметры)
	
	Перем GUID, Расширение, ЭтоВнешнийФайл, ИмяБезРасширения, Идентификатор, РежимОтладки, ПолныйПуть;
	РежимОтладки = Ложь;
	
	ЮнитОбъект = Объект();
	Если ТипЗнч(ДополнительныеПараметры) = Тип("Структура") Тогда
		ДополнительныеПараметры.Свойство("GUID", GUID);
		ДополнительныеПараметры.Свойство("Расширение", Расширение);
		ДополнительныеПараметры.Свойство("ВнешнийФайл", ЭтоВнешнийФайл);
		ДополнительныеПараметры.Свойство("ИмяБезРасширения", ИмяБезРасширения);
		ДополнительныеПараметры.Свойство("Идентификатор", Идентификатор);
		ДополнительныеПараметры.Свойство("РежимОтладки", РежимОтладки);
	КонецЕсли;
	
	Если ЭтоВнешнийФайл Тогда
		ПолныйПуть = ВыбранноеИмяФайла;
	Иначе
		ПолныйПуть = АдресХранилища;
	КонецЕсли;
	
	Если Расширение = ".epf" Тогда
		МенеджерВстроенногоОбъекта = Обработки;
		МенеджерВнешнегоОбъекта = ВнешниеОбработки;
	ИначеЕсли Расширение = ".erf" Тогда
		МенеджерВстроенногоОбъекта = Отчеты;
		МенеджерВнешнегоОбъекта = ВнешниеОтчеты;
	КонецЕсли;
	
	Попытка
		Если ЭтоВнешнийФайл Тогда
			Если РежимОтладки = Истина Тогда
				Файл = Новый Файл(ВыбранноеИмяФайла);
				//Это файловый вариант или обработка доступна с сервера
				Если Файл.Существует() Тогда
					ВнешнийОбъект = ВнешниеОбработки.Создать(ВыбранноеИмяФайла);
				Иначе
					ДвоичныеДанные = ПолучитьИзВременногоХранилища(АдресХранилища);
					ВременноеИмяФайла = ПолучитьИмяВременногоФайла(Прав(Расширение,3));
					ДвоичныеДанные.Записать(ВременноеИмяФайла);
					ВнешнийОбъект = ВнешниеОбработки.Создать(ВременноеИмяФайла);
				КонецЕсли;
				ИмяОбъекта = МенеджерВнешнегоОбъекта.Подключить(АдресХранилища, ,РежимОтладки);
				
				Попытка
					//УдалитьФайлы(ВременноеИмяФайла);
				Исключение
				КонецПопытки;
				
			Иначе
				
				ИмяОбъекта = МенеджерВнешнегоОбъекта.Подключить(АдресХранилища, GUID, Ложь);
				ВнешнийОбъект = МенеджерВнешнегоОбъекта.Создать(GUID);
			КонецЕсли;
			
		Иначе
			ИмяОбъекта = АдресХранилища;
			ВнешнийОбъект = МенеджерВстроенногоОбъекта[ИмяОбъекта].Создать();
		КонецЕсли;
	Исключение
		ТекстОшибки = ОписаниеОшибки();
		ТекстСообщения = НСтр("ru='ОШИБКА загрузки тестов: %ТекстОшибки%, %ВыбранноеИмяФайла%'; en='Error loading testcases: %ТекстОшибки%, %ВыбранноеИмяФайла%'");
		ТекстСообщения = СтрЗаменить(ТекстСообщения, "%ТекстОшибки%", ТекстОшибки);
		ТекстСообщения = СтрЗаменить(ТекстСообщения, "%ВыбранноеИмяФайла%", ВыбранноеИмяФайла);
		ЮнитОбъект.ВывестиСообщение(ТекстСообщения);
		Возврат Неопределено;
	КонецПопытки;
	
	ПараметрыСтроки = ПолучитьПустуюСтруктуруПараметровСтроки();
	ПараметрыСтроки.Вставить("Имя",	ИмяБезРасширения);
	ПараметрыСтроки.Вставить("ИмяТестовогоСлучая", ИмяБезРасширения);
	ПараметрыСтроки.Вставить("_GUID", GUID);
	ПараметрыСтроки.Вставить("ТипСтроки", ЗначенияТиповСтроки.ТестовыйСлучай);
	ПараметрыСтроки.Вставить("ПолныйПуть", ПолныйПуть);
	ПараметрыСтроки.Вставить("ЭтоВнутренняяОбработка", НЕ ЭтоВнешнийФайл);
	Если Идентификатор = Неопределено Тогда
		НоваяСтрока = ДобавитьСтрокуДереваТестов(Объект.ДеревоТестов, ПараметрыСтроки); //TODO Развернуть, нужно ли тут оповещение ОповеститьОСобытии("TestLoaded", СтрокаТеста);?
	Иначе
		НоваяСтрока = ДобавитьСтрокуДереваТестов(Объект.ДеревоТестов.НайтиПоИдентификатору(Идентификатор), ПараметрыСтроки); //TODO Развернуть, нужно ли тут оповещение ОповеститьОСобытии("TestLoaded", СтрокаТеста);?
	КонецЕсли;
	
	ЗагрузитьСписокТестовыхФормИзМодуляОбъекта(ЮнитОбъект, НоваяСтрока, ВнешнийОбъект, ДополнительныеПараметры);
	ЗагрузитьТестыИзМодуляОбъекта(ЮнитОбъект, НоваяСтрока, ВнешнийОбъект);
	
	Возврат НоваяСтрока.ПолучитьИдентификатор();
	
КонецФункции

&НаКлиенте
Процедура ЗагрузитьТестыИзФормыОбъекта(ЮнитОбъект, Родитель, ВнешнийОбъект, ДополнительныеПараметры)
	
	Попытка
		ИмяФормыОбъекта = ПолучитьИмяФормыОбъекта(ВнешнийОбъект, ДополнительныеПараметры);
		ВнешняяФорма = ПолучитьФорму(ИмяФормыОбъекта, Новый Структура("АвтоТест"), ЭтаФорма);
	Исключение
		
		ТекстОшибки = ОписаниеОшибки();
		ТекстСообщения = НСтр("ru='ОШИБКА загрузки тестов: %ТекстОшибки%'; en='Error loading testcases: %ТекстОшибки%'");
		ТекстСообщения = СтрЗаменить(ТекстСообщения, "%ТекстОшибки%", ТекстОшибки);
		ЮнитОбъект.ВывестиСообщение(ТекстСообщения);
		Возврат;
		
	КонецПопытки;
	
	
	Попытка
		МассивТестовыхМетодов = ВнешняяФорма.ПолучитьСписокТестов(ЮнитОбъект);
	Исключение
		
		ПредставлениеОшибки = ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
		ЕстьОшибка_МетодОбъектаНеОбнаружен = ЕстьОшибка_МетодОбъектаНеОбнаружен(ПредставлениеОшибки, "ПолучитьСписокТестов");
		Если НЕ ЕстьОшибка_МетодОбъектаНеОбнаружен Тогда
			ТекстОшибки = НСтр("ru = 'Не удалось загрузить список тестов для модуля объекта:
									 |  Ошибка получения списка тестовых случаев: %1'");
			ЮнитОбъект.ВывестиОшибку(СтрЗаменить(ТекстОшибки, "%1", ОписаниеОшибки()));
		КонецЕсли;
		
		Возврат;
		
	КонецПопытки;
	
	Для каждого ЭлементМассива Из МассивТестовыхМетодов Цикл
		
		ПараметрыСтроки = ПолучитьСтруктуруПараметровСтроки(
			ЭлементМассива,
			Родитель.ПолныйПуть,
			ВнешнийОбъект.ИмяОбъекта,
			ЗначениеМодульИлиФорма.Форма,
			Родитель.ЭтоВнутренняяОбработка,
			ВнешнийОбъект.ИмяФормы);
		
		СтрокаТеста = Родитель.ПолучитьЭлементы().Добавить();
		ЗаполнитьЗначенияСвойств(СтрокаТеста, ПараметрыСтроки);
		
		ОповеститьОСобытии("TestLoaded", СтрокаТеста);
		
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура ЗагрузитьСписокТестовыхФормИзОсновнойФормыОбъекта(ЮнитОбъект, Родитель, ВнешнийОбъект, ДополнительныеПараметры)
	
	Перем GUID;
	
	МассивТестовыхФорм = Новый Массив;
	Если ТипЗнч(ДополнительныеПараметры) = Тип("Структура") Тогда
		ДополнительныеПараметры.Свойство("GUID", GUID);
	КонецЕсли;
	
	Попытка
		ИмяФормыОбъекта = ПолучитьИмяФормыОбъекта(ВнешнийОбъект, ДополнительныеПараметры);
		ВнешняяФорма = ПолучитьФорму(ИмяФормыОбъекта, Новый Структура("АвтоТест"), ЭтаФорма);
	Исключение
		
		ТекстОшибки = ОписаниеОшибки();
		ТекстСообщения = НСтр("ru='ОШИБКА загрузки тестовых форм из основной формы объекта: %ТекстОшибки%'; en='Error loading testforms from main object form: %ТекстОшибки%'");
		ТекстСообщения = СтрЗаменить(ТекстСообщения, "%ТекстОшибки%", ТекстОшибки);
		ЮнитОбъект.ВывестиСообщение(ТекстСообщения);
		Возврат;
		
	КонецПопытки;
	
	Попытка
		
		МассивТестовыхФорм = ВнешняяФорма.ПолучитьСписокТестовыхФорм();
		
		ПараметрыОтбора = Новый Структура("GUID, ОсновнаяФорма", GUID, Ложь);
		РезультатОтбора = ТаблицаИменаФорм.НайтиСтроки(ПараметрыОтбора);
		Для Каждого ЭлементОтбора Из РезультатОтбора Цикл
			ТаблицаИменаФорм.Удалить(ЭлементОтбора);
		КонецЦикла;
		
		Для Каждого ИмяТестовойФормы Из МассивТестовыхФорм Цикл
			Если ИмяТестовойФормы <> ВнешнийОбъект.ИмяФормы Тогда
				НоваяЗапись = ТаблицаИменаФорм.Добавить();
				НоваяЗапись.GUID = GUID;
				НоваяЗапись.ИмяФормы = ИмяТестовойФормы;
				НоваяЗапись.ИмяОбъекта = ВнешнийОбъект.ИмяОбъекта;
				НоваяЗапись.ОсновнаяФорма = Ложь;
			КонецЕсли;
		КонецЦикла;
		
	Исключение
		
		ПредставлениеОшибки = ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
		ЕстьОшибка_МетодОбъектаНеОбнаружен = ЕстьОшибка_МетодОбъектаНеОбнаружен(ПредставлениеОшибки, "ПолучитьСписокТестовыхФорм");
		Если НЕ ЕстьОшибка_МетодОбъектаНеОбнаружен Тогда
			ТекстОшибки = НСтр("ru = 'Не удалось загрузить список тестовых форм объекта:
									 |  Ошибка получения списка тестовых форм: %1'");
			ЮнитОбъект.ВывестиОшибку(СтрЗаменить(ТекстОшибки, "%1", ОписаниеОшибки()));
			Родитель.Состояние = ЗначенияСостоянияТестов.Сломался; // TODO: нужно ли отображение, что при получении списка форм вывалилась ошибка?
		КонецЕсли;
		
	КонецПопытки;
	
КонецПроцедуры


&НаСервере
Процедура ЗагрузитьТестыИзМодуляОбъекта(ЮнитОбъект, Родитель, ВнешнийОбъект)
	
	Попытка
		МассивТестовыхМетодов = ВнешнийОбъект.ПолучитьСписокТестов(ЮнитОбъект);
	Исключение
		
		ПредставлениеОшибки = ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
		ЕстьОшибка_МетодОбъектаНеОбнаружен = ЕстьОшибка_МетодОбъектаНеОбнаружен(ПредставлениеОшибки, "ПолучитьСписокТестов");
		Если НЕ ЕстьОшибка_МетодОбъектаНеОбнаружен Тогда
			ТекстОшибки = НСтр("ru = 'Не удалось загрузить список тестов для модуля объекта:
									 |  Ошибка получения списка тестовых случаев: %1'");
			ЮнитОбъект.ВывестиОшибку(СтрЗаменить(ТекстОшибки, "%1", ОписаниеОшибки()));
			Родитель.Состояние = ЗначенияСостоянияТестов.Сломался;
		КонецЕсли;
		
		Возврат;
		
	КонецПопытки;
	
	МетаданныеИмя = ВнешнийОбъект.Метаданные().Имя;
	Для каждого ЭлементМассива Из МассивТестовыхМетодов Цикл
		
		ПараметрыСтроки = ПолучитьСтруктуруПараметровСтроки(
			ЭлементМассива,
			МетаданныеИмя,
			МетаданныеИмя,
			ЗначениеМодульИлиФорма.Модуль,
			Родитель.ЭтоВнутренняяОбработка);
		
		СтрокаТеста = Родитель.ПолучитьЭлементы().Добавить();
		ЗаполнитьЗначенияСвойств(СтрокаТеста, ПараметрыСтроки);
		
		ОчередьОповещений.Добавить(СтрокаТеста.ПолучитьИдентификатор());
		
	КонецЦикла;
	
КонецПроцедуры

// Загружает список управляемых форм из которых нужно получить тестовые случаи
//
// Параметры:
//	ЮнитОбъект 	  			- ОбработкаОбъект 			- Преобразованный реквизит формы "Объект" в объект прикладного тип
//  Родитель   	  			- ДанныеФормыЭлементДерева 	- Элемент дерева "ДеревоТестов"
//  ВнешнийОбъект 			- ВнешняяОбработкаОбъект, 
//							  ВнешнийОтчетОбъект, 
//							  ОбработкаОбъект, 
//							  ОтчетОбъект 				- Объект содержащий тестовые случаи  
//  ДополнительныеПараметры - Структура 				- Структура с дополнительными параметрами
//		* GUID - Строка(36) - Уникальный идентификатор внешнего объекта 
//
&НаСервере
Процедура ЗагрузитьСписокТестовыхФормИзМодуляОбъекта(ЮнитОбъект, Родитель, ВнешнийОбъект, ДополнительныеПараметры)
	
	Перем GUID;
	
	МетаданныеОбъекта = ВнешнийОбъект.Метаданные();
	Если МетаданныеОбъекта.Формы.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	МассивТестовыхФорм = Новый Массив;
	Если ТипЗнч(ДополнительныеПараметры) = Тип("Структура") Тогда
		ДополнительныеПараметры.Свойство("GUID", GUID);
	КонецЕсли;
	
	// Обычные формы при проведении тестов в управляемом режиме не поддерживаются
	ТипФормыОбъекта = Метаданные.СвойстваОбъектов.ТипФормы.Управляемая;
	
	Попытка
		МассивТестовыхФорм = ВнешнийОбъект.ПолучитьСписокТестовыхФорм();
	Исключение
		
		ПредставлениеОшибки = ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
		ЕстьОшибка_МетодОбъектаНеОбнаружен = ЕстьОшибка_МетодОбъектаНеОбнаружен(ПредставлениеОшибки, "ПолучитьСписокТестовыхФорм");
		Если НЕ ЕстьОшибка_МетодОбъектаНеОбнаружен Тогда
			ТекстОшибки = НСтр("ru = 'Не удалось загрузить список тестовых форм объекта:
									 |  Ошибка получения списка тестовых форм: %1'");
			ЮнитОбъект.ВывестиОшибку(СтрЗаменить(ТекстОшибки, "%1", ОписаниеОшибки()));
			Родитель.Состояние = ЗначенияСостоянияТестов.Сломался; // TODO: нужно ли отображение, что при получении списка форм вывалилась ошибка?
		КонецЕсли;
		
	КонецПопытки;
	
	Если МетаданныеОбъекта.ОсновнаяФорма <> Неопределено
	   И МетаданныеОбъекта.ОсновнаяФорма.ТипФормы = ТипФормыОбъекта Тогда
		
		ИмяОсновнойФормы = МетаданныеОбъекта.ОсновнаяФорма.Имя;
		
	ИначеЕсли МетаданныеОбъекта.ДополнительнаяФорма <> Неопределено 
			И МетаданныеОбъекта.ДополнительнаяФорма.ТипФормы = ТипФормыОбъекта Тогда
		
		ИмяОсновнойФормы = МетаданныеОбъекта.ДополнительнаяФорма.Имя;
		
	Иначе
		ИмяОсновнойФормы = Неопределено;
	КонецЕсли;
	
	Для каждого Форма Из МетаданныеОбъекта.Формы Цикл
		Если Форма.ТипФормы = ТипФормыОбъекта Тогда
			Если Форма.Имя <> ИмяОсновнойФормы Тогда
				Если МассивТестовыхФорм.Найти(Форма.Имя) = Неопределено Тогда
					Продолжить;
				КонецЕсли;
			КонецЕсли;
			
			НоваяЗапись = ТаблицаИменаФорм.Добавить();
			НоваяЗапись.GUID = GUID;
			НоваяЗапись.ИмяФормы = Форма.Имя;
			НоваяЗапись.ИмяОбъекта = МетаданныеОбъекта.Имя;
			НоваяЗапись.ОсновнаяФорма = Форма.Имя = ИмяОсновнойФормы;
			
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры


// Функция возвращает полное имя формы объекта
//
&НаКлиенте
Функция ПолучитьИмяФормыОбъекта(ВнешнийОбъект, ДополнительныеПараметры)
	
	Перем Расширение, ЭтоВнешнийФайл;
	Если ТипЗнч(ДополнительныеПараметры) = Тип("Структура") Тогда
		ДополнительныеПараметры.Свойство("Расширение", Расширение);
		ДополнительныеПараметры.Свойство("ВнешнийФайл", ЭтоВнешнийФайл);
	КонецЕсли;
	
	ИмяФормыОбъекта = "%Объект%.%ИмяОбъекта%.Форма.%ИмяФормы%";
	ИмяФормыОбъекта = СтрЗаменить(ИмяФормыОбъекта, "%ИмяФормы%", ВнешнийОбъект.ИмяФормы);
	ИмяФормыОбъекта = СтрЗаменить(ИмяФормыОбъекта, "%ИмяОбъекта%", ВнешнийОбъект.ИмяОбъекта);
	Если 		 ЭтоВнешнийФайл И Расширение = ".epf" Тогда
		ИмяФормыОбъекта = СтрЗаменить(ИмяФормыОбъекта, "%Объект%", "ВнешняяОбработка");
	ИначеЕсли 	 ЭтоВнешнийФайл И Расширение = ".erf" Тогда
		ИмяФормыОбъекта = СтрЗаменить(ИмяФормыОбъекта, "%Объект%", "ВнешнийОтчет");
	ИначеЕсли НЕ ЭтоВнешнийФайл И Расширение = ".epf" Тогда
		ИмяФормыОбъекта = СтрЗаменить(ИмяФормыОбъекта, "%Объект%", "Обработка");
	ИначеЕсли НЕ ЭтоВнешнийФайл И Расширение = ".erf" Тогда
		ИмяФормыОбъекта = СтрЗаменить(ИмяФормыОбъекта, "%Объект%", "Отчет");
	Иначе
		ИмяФормыОбъекта = Неопределено;
	КонецЕсли;
	
	Возврат ИмяФормыОбъекта;
	
КонецФункции

// Функция добавляет новую строку в ДеревоЗначений, которая соответствует тестовому случаю
//
// Возвращаемое значение:
//	НоваяСтрока - ДанныеФормыЭлементДерева - Новая строка в дереве тестов
//
&НаКлиентеНаСервереБезКонтекста
Функция ДобавитьСтрокуДереваТестов(Родитель, ПараметрыСтроки)
	
	НоваяСтрока = Родитель.ПолучитьЭлементы().Добавить();
	ЗаполнитьЗначенияСвойств(НоваяСтрока, ПараметрыСтроки);
	
	//ОповеститьОСобытии("TestLoaded", НоваяСтрока); //TODO Возможно удалить всю функцию?
	
	Возврат НоваяСтрока;
	
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Функция ПолучитьПустуюСтруктуруПараметровСтроки()
	
	ПараметрыСтроки = Новый Структура;
	ПараметрыСтроки.Вставить("Имя");
	ПараметрыСтроки.Вставить("ИмяТестовогоСлучая");
	ПараметрыСтроки.Вставить("_GUID");
	ПараметрыСтроки.Вставить("Состояние");
	ПараметрыСтроки.Вставить("ТипСтроки");
	ПараметрыСтроки.Вставить("ПолныйПуть");
	ПараметрыСтроки.Вставить("МодульИлиФорма");
	ПараметрыСтроки.Вставить("ПараметрыТеста");
	ПараметрыСтроки.Вставить("КоличествоТестов");
	ПараметрыСтроки.Вставить("ЕстьПараметрыТеста");
	ПараметрыСтроки.Вставить("ТестовыйСлучайОбъектМетаданныхИмя");
	ПараметрыСтроки.Вставить("ЭтоВнутренняяОбработка");
	
	Возврат ПараметрыСтроки;
	
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Функция ПолучитьСтруктуруПараметровСтроки(ЭлементМассива, ПолныйПуть, МетаданныеИмя, МодульИлиФорма, ЭтоВнутренняяОбработка, ИмяОсновнойУправляемойФормы = "")
	
	ПараметрыСтроки = ПолучитьПустуюСтруктуруПараметровСтроки();
	Если ТипЗнч(ЭлементМассива) = Тип("Строка") Тогда
		ПараметрыСтроки.Вставить("Имя", ЭлементМассива);
		ПараметрыСтроки.Вставить("ИмяТестовогоСлучая", ЭлементМассива);
	Иначе
		ПредставлениеТеста = ?(ЭлементМассива.Свойство("ПредставлениеТеста") и ЗначениеЗаполнено(ЭлементМассива.ПредставлениеТеста), ЭлементМассива.ПредставлениеТеста, ЭлементМассива.ИмяТеста);
		ПараметрыСтроки.Вставить("Имя", ПредставлениеТеста);
		ПараметрыСтроки.Вставить("ИмяТестовогоСлучая", ЭлементМассива.ИмяТеста);
		
		ЗаданоСпециальноеПредставлениеТеста = ПараметрыСтроки.Имя <> ПараметрыСтроки.ИмяТестовогоСлучая;
		
		СписокЗначений = Новый СписокЗначений;
		СписокЗначений.Добавить(ЭлементМассива);
		ПараметрыСтроки.Вставить("ПараметрыТеста", СписокЗначений);
		ПараметрыСтроки.Вставить("ЕстьПараметрыТеста", Истина);
			
		Если Не ЗаданоСпециальноеПредставлениеТеста Тогда
			ОписаниеПараметра = "<пусто>";
			Если ЭлементМассива.Свойство("Параметр") Тогда
				ОписаниеПараметра = "" + ЭлементМассива.Параметр;
			КонецЕсли;
			ПараметрыСтроки.Вставить("Имя", ЭлементМассива.ИмяТеста +  "( " + ОписаниеПараметра + " )");
		КонецЕсли;
	КонецЕсли;
	
	ПараметрыСтроки.Вставить("_GUID", Строка(Новый УникальныйИдентификатор));
	ПараметрыСтроки.Вставить("ТипСтроки", 2); // ЗначенияТиповСтроки.ТестовыйМетод
	ПараметрыСтроки.Вставить("ПолныйПуть", ПолныйПуть);
	ПараметрыСтроки.Вставить("МодульИлиФорма", МодульИлиФорма);
	ПараметрыСтроки.Вставить("ТестовыйСлучайОбъектМетаданныхИмя", МетаданныеИмя);
	ПараметрыСтроки.Вставить("ЭтоВнутренняяОбработка", ЭтоВнутренняяОбработка);
	ПараметрыСтроки.Вставить("ИмяОсновнойУправляемойФормы", ИмяОсновнойУправляемойФормы);
	
	Возврат ПараметрыСтроки;
	
КонецФункции


//#Область ОбработкаПараметровЗапуска

// { Обработка параметров запуска

&НаКлиенте
Процедура ЗаписатьКодВозвратаВЛогФайл(ПутьКФайлуЛога, КодВозврата, КодРезультатаТестирования = Неопределено)
	ФайлЛога = Новый Файл(ПутьКФайлуЛога);
	КаталогЛога = Новый Файл(ФайлЛога.Путь);
	Проверить(КаталогЛога.Существует(), "Ожидали, что каталог лог-файла кода возврата будет существовать, а он не существует. <"+ФайлЛога.Путь+">");
	
	Запись = Новый ЗаписьТекста(ПутьКФайлуЛога);
	Запись.ЗаписатьСтроку(КодВозврата);
	Если КодРезультатаТестирования <> Неопределено Тогда
		Запись.ЗаписатьСтроку(КодРезультатаТестирования);
	КонецЕсли;
	Запись.Закрыть();
	
КонецПроцедуры

// Пример строки запуска - "Тесты_Команда_Тестировать;E:\ТестыИМ\Тесты;ЗавершитьРаботуПослеТестирования"
//	"C:\Program Files (x86)\1cv82\8.2.18.96\bin\1cv8c.exe" /DisableStartupMessages /Execute E:\ТестыИМ\xddTestRunner.epf /S "server\ib" /Out "E:\ТестыИМ\Тесты\log.txt"  /C "Тесты_Команда_Тестировать;E:\ТестыИМ\Тесты;ЗавершитьРаботуПослеТестирования" /LogUI
&НаКлиенте
Функция ОбработатьПараметрыЗапуска(СтрокаПараметровЗапуска)
	
	ПараметрыЗапуска = РазобратьПараметрыЗапускаСервер(СтрокаПараметровЗапуска);
	Если ПараметрыЗапуска = Неопределено Тогда
		Возврат Ложь;
	КонецЕсли;
	
	ЭтоЗапускИзКоманднойСтроки = Истина;
	УстановитьЗначенияНастроекПоУмолчанию();
	
	естьКоманда_ВыполнитьТестирование   = ПараметрыЗапуска.Выполнить;
	естьКоманда_Загрузить               = ПараметрыЗапуска.Загрузить;
	путьКТестам                         = ПараметрыЗапуска.ПутьТестов;
	лЗавершитьРаботуПослеТестирования   = ПараметрыЗапуска.ОстановитьСистему;
	
	ПутьОтчетаТестирования  = ПараметрыЗапуска.ПутьОтчета;
	ТипОтчетаОТестировании  = ПараметрыЗапуска.ФорматОтчета;
	ПутьКФайлуЛога          = ПараметрыЗапуска.ПутьКодаВозврата;
	
	Объект.РегистрацияПрохожденияТестовВЖР = ПараметрыЗапуска.РегистрацияПрохожденияТестовВЖР;
	Элементы.КомандаРегистрацияПрохожденияТестовВЖР.Пометка = Объект.РегистрацияПрохожденияТестовВЖР;
	
	Объект.РегистрацияОшибокТестовВЖР = ПараметрыЗапуска.РегистрацияОшибокТестовВЖР;
	Элементы.КомандаРегистрацияОшибокТестовВЖР.Пометка = Объект.РегистрацияОшибокТестовВЖР;
	
	Объект.РежимОтладки = ПараметрыЗапуска.РежимОтладки;
	Элементы.КомандаВключитьРежимОтладки.Пометка = Объект.РежимОтладки;
	
	Объект.СлучайныйПорядокВыполнения = ПараметрыЗапуска.СлучайныйПорядокВыполнения;
	Элементы.КомандаСлучайныйПорядокВыполнения.Пометка = Объект.СлучайныйПорядокВыполнения;
	
	Объект.ВыводЛогаВФорматеTeamCity = ПараметрыЗапуска.ВыводЛогаВФорматеTeamCity;
	
	мЭтоВнутренняяОбработка = ПараметрыЗапуска.ВстроенныеТесты;
	
	Если ЗначениеЗаполнено(путьКТестам) Тогда
		Пути.Добавить(путьКТестам);
	КонецЕсли;
	
	Если Не естьКоманда_Загрузить Тогда
		Возврат Ложь;
	КонецЕсли;
	
	Если естьКоманда_ВыполнитьТестирование Тогда
		Если НЕ мЭтоВнутренняяОбработка Тогда
			ОписаниеОшибки = НСтр("ru = 'ОШИБКА загрузки тестов: Ожидали, что каталог/файл тестов будет существовать, а он не существует.'");
			Если Не ПроверитьСуществованиеФайла(путьКТестам, ОписаниеОшибки, лЗавершитьРаботуПослеТестирования) Тогда
				Возврат Истина;
			КонецЕсли;
		КонецЕсли;
		
		ФайлИлиКаталогОтчетовТестирования = Новый Файл(ПутьОтчетаТестирования);
		ОписаниеОшибки =  НСтр("ru = 'ОШИБКА загрузки тестов: Ожидали, что каталог отчетов тестирования будет существовать, а он не существует.'");
		Если Не ПроверитьСуществованиеФайла(ФайлИлиКаталогОтчетовТестирования.Путь, ОписаниеОшибки, лЗавершитьРаботуПослеТестирования) Тогда
			Возврат Истина;
		КонецЕсли;
		
	КонецЕсли;
	
	ЗаписатьКодВозвратаВЛогФайл(ПутьКФайлуЛога, "1"); //По умолчанию считаем что ошибка. Перед завершением работы запишем 0.
	
	ОбновитьНадписьПути();
	
	Попытка
		LoadAllTests();
	Исключение
		
		ТекстОшибки = ОписаниеОшибки();
		ТекстСообщения = НСтр("ru='ОШИБКА загрузки тестов: %ТекстОшибки%'; en='Error loading testcases: %ТекстОшибки%'");
		ТекстСообщения = СтрЗаменить(ТекстСообщения, "%ТекстОшибки%", ТекстОшибки); 
		ВывестиСообщение(ТекстСообщения);
		
		Если лЗавершитьРаботуПослеТестирования Тогда
			ЗавершитьРаботуСистемы(Ложь);
			Возврат Ложь;
		КонецЕсли;
		
	КонецПопытки;
	
	Если естьКоманда_ВыполнитьТестирование Тогда
		
		Попытка
			КодРезультатаТестирования = RunAllTests();
		Исключение
			
			ТекстОшибки = ОписаниеОшибки();
			ТекстСообщения = НСтр("ru='ОШИБКА выполнения тестов: %ТекстОшибки%'; en='Error running testcases: %ТекстОшибки%'");
			ТекстСообщения = СтрЗаменить(ТекстСообщения, "%ТекстОшибки%", ТекстОшибки); 
			ВывестиСообщение(ТекстСообщения);
			
			Если лЗавершитьРаботуПослеТестирования Тогда
				ЗавершитьРаботуСистемы(Ложь);
				Возврат Ложь;
			КонецЕсли;
			
		КонецПопытки;
		
	КонецЕсли;
	
	Если лЗавершитьРаботуПослеТестирования Тогда
		
		ЗаписатьКодВозвратаВЛогФайл(ПутьКФайлуЛога, "0", КодРезультатаТестирования);
		ЗавершитьРаботуСистемы(Ложь);
		
	КонецЕсли;
	
	Возврат Истина;
	
КонецФункции

&НаСервере
Функция РазобратьПараметрыЗапускаСервер(СтрокаПараметровЗапуска)
	Возврат Объект().РазобратьПараметрыЗапуска(СтрокаПараметровЗапуска);
КонецФункции

&НаКлиенте
Функция ПроверитьСуществованиеФайла(ПутьФайла, Знач ОписаниеОшибки, ЗавершитьРаботуПослеТестирования)
	Файл = Новый Файл(ПутьФайла);
	Если НЕ Файл.Существует() Тогда
		ОписаниеОшибки = ОписаниеОшибки + СтрЗаменить(НСтр("ru = 'путь <%1>'"), "%1", ПутьФайла);
		ВывестиСообщение(ОписаниеОшибки);
		ЗарегистрироватьОшибкуЗапускаТестированияСервер(Файл.ПолноеИмя, ОписаниеОшибки);
		
		Если ЗавершитьРаботуПослеТестирования Тогда
			ЗавершитьРаботуСистемы(Ложь);
		КонецЕсли;
		Возврат Ложь;
	КонецЕсли;
	Возврат Истина;
КонецФункции

&НаСервере
Функция ЗарегистрироватьОшибкуЗапускаТестированияСервер(ПолныйПуть, ОписаниеОшибки)
	Объект().ЗарегистрироватьОшибкуЗапускаТестирования(ПолныйПуть, ОписаниеОшибки);
КонецФункции

// }

//#КонецОбласти



&НаКлиенте
Процедура ОбновитьНадписьПути()
	
	Если Пути.Количество() = 1 Тогда
		ПутиНадпись = Пути[0].Значение;
		Элементы.ПутиНадпись.МногострочныйРежим = Ложь;
	Иначе
		ПутиНадпись = "";
		КонечныйИндекс = Пути.Количество() - 1;
		Для Индекс = 0 По КонечныйИндекс Цикл
			Если Индекс = КонечныйИндекс Тогда
				ПутиНадпись = ПутиНадпись + Пути[Индекс].Значение;
			Иначе
				ПутиНадпись = ПутиНадпись + Пути[Индекс].Значение + Символы.ПС;
			КонецЕсли;
		КонецЦикла;
		Элементы.ПутиНадпись.МногострочныйРежим = Истина;
	КонецЕсли;
	
КонецПроцедуры

// Процедура изменяет значение реквизита обработки и устанавливает пометку на кнопке командной панели
//
// Параметры:
//	ИмяРеквизитаОбъекта 	 - Строка - Имя реквизита типа "Булево"
//  ИмяКнопкиКоманднойПанели - Строка - Имя кнопки командной панели для которой нужно изменить реквизит "Пометка"
//
&НаКлиенте
Процедура ИзменитьЗначениеРеквизитаИУстановитьПометкуНаКнопке(ИмяРеквизитаОбработки, ИмяКнопкиКоманднойПанели)
	
	Объект[ИмяРеквизитаОбработки] = НЕ Объект[ИмяРеквизитаОбработки];
	Элементы[ИмяКнопкиКоманднойПанели].Пометка = Объект[ИмяРеквизитаОбработки];
	
КонецПроцедуры



&НаКлиенте
Функция ВывестиОшибку(Ошибка) Экспорт
	НужныйТекстОшибки = ПолучитьРазвернутыйТекстОшибки(Ошибка);
	
	ВывестиСообщение(НужныйТекстОшибки, СтатусСообщения.Важное);
	
	Возврат НужныйТекстОшибки;
КонецФункции

Функция ПолучитьРазвернутыйТекстОшибки(Ошибка)
	ТипОбъектаОшибка = ТипЗнч(Ошибка);
	
	Если ТипОбъектаОшибка = Тип("Структура") Тогда
		
		НужныйТекстОшибки = "[" + Ошибка.ИмяТестовогоНабора + ": " + Ошибка.ИмяТестовогоСлучая + "] " + Ошибка.Описание;
		
	ИначеЕсли ТипОбъектаОшибка = Тип("ИнформацияОбОшибке") Тогда
		
		НужныйТекстОшибки = Ошибка.Описание;
		
	Иначе
		
		НужныйТекстОшибки = Ошибка;
		
	КонецЕсли;
	Возврат НужныйТекстОшибки;
КонецФункции

// Выводит сообщение. В тестах ВСЕГДА должна использоваться ВМЕСТО метода Сообщить().
// 
&НаКлиенте
Функция ВывестиСообщение(ТекстСообщения, Статус = Неопределено) Экспорт
	Если MiddleMan <> Неопределено Тогда
		Если НЕ ОповеститьОСобытии("Message", ТекстСообщения) Тогда
			ОповеститьОСобытии("ВывестиСообщение", ТекстСообщения);
		КонецЕсли;
	Иначе
		Если Объект.ВыводЛогаВФорматеTeamCity Тогда
			ТекстСообщения = СтрЗаменить(ТекстСообщения,"|","||");
			ТекстСообщения = СтрЗаменить(ТекстСообщения,"'","|'");
			ТекстСообщения = СтрЗаменить(ТекстСообщения,"[","|[");
			ТекстСообщения = СтрЗаменить(ТекстСообщения,"]","|]");
			ТекстСообщения = СтрЗаменить(ТекстСообщения,Символы.ВК,"|r");
			ТекстСообщения = СтрЗаменить(ТекстСообщения,Символы.ПС,"|n");
			
			Сообщить("##teamcity[message text='"+ТекстСообщения+"' errorDetails='' status='"+СоответствиеСтатусовДляTeamCity[Статус]+"']");
		Иначе
			Сообщить(ТекстСообщения, Статус);
		КонецЕсли;
	КонецЕсли;
КонецФункции

// Вызывает исключение с заданным текстом ошибки для прерывания выполнения тестового случая.
// 
&НаКлиенте
Функция ПрерватьТест(ТекстОшибки) Экспорт
	
	ВызватьИсключение ТекстОшибки;
	
КонецФункции


//#КонецОбласти

//#КонецОбласти



// { Методы генерации тестовых данных

// количествоСозданныхОбъектов учитывает только созданные элементы справочников, документы и пользователей ИБ. Записи регистров сведений не считаются!
&НаКлиенте
Функция СоздатьДанныеПоТабличномуДокументу(ТабличныйДокумент, количествоСозданныхОбъектов = 0) Экспорт
	Данные = СоздатьДанныеПоТабличномуДокументуСервер(ТабличныйДокумент, количествоСозданныхОбъектов);
	Возврат Данные;
КонецФункции

&НаСервере
Функция СоздатьДанныеПоТабличномуДокументуСервер(ТабличныйДокумент, количествоСозданныхОбъектов = 0) Экспорт
	Данные = Объект().СоздатьДанныеПоТабличномуДокументу(ТабличныйДокумент, количествоСозданныхОбъектов);
	Возврат Неопределено; //Данные;
КонецФункции
//}








// { работа с тестами



&НаСервере
Функция ПолучитьИмяОсновнойУправляемойФормы(имОбработки)
	Попытка
		лМетаданные = ВнешниеОбработки.Создать(имОбработки).Метаданные();
	Исключение
		лМетаданные = Метаданные.Обработки[имОбработки];
	КонецПопытки;
	МассивФорм = Новый Массив;
	МассивФорм.Добавить(лМетаданные.ОсновнаяФорма);
	МассивФорм.Добавить(лМетаданные.ДополнительнаяФорма);
	Для каждого метаФорма Из МассивФорм Цикл
		Если метаФорма = Неопределено Тогда
			Продолжить;
		КонецЕсли;
		Если метаФорма.ТипФормы = Метаданные.СвойстваОбъектов.ТипФормы.Управляемая Тогда
			Возврат метаФорма.Имя;
		КонецЕсли;
	КонецЦикла;
	Возврат "";
КонецФункции

&НаКлиенте
Функция ПолучитьТестОбъект(ИдентификаторОбработки, имяОсновнойУправляемойФормы)
	Попытка
		Возврат ПолучитьФорму("ВнешняяОбработка." + ИдентификаторОбработки + ".Форма." + имяОсновнойУправляемойФормы);
	Исключение
		Попытка
			Возврат ПолучитьФорму("Обработка." + ИдентификаторОбработки + ".Форма." + имяОсновнойУправляемойФормы);
		Исключение
			Возврат Неопределено;
		КонецПопытки;
	КонецПопытки;
КонецФункции

&НаКлиенте
Функция ПодключитьНаСервереВнешнююОбработкуОтКлиента(ПолныйПутьФайлаОбработки, ИдентификаторОбработки, РежимОтладки = Ложь)
	
	Перем ИмяПодключеннойВнешнейОбработки;
	
	Если ЕстьВстроеннаяОбработкаСервер(ИдентификаторОбработки) Тогда
		ИмяПодключеннойВнешнейОбработки = ИдентификаторОбработки;
	Иначе
		ИмяВнешнейОбработки = "";
		БезопасныйРежимПодключенияОбработки = Ложь;
		Если РежимОтладки Тогда
			БезопасныйРежимПодключенияОбработки = Истина;
		КонецЕсли;
		ДопПараметры = Новый Структура;
		ДопПараметры.Вставить("ИдентификаторОбработки", ИдентификаторОбработки);
		ДопПараметры.Вставить("БезопасныйРежимПодключения", РежимОтладки);
		
		Если ЕстьПоддержкаАсинхронныхВызовов Тогда
			Оповещение = Вычислить("Новый ОписаниеОповещения(""ПодключитьВнешнююОбработкуСервер"", ЭтаФорма, ДопПараметры)");
			Выполнить("НачатьПомещениеФайла(Оповещение, , ПолныйПутьФайлаОбработки, Ложь);");// Адрес действителен только на 1 вызов сервера!
		Иначе
			АдресХранилища = "";
			ПоместитьФайл(АдресХранилища, ПолныйПутьФайлаОбработки, , Ложь); 
			ПодключитьВнешнююОбработкуСервер(Неопределено, АдресХранилища, ПолныйПутьФайлаОбработки, ДопПараметры);
		КонецЕсли;
		
		ИмяПодключеннойВнешнейОбработки = ИмяВнешнейОбработки;
		
	КонецЕсли;
	
	Возврат ИмяПодключеннойВнешнейОбработки;
	
КонецФункции

&НаСервере
Процедура ПодключитьВнешнююОбработкуСервер(Результат, АдресХранилища, ВыбранноеИмяФайла, Параметры)
	Перем ИдентификаторОбработки, БезопасныйРежимПодключения;
	
	БезопасныйРежимПодключения = Ложь;
	ИдентификаторОбработки = "";
	
	Если ТипЗнч(Параметры) = Тип("Структура") Тогда 
		БезопасныйРежимПодключения = ?(Параметры.Свойство("БезопасныйРежимПодключения"), Параметры.БезопасныйРежимПодключения, БезопасныйРежимПодключения);
		ИдентификаторОбработки = ?(Параметры.Свойство("ИдентификаторОбработки"), Параметры.ИдентификаторОбработки, ИдентификаторОбработки);
	Иначе
		ИдентификаторОбработки = Параметры;
	КонецЕсли;
	
	ИмяВнешнейОбработки = ВнешниеОбработки.Подключить(АдресХранилища, ИдентификаторОбработки, БезопасныйРежимПодключения); // подключаю не в безопасном режиме
	
КонецПроцедуры



&НаСервереБезКонтекста
Функция ЕстьВстроеннаяОбработкаСервер(ИдентификаторОбработки)
	Возврат Метаданные.Обработки.Найти(ИдентификаторОбработки) <> Неопределено;
КонецФункции

&НаСервереБезКонтекста
Функция ПредставлениеВстроеннойОбработкиСервер(ИдентификаторОбработки)
	Возврат Метаданные.Обработки.Найти(ИдентификаторОбработки).Представление();
КонецФункции


&НаКлиенте
Функция ПолучитьТестовыеСлучаи(ТестовыйСлучайОбъект, ТестовыйСлучайИмяФайла)
	Попытка
		МассивТестовыхМетодов = ТестовыйСлучайОбъект.ПолучитьСписокТестов(ЭтаФорма);
	Исключение
		ТекстОшибки = ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()) ;
		
		// TODO если не использовать переменную ниже, а поставить вызов метода в условие, то будет глюк - внутрь условия не попадаем !
		ЕстьОшибка_МетодОбъектаНеОбнаружен = ЕстьОшибка_МетодОбъектаНеОбнаружен(ТекстОшибки, "ПолучитьСписокТестов");
		Если НЕ ЕстьОшибка_МетодОбъектаНеОбнаружен Тогда
			ТекстОшибки = НСтр("ru = 'Тестовый случай не загружен: %1
									 |  Ошибка получения списка тестовых случаев: %2'");
			ВывестиОшибку(СтрЗаменить(СтрЗаменить(ТекстОшибки, "%1", ТестовыйСлучайИмяФайла), "%2", ОписаниеОшибки()));
		КонецЕсли;
		
		Возврат Неопределено;
	КонецПопытки;
	
	Если ТипЗнч(МассивТестовыхМетодов) <> Тип("Массив") Тогда
		ТекстОшибки = НСтр("ru = 'Тест не загружен: %1
								 |  Ошибка получения списка тестовых случаев: вместо массива
								 |имен тестовых случаев получен объект <%2>'");
		ВывестиОшибку(СтрЗаменить(СтрЗаменить(ТекстОшибки, "%1", ТестовыйСлучайИмяФайла), "%2", Строка(ТипЗнч(МассивТестовыхМетодов))));
		
		Возврат Неопределено;
	КонецЕсли;
	
	Возврат МассивТестовыхМетодов;
КонецФункции

&НаКлиенте
Процедура ВыполнитьВыделенныеТесты(ВызыватьИсключение = Ложь)
	
	ОчиститьСообщения();
	
	МассивИдентификаторовСтрок = Элементы.ДеревоТестов.ВыделенныеСтроки;
	
	_ВыполнитьВыбранныеТесты(МассивИдентификаторовСтрок, ВызыватьИсключение);
	
КонецПроцедуры

&НаКлиенте
Функция _ВыполнитьВыбранныеТесты(МассивИдентификаторовСтрок, ВызыватьИсключение = Ложь)
	ПередВыполнениемТестовСервер();
	
	датаНачалаТестирования = ТекущаяДата();
	
	НовыйМассивИдентификаторовСтрок = Новый Массив;
	ПолучитьТолькоТестовыеМетодыИзДерева(МассивИдентификаторовСтрок, НовыйМассивИдентификаторовСтрок);
	
	ИнициализироватьИндикаторВыполнения(НовыйМассивИдентификаторовСтрок.Количество());
	
	Если Объект.СлучайныйПорядокВыполнения Тогда
		НовыйМассивИдентификаторовСтрок = ПеремешатьМассив(НовыйМассивИдентификаторовСтрок);
	КонецЕсли;
	
	СостояниеРезультат = _ВыполнитьТесты(НовыйМассивИдентификаторовСтрок, ВызыватьИсключение);
	
	ОбновитьСостояниеВыбранныхСтрок(МассивИдентификаторовСтрок);
	ОбновитьСостояниеРодителейВыбранныхСтрок(МассивИдентификаторовСтрок);
	СвернутьУспешныеСтрокиДереваИРазвернутьНеуспешные(Объект.ДеревоТестов.ПолучитьЭлементы(), Истина);
	
	ОбработатьОтчетТестирования(датаНачалаТестирования);
	
	Возврат СостояниеРезультат;
КонецФункции

&НаКлиенте
Процедура ОбработатьОтчетТестирования(датаНачалаТестирования)
	Перем ТипОтчетаОТестировании;
	ТипОтчетаОТестировании = ?(ПустаяСтрока(ЭтаФорма.ТипОтчетаОТестировании), "mxl", ЭтаФорма.ТипОтчетаОТестировании);
	
	табДок = ПолучитьОтчетТестирования(датаНачалаТестирования, ТипОтчетаОТестировании);
	
	имяФайлаТестирования = "лог_тестирования";
	
	файлФайлаТестирования = Новый Файл(ПутьОтчетаТестирования);
	Если файлФайлаТестирования.Существует() И НЕ файлФайлаТестирования.ЭтоКаталог() Тогда 
		имяФайлаТестирования = файлФайлаТестирования.ИмяБезРасширения;
		ПутьОтчетаТестирования = файлФайлаТестирования.Путь;
	ИначеЕсли НЕ файлФайлаТестирования.Существует() И НЕ ПустаяСтрока(файлФайлаТестирования.Расширение) Тогда 
		имяФайлаТестирования = файлФайлаТестирования.ИмяБезРасширения;
		ПутьОтчетаТестирования = файлФайлаТестирования.Путь;
	КонецЕсли;
	
	ПутьОтчетаТестирования = ДополнитьСлешВПуть(ПутьОтчетаТестирования);
	
	Если табДок <> Неопределено Тогда
		Если НЕ ПустаяСтрока(ПутьОтчетаТестирования) Тогда
			ЗаписатьФайлОтчетТестирования(табДок, имяФайлаТестирования, ТипОтчетаОТестировании);
		КонецЕсли;
		
		табДок.Показать(СтрЗаменить(НСтр("ru = 'Отчет об автоматическом тестировании, xUnitFor1C v.%1'"), "%1", ВерсияСервер()));
	Иначе // все тесты прошли
		Если НЕ ПустаяСтрока(ПутьОтчетаТестирования) Тогда
			УдалитьФайлыОтчетовТестирования(имяФайлаТестирования);
		КонецЕсли;
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Функция ПолучитьМассивИдентификаторовСтрок(СтрокаДереваТестов)
	МассивИдентификаторовСтрок = Новый Массив();
	Для каждого ЭлементДерева Из СтрокаДереваТестов.ПолучитьЭлементы() Цикл
		МассивИдентификаторовСтрок.Добавить(ЭлементДерева.ПолучитьИдентификатор());
	КонецЦикла;
	Возврат МассивИдентификаторовСтрок;
КонецФункции

&НаКлиенте
Процедура ПолучитьТолькоТестовыеМетодыИзДерева(МассивИдентификаторовСтрок, МассивДляДобавления)
	Для каждого Идентификатор из МассивИдентификаторовСтрок Цикл
		СтрокаДереваТестов = Объект.ДеревоТестов.НайтиПоИдентификатору(Идентификатор);
		
		Если СтрокаДереваТестов.ТипСтроки = ЗначенияТиповСтроки.ТестовыйМетод Тогда
			
			Если МассивДляДобавления.Найти(Идентификатор) = Неопределено Тогда
				МассивДляДобавления.Добавить(Идентификатор);
			КонецЕсли;
			
		Иначе
			НовыйМассив = ПолучитьМассивИдентификаторовСтрок(СтрокаДереваТестов);
			ПолучитьТолькоТестовыеМетодыИзДерева(НовыйМассив, МассивДляДобавления);
			
		КонецЕсли;
		
	КонецЦикла;
КонецПроцедуры

&НаКлиенте
Функция ОбновитьСостояниеВыбранныхСтрок(ВыбранныеСтроки)
	
	СостояниеВыполнение = ЗначенияСостоянияТестов.НеВыполнялся;
	Для Каждого СтрокаИлиИдентификатор Из ВыбранныеСтроки Цикл
		
		Если ТипЗнч(СтрокаИлиИдентификатор) = Тип("ДанныеФормыЭлементДерева") Тогда
			Строка = СтрокаИлиИдентификатор;
		Иначе
			Строка = Объект.ДеревоТестов.НайтиПоИдентификатору(СтрокаИлиИдентификатор);
		КонецЕсли;
		
		Если Строка.ТипСтроки <> ЗначенияТиповСтроки.ТестовыйМетод Тогда
			Строка.Состояние = ОбновитьСостояниеВыбранныхСтрок(Строка.ПолучитьЭлементы());
		КонецЕсли;
		
		ЗапомнитьСамоеХудшееСостояние(СостояниеВыполнение, Строка.Состояние);
		
	КонецЦикла;
	
	Возврат СостояниеВыполнение;
	
КонецФункции

&НаКлиенте
Процедура ОбновитьСостояниеРодителейВыбранныхСтрок(МассивИдентификаторовСтрок)
	
	Для Каждого Идентификатор Из МассивИдентификаторовСтрок Цикл
		ИзменитьСостояниеВыполненияРодителей(Объект.ДеревоТестов.НайтиПоИдентификатору(Идентификатор));
	КонецЦикла;
	
КонецПроцедуры


&НаКлиентеНаСервереБезКонтекста
Функция ПеремешатьМассив(ИсходныйМассив)
	
	Результат = Новый Массив;
	Для Каждого Элемент Из ИсходныйМассив Цикл
		Результат.Добавить(Элемент);
	КонецЦикла;
	
	// алгоритм перемешивания взят из книги Кнута "Искусство программирования" т.2
	ГСЧ = Новый ГенераторСлучайныхЧисел();
	ПоследнийИндекс = Результат.ВГраница();
	Для Индекс = 1 По ПоследнийИндекс Цикл
		ТекущийИндекс = ПоследнийИндекс - Индекс + 1;
		НовыйИндекс = ГСЧ.СлучайноеЧисло(0, ТекущийИндекс);
		Если НовыйИндекс <> ТекущийИндекс Тогда
			Значение = Результат[НовыйИндекс];
			Результат[НовыйИндекс] = Результат[ТекущийИндекс];
			Результат[ТекущийИндекс] = Значение;
		КонецЕсли;
	КонецЦикла;
	
	Возврат Результат;
	
КонецФункции

&НаКлиенте
Процедура ЗаписатьФайлОтчетТестирования(табДок, имяФайлаТестирования, ТипОтчетаОТестировании)
	Если ТипОтчетаОТестировании = "xml" Тогда
		ЗаписьXML = Новый ЗаписьXML;
		Попытка
			ЗаписьXML.ОткрытьФайл(ПутьОтчетаТестирования + имяФайлаТестирования + "."+ТипОтчетаОТестировании);
			ЗаписьXML.ЗаписатьОбъявлениеXML();
			ЗаписьXML.ЗаписатьБезОбработки(табДок.ПолучитьТекст());
			ЗаписьXML.Закрыть();
		Исключение
		КонецПопытки;
	Иначе
		
		Попытка
			табДок.Записать(ПутьОтчетаТестирования + имяФайлаТестирования + "." + ТипОтчетаОТестировании, ТипФайлаТабличногоДокумента[ТипОтчетаОТестировании]);
		Исключение
		КонецПопытки;
		
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура УдалитьФайлыОтчетовТестирования(имяФайлаТестирования)
	
	Попытка
		УдалитьФайлы(ПутьОтчетаТестирования + "\" + имяФайлаТестирования + ".xlsx");
	Исключение
	КонецПопытки;
	
	Попытка
		УдалитьФайлы(ПутьОтчетаТестирования + "\" + имяФайлаТестирования + ".mxl");
	Исключение
	КонецПопытки;
	
КонецПроцедуры

&НаКлиенте
Функция _ВыполнитьТесты(МассивИдентификаторовСтрок, ВызыватьИсключение = Ложь) Экспорт
	СостояниеРезультат = ЗначенияСостоянияТестов.НеВыполнялся;
	Для каждого ИдентификаторСтроки из МассивИдентификаторовСтрок Цикл
		ОбработкаПрерыванияПользователя();
		
		ВсегоТестов = ВсегоТестов + 1;
		СтрокаДереваТестов = Объект.ДеревоТестов.НайтиПоИдентификатору(ИдентификаторСтроки); // обход бага платформы
		НовоеСостояние = ВыполнитьТестВЗависимостиОтВидаТеста_МодульИлиФорма(СтрокаДереваТестов, СписокОшибок, ВызыватьИсключение);
		
		ЗапомнитьСамоеХудшееСостояние(СостояниеРезультат, НовоеСостояние);
	КонецЦикла;
	
	Возврат СостояниеРезультат;
КонецФункции

&НаКлиенте
Функция ВыполнитьТестВЗависимостиОтВидаТеста_МодульИлиФорма(СтрокаДереваТестов, СписокОшибок, ВызыватьИсключение = Ложь)
	Если Объект.РегистрацияПрохожденияТестовВЖР Тогда
		Родитель = СтрокаДереваТестов.ПолучитьРодителя();
		ЗарегистрироватьНачалоПрохожденияТестаСервер(СтрокаДереваТестов.Имя, Родитель.ПолныйПуть);
	КонецЕсли;
	
	Если СтрокаДереваТестов.МодульИлиФорма = ЗначениеМодульИлиФорма.Форма Тогда
		СостояниеРезультат = ВыполнитьТест(СтрокаДереваТестов, ВызыватьИсключение);
	Иначе
		
		КоличествоОшибокДо = СписокОшибок.Количество();
		
		ОповеститьОСобытии("TestStart", СтрокаДереваТестов);
		СтрокаДереваТестов.ВызыватьИсключение = ВызыватьИсключение;
		СостояниеРезультат = ВыполнитьТестНаСервере(СтрокаДереваТестов.ПолучитьИдентификатор(), СписокОшибок);
		
		Если КоличествоОшибокДо <> СписокОшибок.Количество() Тогда
			ОповеститьОСобытии("TestFailed", СтрокаДереваТестов);
		Иначе
			ОповеститьОСобытии("TestPassed", СтрокаДереваТестов);
		КонецЕсли;
		
	КонецЕсли;
	
	СтрокаДереваТестов.Состояние = СостояниеРезультат;
	
	Возврат СостояниеРезультат;
КонецФункции

&НаКлиенте
Функция ВыполнитьТест(СтрокаДереваТестов, ВызыватьИсключение = Ложь) Экспорт
	Перем Тест; // специально локальная переменная для исключения циклических ссылок
	
	ИдентификаторСтроки = СтрокаДереваТестов.ПолучитьИдентификатор();
	
	ИмяТестовогоМетода = СтрокаДереваТестов.ИмяТестовогоСлучая; //Имя;
	ПолноеИмяТестовогоМетода = СтрокаДереваТестов.Имя;
	
	Если ПустаяСтрока(СтрокаДереваТестов.ТестовыйСлучайОбъектМетаданныхИмя) Тогда
		Возврат ЗначенияСостоянияТестов.НеВыполнялся;
	КонецЕсли;
	
	полныйПуть = СтрокаДереваТестов.ПолныйПуть;
	
	ТестовыйСлучайОбъектМетаданныхИмя = СтрокаДереваТестов.ТестовыйСлучайОбъектМетаданныхИмя;
	Попытка
		лИмяОбработки = ПодключитьНаСервереВнешнююОбработкуОтКлиента(полныйПуть, ТестовыйСлучайОбъектМетаданныхИмя, Объект.РежимОтладки);
	Исключение
		ТекстОшибки = НСтр("ru = 'Тест не загружен: %1 Ошибка: %2'");
		ВывестиОшибку(СтрЗаменить(СтрЗаменить(ТекстОшибки, "%1", полныйПуть), "%2", ОписаниеОшибки()));
		
		Возврат ЗначенияСостоянияТестов.Сломался;
	КонецПопытки;
	
	ТекущаяДата = ТекущаяДата();
	Попытка
		Тест = ПолучитьТестОбъект(ТестовыйСлучайОбъектМетаданныхИмя, СтрокаДереваТестов.ИмяОсновнойУправляемойФормы);
		массивТестов = ПолучитьТестовыеСлучаи(Тест, полныйПуть); // возможно, что тест сохранит у себя ссылку на браузер
		ПроверитьЗаполненность(массивТестов, НСтр("ru = 'Не удалось получить тестовые случаи'"));
	Исключение
		текстОшибки = ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()) ;
		СостояниеВыполнения = ВывестиОшибкуВыполненияТеста(ПолноеИмяТестовогоМетода, текстОшибки, СтрокаДереваТестов, ОписаниеОшибки(), ИнформацияОбОшибке());
		
		Тест = Неопределено; // для исключения циклических ссылок
		
		Возврат СостояниеВыполнения;
		
	КонецПопытки;
	
	СостояниеВыполнения = ЗначенияСостоянияТестов.НеВыполнялся;
	
	параметрыТеста = СтрокаДереваТестов.ПараметрыТеста;
	естьПараметры = СтрокаДереваТестов.ЕстьПараметрыТеста;
	
	естьТранзакция = Ложь;
	Если естьПараметры Тогда
		естьТранзакция = ?(ТипЗнч(параметрыТеста) = Тип("Структура"), параметрыТеста, параметрыТеста[0].Значение).Свойство("Транзакция", естьТранзакция)
			или ТипЗнч(естьТранзакция) = Тип("Булево") и естьТранзакция;
		Если естьТранзакция Тогда
			#Если ТолстыйКлиентОбычноеПриложение ИЛИ ТолстыйКлиентУправляемоеПриложение Тогда
				НачатьТранзакцию();
			#КонецЕсли
		КонецЕсли;
	КонецЕсли;
	
		//TODO Если первый раз запуск setup оказался неуспешным, то при повторном прогоне без перезагрузки
		// не выполнять, также не выполнять для следующих тестовых случаев.
	лСостояниеВыполнения = ВыполнитьПроцедуруТестовогоСлучая(Тест, "ПередЗапускомТеста", ПолноеИмяТестовогоМетода, СтрокаДереваТестов);
	Если лСостояниеВыполнения <> Неопределено Тогда
		СостояниеВыполнения = лСостояниеВыполнения;
	КонецЕсли;
	Если СостояниеВыполнения = ЗначенияСостоянияТестов.Сломался Тогда
		Возврат ЗначенияСостоянияТестов.Сломался;
	КонецЕсли;
	
	переданПараметрТеста = Ложь;
	параметрТеста = "";
	
	Если естьПараметры Тогда
		лПараметрыТеста = ?(ТипЗнч(параметрыТеста) = Тип("Структура"), параметрыТеста, параметрыТеста[0].Значение);
		переданПараметрТеста = лПараметрыТеста.Свойство("Параметр", параметрТеста);
		
		Если переданПараметрТеста Тогда
			строкаПараметров = "параметрТеста";
		КонецЕсли;
	КонецЕсли;
	
	ОповеститьОСобытии("TestStart", СтрокаДереваТестов);
	
	Если ВызыватьИсключение = Истина Тогда //FIXME: добавить вывод полного пути к файлу, для дальнейшего его открытия. 
		Если Объект.РежимОтладки = Ложь Тогда //Мы подключенны не в безопастном режиме, можем только выдать только исключение с полным путем к обработке,.
			СтрокаДереваТестовОтладки = ИдентификаторСтроки;
			ВызватьИсключение ПолныйПуть;
		Иначе
			ВызватьИсключение "ПередЗапускомТеста " + ИмяТестовогоМетода + " файл:"+ПолныйПуть;
		КонецЕсли;
	КонецЕсли;
	
	Попытка
		
		Выполнить("Тест." + ИмяТестовогоМетода + "(" + строкаПараметров + ");");
		
		СостояниеВыполнения = ЗначенияСостоянияТестов.Прошел;
		ОповеститьОСобытии("TestPassed", СтрокаДереваТестов);
	Исключение
		текстОшибки = ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()) ; //Ошибка.Описание + ". " + Ошибка.Причина.Описание + ". Номер строки: " + Ошибка.НомерСтроки;
		
		Если ЕстьОшибка_МетодОбъектаНеОбнаружен(текстОшибки, ИмяТестовогоМетода) Тогда
			СостояниеВыполнения = ЗначенияСостоянияТестов.НеРеализован;
			ОповеститьОСобытии("TestNotImplemented", СтрокаДереваТестов);
		Иначе
			СостояниеВыполнения = ВывестиОшибкуВыполненияТеста(ПолноеИмяТестовогоМетода, текстОшибки, СтрокаДереваТестов, ОписаниеОшибки(), ИнформацияОбОшибке());
		КонецЕсли;
	КонецПопытки;
	
	лСостояниеВыполнения = ВыполнитьПроцедуруТестовогоСлучая(Тест, "ПослеЗапускаТеста", ПолноеИмяТестовогоМетода, СтрокаДереваТестов);
	Если лСостояниеВыполнения <> Неопределено Тогда
		СостояниеВыполнения = ЗапомнитьСамоеХудшееСостояние(СостояниеВыполнения, лСостояниеВыполнения); //СостояниеВыполнения = лСостояниеВыполнения;
	КонецЕсли;
	
	Если естьТранзакция Тогда
		#Если ТолстыйКлиентОбычноеПриложение ИЛИ ТолстыйКлиентУправляемоеПриложение Тогда
			Если ТранзакцияАктивна() Тогда
				ОтменитьТранзакцию();
			КонецЕсли;
		#КонецЕсли
	КонецЕсли;
	
	Тест = Неопределено; // для исключения циклических ссылок
	
	СтрокаДереваТестов = Объект.ДеревоТестов.НайтиПоИдентификатору(ИдентификаторСтроки);
	СтрокаДереваТестов.ВремяВыполнения = Число(ТекущаяДата()-ТекущаяДата);
	Возврат СостояниеВыполнения;
КонецФункции


&НаСервере
Функция ВыполнитьТестНаСервере(ИдентификаторСтрокиТестовогоМетода, СписокОшибокРез)
	Возврат Объект().ВыполнитьТест(Объект.ДеревоТестов.НайтиПоИдентификатору(ИдентификаторСтрокиТестовогоМетода), СписокОшибокРез);
КонецФункции

&НаКлиенте
Функция ВыполнитьПроцедуруТестовогоСлучая(Тест, ИмяПроцедуры, ИмяТестовогоСлучая, СтрокаДереваТестов)
	СостояниеВыполнения = Неопределено;
	
	Попытка
		Выполнить("Тест." + ИмяПроцедуры + "();");
	Исключение
		Ошибка = ИнформацияОбОшибке();
		текстОшибки = ПодробноеПредставлениеОшибки(Ошибка) ;
		
		Если НЕ ЕстьОшибка_МетодОбъектаНеОбнаружен(текстОшибки, ИмяПроцедуры) Тогда
			СостояниеВыполнения = ВывестиОшибкуВыполненияТеста(ИмяТестовогоСлучая, текстОшибки, СтрокаДереваТестов, ОписаниеОшибки(), ИнформацияОбОшибке());
		КонецЕсли;
	КонецПопытки;
	
	Возврат СостояниеВыполнения;
	
КонецФункции

&НаСервере
Процедура ПередВыполнениемТестовСервер()
	СписокОшибок = Новый СписокЗначений;
	ВсегоТестов = 0;
КонецПроцедуры

// Возвращаем Неопределено, если все тесты прошли
&НаСервере
Функция ПолучитьОтчетТестирования(датаНачалаТестирования, ТипОтчета =  "mxl") Экспорт
	
	Если ТипОтчета = "xml" Тогда
		
		ДеревоТестов = ДанныеФормыВЗначение(Объект.ДеревоТестов, Тип("ДеревоЗначений"));
		Текст = Объект().ВывестиОтчетТестированияВФорматеJunitXML(ДеревоТестов, датаНачалаТестирования, ВсегоТестов, СписокОшибок);
		
		ТабДокумент = Новый ТекстовыйДокумент;
		ТабДокумент.УстановитьТекст(Текст);
		Возврат ТабДокумент;
		
	КонецЕсли;
	
	КоличествоОшибок = СписокОшибок.Количество();
	Если КоличествоОшибок = 0 Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	ПрошлоВремени = ТекущаяДата() - датаНачалаТестирования;
	
	ТабДок = Новый ТабличныйДокумент;
	макет = Объект().ПолучитьМакет("ОтчетТестирования");
	
	Секция = макет.ПолучитьОбласть("Заголовок");
	Секция.Параметры.ВсегоТестов = ВсегоТестов;
	Секция.Параметры.КоличествоОшибок = СписокОшибок.Количество();
	Секция.Параметры.ПрошлоВремени = ПрошлоВремени;
	Секция.Параметры.ПрошлоВремениСтрока = формат(Дата(1, 1, 1) + ПрошлоВремени, "ДЛФ=T");
	Секция.Параметры.ВремяВыполненияОдногоТеста = ?(ВсегоТестов = 0, "", Формат(ПрошлоВремени / ВсегоТестов * 1000, "ЧЦ=15; ЧДЦ=2"));
	ТабДок.Вывести(Секция);
	
	ТабДок.НачатьАвтогруппировкуСтрок();
	
	Для Каждого Ошибка Из СписокОшибок Цикл
		ВывестиОшибкуВМакет(ТабДок, макет, Ошибка.Значение);
	КонецЦикла;
	
	ТабДок.ЗакончитьАвтогруппировкуСтрок();
	
	ТабДок.ОтображатьСетку = Ложь;
	ТабДок.Защита = Ложь;
	ТабДок.ТолькоПросмотр = Ложь;
	ТабДок.ОтображатьЗаголовки = Ложь;
	
	Возврат ТабДок;
КонецФункции

Процедура ВывестиОшибкуВМакет(ТабДок, макет, _Ошибка)
	Секция = макет.ПолучитьОбласть("НаименованиеНабора");
	ЗаполнитьЗначенияСвойств(Секция.Параметры, _Ошибка);
	ТабДок.Вывести(Секция, 1);
	
	Секция = макет.ПолучитьОбласть("НаименованиеТеста");
	ЗаполнитьЗначенияСвойств(Секция.Параметры, _Ошибка);
	ТабДок.Вывести(Секция, 2);
	
	Если _Ошибка.ЕстьПараметрыТеста Тогда
		Секция = макет.ПолучитьОбласть("ПараметрыТеста");
		ЗаполнитьЗначенияСвойств(Секция.Параметры, _Ошибка);
		ТабДок.Вывести(Секция, 2);
	КонецЕсли;
	
	ИнфоОшибки = _Ошибка.ИнфоОшибки;
	Пока ИнфоОшибки <> Неопределено Цикл
		Секция = макет.ПолучитьОбласть("R1C1:R4C3");
		Секция.Область("R1C3").Текст = ИнфоОшибки.ИмяМодуля + ", строка № "+ИнфоОшибки.НомерСтроки;
		Секция.Область("R2C3").Текст = ИнфоОшибки.ИсходнаяСтрока;
		Секция.Область("R3C3").Текст = ИнфоОшибки.Описание;
		
		ТабДок.Вывести(Секция, 3);
		
		ИнфоОшибки = ИнфоОшибки.Причина;
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Функция ВывестиОшибкуВыполненияТеста(ПредставлениеТестовогоСлучая, текстОшибки, СтрокаДереваТестов, СообщениеОбОшибке, ИнфоОшибки)
	
	СостояниеВыполнения = ЗначенияСостоянияТестов.Сломался;
	
	СтруктураОшибки = Новый Структура;
	
	стИнфоОшибки = Новый Структура("ИмяМодуля,ИсходнаяСтрока,НомерСтроки,Описание");
	ЗаполнитьЗначенияСвойств(стИнфоОшибки, ИнфоОшибки);
	стИнфоОшибки.Вставить("Причина",  Неопределено);
	
	стИнфоОшибкиЦикл = стИнфоОшибки;
	ИнфоОшибки = ИнфоОшибки.Причина;
	Пока ИнфоОшибки <> Неопределено Цикл  // Артур TODO
		стИнфоОшибкиЦикл.Причина = Новый Структура("ИмяМодуля,ИсходнаяСтрока,НомерСтроки,Описание");
		стИнфоОшибкиЦикл = стИнфоОшибкиЦикл.Причина;
		ЗаполнитьЗначенияСвойств(стИнфоОшибкиЦикл, ИнфоОшибки);
		стИнфоОшибкиЦикл.Вставить("Причина",  Неопределено);
		
		ИнфоОшибки = ИнфоОшибки.Причина;
	КонецЦикла;
	
	Родитель = Объект.ДеревоТестов.НайтиПоИдентификатору(СтрокаДереваТестов.ПолучитьИдентификатор()).ПолучитьРодителя();
	СтруктураОшибки.Вставить("ИмяТестовогоНабора", Родитель.Имя);
	СтруктураОшибки.Вставить("ИмяТестовогоСлучая", ПредставлениеТестовогоСлучая);
	
	СтруктураОшибки.Вставить("Описание", текстОшибки);
	
	СтруктураОшибки.Вставить("СообщениеОбОшибке",     СообщениеОбОшибке);
	СтруктураОшибки.Вставить("ИнфоОшибки",            стИнфоОшибки);
	СтруктураОшибки.Вставить("ЕстьПараметрыТеста",    СтрокаДереваТестов.ЕстьПараметрыТеста);
	СтруктураОшибки.Вставить("ПараметрыТеста",        СтрокаДереваТестов.ПараметрыТеста);
	СтруктураОшибки.Вставить("ПолныйПуть",            СтрокаДереваТестов.ПолныйПуть);
	СтруктураОшибки.Вставить("УИДСтрокиДерева",       СтрокаДереваТестов._guid);
	
	СписокОшибок.Добавить(СтруктураОшибки);
	
	НужныйТекстОшибки = ВывестиОшибку(СтруктураОшибки);
	
	ЗарегистрироватьОшибкуТестаСервер(НужныйТекстОшибки, ПредставлениеТестовогоСлучая, СтрокаДереваТестов.ПолныйПуть);
	
	ОповеститьОСобытии("TestFailed", СтрокаДереваТестов);
	
	Возврат СостояниеВыполнения;
	
КонецФункции

&НаКлиенте
Функция ОповеститьОСобытии(ИмяСобытия, Аргумент1 = Неопределено)
	рез = Истина;
	Если MiddleMan <> Неопределено Тогда
		Попытка
			Выполнить("MiddleMan." + ИмяСобытия + "(Аргумент1);");
		Исключение
			рез = Ложь;
		КонецПопытки;
	КонецЕсли;
	
	Оповестить("xUnitFor1C." + ИмяСобытия, Аргумент1);
	
	Возврат рез;
КонецФункции

&НаКлиенте
Функция ЗапомнитьСамоеХудшееСостояние(ТекущееСостояние, НовоеСостояние)
	ТекущееСостояние = Макс(ТекущееСостояние, НовоеСостояние); // TODO некоторый хак, который зависит от порядка заполнения в мЗначенияСостоянияТестов
	Возврат ТекущееСостояние;
КонецФункции

&НаСервере
Процедура ЗарегистрироватьНачалоПрохожденияТестаСервер(ИмяТестовогоСлучая, ПолныйПуть)
	Объект().ЗарегистрироватьНачалоПрохожденияТеста(ИмяТестовогоСлучая, ПолныйПуть);
КонецПроцедуры

&НаСервере
Процедура ЗарегистрироватьОшибкуТестаСервер(НужныйТекстОшибки, ИмяТестовогоСлучая, ПолныйПуть)
	Объект().ЗарегистрироватьОшибкуТеста(НужныйТекстОшибки, ИмяТестовогоСлучая, ПолныйПуть);
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ИСТОРИЯ ВЫБОРА ТЕСТОВ

&НаСервере
Процедура ДобавитьВИсториюКонтекст(ИмяИстории, Путь)
	
	ДобавитьВИсторию(ЭтаФорма, ИмяИстории, Путь, ДлинаИстории, "Подключаемый_КомандаЗагрузитьТестыИзИстории");
	
КонецПроцедуры

&НаСервереБезКонтекста
Процедура ДобавитьВИсторию(Форма, ИмяИстории, Путь, ДлинаИстории, Действие)
	
	История = Форма[ИмяИстории];
	Элементы = Форма.Элементы;
	Команды = Форма.Команды;
	
	Если ТипЗнч(Путь) <> Тип("Массив") Тогда
		Пути = Новый Массив;
		Пути.Добавить(Путь);
	Иначе
		Пути = Путь;
	КонецЕсли;
	
	Для Каждого ВыбранныйПуть Из Пути Цикл
		Элемент = История.НайтиПоЗначению(ВыбранныйПуть);
		Если Элемент = Неопределено Тогда
			История.Вставить(0, ВыбранныйПуть);
			Если История.Количество() = ДлинаИстории + 1 Тогда
				История.Удалить(ДлинаИстории);
			Иначе
				Команда = Команды.Добавить(ИмяИстории + Формат(История.Количество() - 1, "ЧН=0; ЧГ=0"));
				Команда.Действие = Действие;
				ПунктМеню = Элементы.Добавить(Команда.Имя, Тип("КнопкаФормы"), Элементы[ИмяИстории]);
				ПунктМеню.ИмяКоманды = Команда.Имя;
			КонецЕсли;
		Иначе
			Индекс = История.Индекс(Элемент);
			Если Индекс > 0 Тогда
				История.Сдвинуть(Элемент, -Индекс);
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;
	
	Для Каждого Элемент Из История Цикл
		ПунктМеню = Элементы[ИмяИстории + Формат(История.Индекс(Элемент), "ЧН=0; ЧГ=0")];
		ПунктМеню.Заголовок = Элемент.Значение;
	КонецЦикла;
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// НАСТРОЙКИ

&НаКлиенте
Процедура УстановитьЗначенияНастроекПоУмолчанию()
	
	Объект.РегистрацияПрохожденияТестовВЖР = Ложь;
	Объект.РегистрацияОшибокТестовВЖР = Ложь;
	Объект.РежимОтладки = Ложь;
	Объект.СлучайныйПорядокВыполнения = Ложь;
	
	Пути.Очистить();
	
КонецПроцедуры

&НаСервере
Процедура СохранитьНастройки()
	
	Настройки = Новый Структура;
	Настройки.Вставить("РегистрацияПрохожденияТестовВЖР", Объект.РегистрацияПрохожденияТестовВЖР);
	Настройки.Вставить("РегистрацияОшибокТестовВЖР", Объект.РегистрацияОшибокТестовВЖР);
	Настройки.Вставить("РежимОтладки", Объект.РежимОтладки);
	Настройки.Вставить("СлучайныйПорядокВыполнения", Объект.СлучайныйПорядокВыполнения);
	
	Настройки.Вставить("Пути", Пути.ВыгрузитьЗначения());
	
	Настройки.Вставить("ДлинаИстории", ДлинаИстории);
	Настройки.Вставить("ИсторияВыбораМест", ИсторияВыбораМест.ВыгрузитьЗначения());
	Настройки.Вставить("ИсторияВыбораФайлов", ИсторияВыбораФайлов.ВыгрузитьЗначения());
	
	ХранилищеОбщихНастроек.Сохранить("xUnitFor1C",, Настройки);
	
КонецПроцедуры

&НаСервере
Процедура ВосстановитьНастройки()
	
	ДлинаИсторииПоУмолчанию = 10;
	
	Настройки = ХранилищеОбщихНастроек.Загрузить("xUnitFor1C");
	Если ТипЗнч(Настройки) = Тип("Структура") Тогда
		
		Настройки.Свойство("РегистрацияПрохожденияТестовВЖР", Объект.РегистрацияПрохожденияТестовВЖР);
		Настройки.Свойство("РегистрацияОшибокТестовВЖР", Объект.РегистрацияОшибокТестовВЖР);
		Настройки.Свойство("РежимОтладки", Объект.РежимОтладки);
		Настройки.Свойство("СлучайныйПорядокВыполнения", Объект.СлучайныйПорядокВыполнения);
		
		Если Настройки.Свойство("Пути") Тогда
			Пути.ЗагрузитьЗначения(Настройки.Пути);
		КонецЕсли;
		
		Если Не Настройки.Свойство("ДлинаИстории", ДлинаИстории) Или Не ЗначениеЗаполнено(ДлинаИстории) Тогда
			ДлинаИстории = ДлинаИсторииПоУмолчанию;
		КонецЕсли;
		Если Настройки.Свойство("ИсторияВыбораМест") Тогда
			ДобавитьВИсториюКонтекст(Элементы.ИсторияВыбораМест.Имя, Настройки.ИсторияВыбораМест);
		КонецЕсли;
		Если Настройки.Свойство("ИсторияВыбораФайлов") Тогда
			ДобавитьВИсториюКонтекст(Элементы.ИсторияВыбораФайлов.Имя, Настройки.ИсторияВыбораФайлов);
		КонецЕсли;
	Иначе
		ДлинаИстории = ДлинаИсторииПоУмолчанию;
	КонецЕсли;
	
	Элементы.КомандаРегистрацияПрохожденияТестовВЖР.Пометка = Объект.РегистрацияПрохожденияТестовВЖР;
	Элементы.КомандаРегистрацияОшибокТестовВЖР.Пометка = Объект.РегистрацияОшибокТестовВЖР;
	Элементы.КомандаСлучайныйПорядокВыполнения.Пометка = Объект.СлучайныйПорядокВыполнения;
	Элементы.КомандаВключитьРежимОтладки.Пометка = Объект.РежимОтладки;
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ И ФУНКЦИИ ОБЩЕГО НАЗНАЧЕНИЯ

&НаКлиенте
Функция ФорматДСО(ДопСообщениеОшибки)
	Если ДопСообщениеОшибки = "" Тогда
		Возврат "";
	КонецЕсли;
	
	Возврат Символы.ПС + ДопСообщениеОшибки;
КонецФункции

&НаКлиенте
Функция Новый_НаборТестов() Экспорт
	Возврат Новый Массив;
КонецФункции

&НаКлиенте
Функция НаборТестов_Добавить(НаборТестов, ИмяТестовогоСлучая, П1 = Неопределено, П2 = Неопределено, П3 = Неопределено,
                             П4 = Неопределено, П5 = Неопределено, П6 = Неопределено, П7 = Неопределено) Экспорт
	НаборТестов.Добавить(ИмяТестовогоСлучая);
	
	Возврат НаборТестов;
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Функция ЕстьОшибка_МетодОбъектаНеОбнаружен(текстОшибки, имяМетода)
	Возврат Найти(ТекстОшибки, "Метод объекта не обнаружен (" + ИмяМетода + ")") > 0
		Или Найти(ТекстОшибки, "Object method not found (" + ИмяМетода + ")") > 0;
КонецФункции

// Функция ДополнитьСлешВПуть
//
// Параметры:
// ИмяКаталога
//
// Описание:
// Функция дополняет и возвращает слеш в путь в конец строки, если он отсутствует
//
&НаКлиенте
Функция ДополнитьСлешВПуть(Знач Каталог)
	разделитель = "\";
	
	СисИнфо = Новый СистемнаяИнформация;
	Если Найти(Строка(СисИнфо.ТипПлатформы), "Linux")>0 Тогда
		разделитель = "/";
	КонецЕсли;
		
	Если Прав(Каталог, 1) <> разделитель Тогда
		Каталог = Каталог + разделитель;
	КонецЕсли;
	Возврат Каталог;
КонецФункции


// }

// { блок кода для переоткрытия формы

&НаСервере
Функция ИмяФайлаВнешнейОбработки_ПерезагрузитьФорму()
	Если ЭтоВстроеннаяОбработка Тогда
		Возврат МетаданныеИмяСервер();
	КонецЕсли;
	Возврат Объект().ИспользуемоеИмяФайла;
КонецФункции

//}



// Экспорт нужен для тестов
&НаКлиенте
Функция  ОткрытьИнструмент(ИмяИнструмента) Экспорт
	рез = Неопределено;
	Если Не ЭтоВстроеннаяОбработка Тогда
		Файл = Новый Файл(ИспользуемоеИмяФайла());
		Если НЕ Файл.Существует() Тогда
			Сообщить(НСтр("ru = 'Не удалось найти путь к xddTestRunner'"));
			Сообщить(СтрЗаменить(НСтр("ru = 'Не удалось открыть инструмент <%1>'"), "%1", ИмяИнструмента));
			Возврат рез;
		КонецЕсли;
		ПутьВнешнихИнструментов = Файл.Путь;
		
		ПутьИнструмента = ПутьВнешнихИнструментов + ИмяИнструмента + ".epf";
		Файл = Новый Файл(ПутьИнструмента);
		Если Не Файл.Существует() Тогда
			Сообщить(СтрЗаменить(СтрЗаменить(НСтр("ru = 'Инструмент <%1> не найден в каталоге <%2>'"), "%1", ИмяИнструмента), "%2", ПутьИнструмента));
			Возврат рез;
		КонецЕсли;
		ИдентификаторИнструмента = Строка(Новый УникальныйИдентификатор());
		
		ИмяОбработки = ПодключитьНаСервереВнешнююОбработкуОтКлиента(ПутьИнструмента, ИдентификаторИнструмента);
	Иначе
		ИмяОбработки = ИмяИнструмента;
		ИдентификаторИнструмента = ИмяИнструмента;
		Если НЕ ЕстьВстроеннаяОбработкаСервер(ИмяОбработки) Тогда
			Сообщить(СтрЗаменить(НСтр("ru = 'Инструмент/встроенная обработка <%1> не найден в конфигурации'"), "%1", ИмяИнструмента));
			Возврат рез;
		КонецЕсли;
	КонецЕсли;
	
	ИмяОсновнойУправляемойФормы = ПолучитьИмяОсновнойУправляемойФормы(ИмяОбработки);
	Если ИмяОсновнойУправляемойФормы = "" Тогда
		Сообщить(СтрЗаменить(НСтр("ru = 'Инструмент <%1>: не удалось вычислить основную управляемую форму по метаданным!'"), "%1", ИмяИнструмента));
		Возврат рез;
	КонецЕсли;
	
	ФормаИнструмента = ПолучитьТестОбъект(ИдентификаторИнструмента, имяОсновнойУправляемойФормы);
	Если ФормаИнструмента = Неопределено Тогда
		Сообщить(СтрЗаменить(НСтр("ru = 'Инструмент <%1>: не удалось получить основную управляемую форму!'"), "%1", ИмяИнструмента));
		Возврат рез;
	КонецЕсли;
	ОткрытьФорму(ФормаИнструмента);
	Возврат ФормаИнструмента;
КонецФункции


&НаКлиенте
Функция ИспользуемоеИмяФайла() Экспорт
	Возврат ИспользуемоеИмяФайлаСервер();
КонецФункции

&НаСервере
Функция ИспользуемоеИмяФайлаСервер()
	ОсновнойОбъект = Объект();
	НайденноеИмяФайла = Неопределено;
	Выполнить("НайденноеИмяФайла = ОсновнойОбъект.ИспользуемоеИмяФайла;");
	
	Возврат НайденноеИмяФайла;
КонецФункции


&НаКлиенте
Функция МетаданныеИмя() Экспорт
	Возврат МетаданныеИмяСервер();
КонецФункции

&НаСервере
Функция МетаданныеИмяСервер()
	ОсновнойОбъект = Объект();
	НайденноеИмя = ОсновнойОбъект.Метаданные().Имя;
	
	Возврат НайденноеИмя;
КонецФункции



&НаКлиенте
Процедура ИнициализироватьИндикаторВыполнения(КоличествоТестовыхСлучаев = 0)
	
	Элементы.ИндикаторВыполнения.МаксимальноеЗначение = КоличествоТестовыхСлучаев;
	ИндикаторВыполнения = 0; //Элементы.ИндикаторВыполнения.Значение = 1;
	Элементы.ИндикаторВыполнения.ЦветРамки = Новый Цвет(0, 174, 0); // Зеленый.
	
	ОбновитьОтображениеДанных();
КонецПроцедуры 

&НаКлиентеНаСервереБезКонтекста
// Сравнить две строки версий.
//
// Параметры
//  СтрокаВерсии1  - Строка - номер версии в формате РР.{П|ПП}.ЗЗ.СС
//  СтрокаВерсии2  - Строка - второй сравниваемый номер версии
//
// Возвращаемое значение:
//   Число   - больше 0, если СтрокаВерсии1 > СтрокаВерсии2; 0, если версии равны.
//
Функция СравнитьВерсии(Знач СтрокаВерсии1, Знач СтрокаВерсии2) Экспорт
	
	Строка1 = ?(ПустаяСтрока(СтрокаВерсии1), "0.0.0.0", СтрокаВерсии1);
	Строка2 = ?(ПустаяСтрока(СтрокаВерсии2), "0.0.0.0", СтрокаВерсии2);
	Версия1 = РазложитьСтрокуВМассивПодстрок(Строка1, ".");
	Если Версия1.Количество() <> 4 Тогда
		ВызватьИсключение СтрЗаменить(НСтр("ru = 'Неправильный формат параметра СтрокаВерсии1: %1'"), "%1", СтрокаВерсии1);
	КонецЕсли;
	Версия2 = РазложитьСтрокуВМассивПодстрок(Строка2, ".");
	Если Версия2.Количество() <> 4 Тогда
		ВызватьИсключение СтрЗаменить(НСтр("ru = 'Неправильный формат параметра СтрокаВерсии2: %1'"), "%1", СтрокаВерсии2);
	КонецЕсли;
	
	Результат = 0;
	Для Разряд = 0 По 3 Цикл
		Результат = Число(Версия1[Разряд]) - Число(Версия2[Разряд]);
		Если Результат <> 0 Тогда
			Возврат Результат;
		КонецЕсли;
	КонецЦикла;
	Возврат Результат;
	
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Функция РазложитьСтрокуВМассивПодстрок(Знач Строка, Знач Разделитель = ",", Знач ПропускатьПустыеСтроки = Неопределено, СокращатьНепечатаемыеСимволы = Ложь) Экспорт
	
	Результат = Новый Массив;
	
	// для обеспечения обратной совместимости
	Если ПропускатьПустыеСтроки = Неопределено Тогда
		ПропускатьПустыеСтроки = ?(Разделитель = " ", Истина, Ложь);
		Если ПустаяСтрока(Строка) Тогда 
			Если Разделитель = " " Тогда
				Результат.Добавить("");
			КонецЕсли;
			Возврат Результат;
		КонецЕсли;
	КонецЕсли;
	//
	
	Позиция = Найти(Строка, Разделитель);
	Пока Позиция > 0 Цикл
		Подстрока = Лев(Строка, Позиция - 1);
		Если Не ПропускатьПустыеСтроки Или Не ПустаяСтрока(Подстрока) Тогда
			Если СокращатьНепечатаемыеСимволы Тогда
				Результат.Добавить(СокрЛП(Подстрока));
			Иначе
				Результат.Добавить(Подстрока);
			КонецЕсли;
		КонецЕсли;
		Строка = Сред(Строка, Позиция + СтрДлина(Разделитель));
		Позиция = Найти(Строка, Разделитель);
	КонецЦикла;
	
	Если Не ПропускатьПустыеСтроки Или Не ПустаяСтрока(Строка) Тогда
		Если СокращатьНепечатаемыеСимволы Тогда
			Результат.Добавить(СокрЛП(Строка));
		Иначе
			Результат.Добавить(Строка);
		КонецЕсли;
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

//}

